{   Модуль дополнения к DataCacheInMemory для работы с оригинальными номерами и не только
{ ------------------------------------------------------------------------------------- }
unit n_DataCacheAddition;

interface

uses Classes, Types, SysUtils, IBDataBase, IBQuery, IBSQL, Math, Forms, SyncObjs,
     Contnrs, DateUtils, n_free_functions, v_constants, v_DataTrans,
     n_constants, n_DataSetsManager, n_LogThreads, n_DataCacheObjects;

type
//---------------------------------------------- набор списков по системам учета
  TArraySysTypeLists = Class (TArrayTypeLists)
  public
    constructor Create(fSorted: Boolean=False; fDelimiter: Boolean=False);
  end;

//--------------------------------------------------------------------- связка 2
  TSecondLink = class (TLinkLink) // LinkPtr - ссылка на узел дерева
  protected                       // DoubleLinks - связка 3 (связки 2 с товарами)
    FQty: Single;           // количество
  public
    constructor Create(pSrcID: Integer; pQty: Single; pNodePtr: Pointer=nil;
                pIsLink: Boolean=False; pHasWares: Boolean=False; pHasFilters: Boolean=False);
    property IsLinkNode    : boolean index ik8_3 read GetLinkBool write SetLinkBool; // признак узла связки 2 (конечная нода)
    property NodeHasWares  : boolean index ik8_4 read GetLinkBool write SetLinkBool; // признак наличия товаров в узле или его ветке
    property NodeHasFilters: boolean index ik8_5 read GetLinkBool write SetLinkBool; // признак наличия фильтров в узле модели
    property Qty: Single read FQty write FQty;       // количество
    function GetWareCodes: Tai;                      // must Free, массив кодов товаров связки 2
  end;

//------------------------------------------------ Модель автомобиля / мотоцикла
  TModelParams = Class  // параметры модели
  public
    pCCM       : Word;     // Тех. обьем куб.см.
    pKW        : Word;     // Мощность кВт
    pHP        : Word;     // Мощность ЛС
    pYStart    : Word;     // Год начала выпуска
    pYEnd      : Word;     // Год окончания выпуска
    pMStart    : Byte;     // Месяц начала выпуска
    pMEnd      : Byte;     // Месяц окончания выпуска
    pCylinders : Byte;     // Количество цилиндров
    pValves    : Byte;     // Количество клапанов на одну камеру сгорания
    pBodyID    : Byte;     // Код, тип кузова               (TYPEDIR=1)
    pDriveID   : Byte;     // Код, тип привода              (TYPEDIR=2)
    pEngTypeID : Byte;     // Код, тип двигателя            (TYPEDIR=3)
    pFuelID    : Byte;     // Код, тип топлива              (TYPEDIR=4)
    pFuelSupID : Byte;     // Код, система впрыска          (TYPEDIR=5)
    pBrakeID   : Byte;     // Код, тип тормозной системы    (TYPEDIR=6)
    pBrakeSysID: Byte;     // Код, Тормозная система        (TYPEDIR=7)
    pCatalID   : Byte;     // Код, Тип катализатора         (TYPEDIR=8)
    pTransID   : Byte;     // Код, Вид коробки передач      (TYPEDIR=9)
  end;

  TNodeLinks = Class;

  TModelAuto = Class (TSubVisDirItem)  // модель
  private // FID - Код модели, FName - Название модели,  FSubCode - Код TecDoc, FOrderNum - порядк.№ модели,
          // IsVisible - Признак видимости модели, FLinks - связки с маркировками двигателей, FParCode - Код модельного ряда
    FParams       : TModelParams; // параметры модели
    FNodeLinks    : TNodeLinks;   // связки 2 узлов дерева с моделью, Links.GetDoubleLinks(NodeID) - связки 3 товаров со связкой 2
    function GetIntM(const ik: T8InfoKinds): Integer;              // получить код
    procedure SetIntM(const ik: T8InfoKinds; Value: Integer);      // записать код
    function GetStrM(const ik: T8InfoKinds): String;               // получить строку
    function GetNodesExists: Boolean;                         //  наличие связок 2
    procedure ClearLinks;
  public
    CS_mlinks: TCriticalSection; // для линков
    constructor Create(pModelID, pModelTDcode, pOrdNum, pModelLineID: Integer; pName: String);
    destructor Destroy; override;
    function GetModelNodesLinks: TLinks; // must Free , список связок 2 модели из базы
    function GetModelNodesList(OnlyVisible: boolean=False; flFromBase: boolean=False): TList; // must Free, список связок 2 с узлами модели (OnlyVisible=True - только видимые ноды)
    function GetModelNodeIsSecondLink(nodeID: Integer): Boolean;   // признак узла 2-й связки
    function GetModelNodeWaresList(nodeID: Integer; withChildNodes: boolean=True; flFromBase: boolean=False): TStringList; // must Free, сорт.список товаров по узлу (Object - Pointer(ID))
    function GetModelNodeWares(nodeID: Integer; withChildNodes: boolean=True; flFromBase: boolean=False): Tai;             // must Free, массив кодов товаров по узлу
    function GetModelParamsUpdSql(mps: TModelParams): string;
    function SetModelVisible(isVis: Boolean): String;          // Изменить видимость модели
    function CheckDiffModelMarks(marks: TStringList): Boolean; // проверить маркировки по marks: True - есть различия
    function ModelEdit(pName: String; pVisible, pIsTop: Boolean; pUserID: Integer; mps: TModelParams;
                       pOrdNum: Integer=-1; pTDcode: Integer=-1; marks: TStringList=nil): String;
    procedure SetModelParams(mps: TModelParams; flFill: Boolean=False);
    procedure SetModelMarks(marks: TStringList; pUserID: integer); // записать маркировки по marks
    function AddModelEngLink(pEngID, pUserID: Integer): String;  // Добавить связку модели с маркировкой двигателя

    property ModelLineID   : Integer index ik8_3 read GetIntM  write SetIntM;  // Код модельного ряда
    property ModelOrderNum : Integer index ik8_4 read GetIntM  write SetIntM;  // порядк.№ модели
    property TypeSys       : Integer index ik8_1 read GetIntM;  // Тип системы 1 - Авто, 2 - Мото and etc.
    property ModelMfauID   : Integer index ik8_2 read GetIntM;  // Код производителя
    property ModelHasWares : Boolean index ik8_3 read GetDirBool write SetDirBool; // Признак наличия товаров по модели
    property IsTop         : Boolean index ik8_4 read GetDirBool write SetDirBool; // Топ модель
    property NodesExists   : Boolean read GetNodesExists;     // наличие связок 2
    property SortName      : String  index ik8_1 read GetStrM;  // Наименование модели для сортировки
    property MarksCommaText: String  index ik8_2 read GetStrM;  // список маркировок двигателей
    property ModelLineName : String  index ik8_3 read GetStrM;  // Название модельного ряда модели
    property ModelMfauName : String  index ik8_4 read GetStrM;  // Название производителя модели
    property WebName       : String  index ik8_5 read GetStrM;  // Название модели для вывода в Web
    property Params        : TModelParams read FParams;       // параметры модели
    property NodeLinks     : TNodeLinks   read FNodeLinks;    // связки 2 узлов дерева с моделью
    property EngLinks      : TLinks       read FLinks;        // связки с маркировками двигателей
  end;

  TModelsAuto = Class   // Список моделей
  private
    FarModels: array of TModelAuto; // массив моделей (индекс - ID)
    CS_Models: TCriticalSection; // для моделей
  public
    constructor Create;
    destructor Destroy; override;
    function GetModel(pModelID: Integer): TModelAuto;       // Получить ссылку на обьект модели
    function ModelExists(pModelID: Integer): Boolean;       // Проверка существования модели
    function GetMLModelsList(pModelLineID: Integer): Tai;   // must Free, Возвращает массив кодов моделей указанного модельного ряда
    function ModelAdd(var pModelID: Integer; pName: String; // Добавить модель, с занесением в базу
             pVis, pTops: Boolean; UserID, MLineID: Integer; mps: TModelParams;
             pOrdNum: Integer=-1; pTDcode: Integer=0; marks: TStringList=nil): String;
    function ModelDel(pModelID: Integer): String;           // Удалить модель, с удалением из базы
    procedure SetStates(pState: Boolean; pModelLineID: Integer=0);     // устанавливает флаг проверки всем моделям модельного ряда
    function SaveMarkAndGetID(var pMarkID: Integer; pTDcode, pModelID, UserID: Integer; pName: String): String; // Добавить маркировку в базу
    property Items[index: Integer]: TModelAuto read GetModel; default; // Безопасное получение ссылки на обьект модели
  end;

//-------------------------------------------------------------------- Двигатели
  TEngParams = Class  // параметры двигателя
  public
    pCompFrom   : Integer; // Компрессор * 100 от
    pCompTo     : Integer; // Компрессор * 100 до
    pRPMtorqFrom: Integer; // Момент вращения (Nm) при [об/мин] от
    pRPMtorqTo  : Integer; // Момент вращения (Nm) при [об/мин] до
    pBore       : Integer; // Расточка * 1000
    pStroke     : Integer; // Ход поршня * 1000
    pYearFrom   : Word;    // Год выпуска от
    pYearTo     : Word;    // Год выпуска до
    pKWfrom     : Word;    // Мощность кВт от
    pRPMKWfrom  : Word;    // при [об/мин] от
    pKWto       : Word;    // Мощность кВт до
    pRPMKWto    : Word;    // при [об/мин] до
    pHPfrom     : Word;    // Мощность ЛС от
    pHPto       : Word;    // Мощность ЛС до
    pCCtecFrom  : Word;    // Тех.обьем в куб.см. от
    pCCtecTo    : Word;    // Тех.обьем в куб.см. до
    pMonFrom    : Byte;    // Месяц выпуска от
    pMonTo      : Byte;    // Месяц выпуска до
    pVal        : Byte;    // Количество клапанов
    pCyl        : Byte;    // Количество цилиндров
    pCrank      : Byte;    // Кол-во подшипников коленвала
    pDesign     : Byte;    // Код, Исполнение двигателя   (KT 96) (TYPEDIR=13)
    pFuelType   : Byte;    // Код, Вид топлива            (KT 88) (TYPEDIR=12)
    pFuelMixt   : Byte;    // Код, Система впрыска        (KT 97) (TYPEDIR=5)
    pAspir      : Byte;    // Код, Подача воздуха         (KT 99) (TYPEDIR=14)
    pType       : Byte;    // Код, Вид двигателя          (KT 80) (TYPEDIR=3)
    pNorm       : Byte;    // Код, Норма выхлопных газов  (KT 63) (TYPEDIR=15)
    pCylDesign  : Byte;    // Код, Тип привода ГРМ        (KT 79) (TYPEDIR=16)
    pManag      : Byte;    // Код, Привод распредвала     (KT 77) (TYPEDIR=17)
    pValCnt     : Byte;    // Код, Привод клапана         (KT 78) (TYPEDIR=18)
    pCoolType   : Byte;    // Код, Тип системы охлаждения (KT 76) (TYPEDIR=19)
    pSalesDesc  : String;  // Обозначение продажи
    procedure Clear;       // очистить параметры
  end;

  TEngine = Class (TSubDirItem) // двигатель
  protected // FID - код, FName - маркировка, FSubCode - код TecDoc
    FEngMFau: Integer;    // код производителя
    FParams : TEngParams; // параметры двигателя
    function GetStrEng(const ik: T8InfoKinds): String;        // получить строку
  public
    constructor Create(pID, pSubCode, pEngMFau: Integer; pName: String; WithLinks: Boolean=False);
    destructor Destroy; override;
    procedure SetParams(eps: TEngParams; flFill: Boolean=False); // записать / обновить параметры
    function GetViewList(Delim: String=cSpecDelim): TStringList;   // must Free, список параметров двигателя для просмотра
    function GetNodesLinks: TLinks;   // must Free, список связок 2 двигателя
//    function GetEngNodeWaresList(nodeID: Integer; withChildNodes: boolean=True): TStringList; // must Free, список товаров по узлу двигателя
//    function GetEngNodeWares(nodeID: Integer; withChildNodes: boolean=True): Tai;             // must Free, массив кодов товаров по узлу двигателя
    function GetEngNodeWareUsesView(nodeID: Integer; WareCodes: Tai; sFilters: String=''): TStringList; // must Free, список условий по товарам узла двигателя для просмотра

    function GetEngNodeWaresWithUsesByFilters(NodeID: Integer; // фильтр.список товаров с текстами и условиями к связкам 3, Objects - WareID
             withChildNodes: boolean=True; sFilters: String=''): TStringList;  // must Free, sFilters - коды значений критериев через запятую

    property TDCode   : Integer read FSubCode;                // код TecDoc
    property EngMFau  : Integer read FEngMFau write FEngMFau; // код производителя
    property EngParams: TEngParams read FParams;              // параметры двигателя
    property Mark     : String  index ik8_1 read GetStrEng;     // маркировка
    property EngKWstr : String  index ik8_2 read GetStrEng;     // строка - значение Мощность кВт
    property EngHPstr : String  index ik8_3 read GetStrEng;     // строка - значение Мощность ЛС
    property EngCCstr : String  index ik8_4 read GetStrEng;     // строка - значение Тех.обьем в куб.см.
    property EngCYLstr: String  index ik8_5 read GetStrEng;     // строка - значение Количество цилиндров
    property WebName  : String  index ik8_6 read GetStrEng;     // WebName
    property MfauName : String  index ik8_7 read GetStrEng;     // MfauName
    property EngHasNodes: boolean index ik8_3 read GetDirBool write SetDirBool; // признак наличия 2-х связок
    property EngHasWares: boolean index ik8_4 read GetDirBool write SetDirBool; // признак наличия 3-х связок
  end;

  TEngines = Class (TDirItems) // двигатели
  public
    function GetEngine(engID: integer): TEngine;         // получить двигатель по коду
    function FindEngineByTDcode(engTD: integer; var eng: TEngine): Boolean; // найти двигатель по коду TecDoc
    function AddEngine(var pID: Integer; pSys, pTDcode, pMFau, pUserID: Integer; // добавить двигатель
                       pMark: String; eps: TEngParams): String;
    function EditEngine(pID: Integer; pTDcode, pMFau, pUserID: Integer;          // изменить двигатель
                       pMark: String; eps: TEngParams): String;
    function GetMfauEngList(mfau: integer): TStringList; // must Free, сорт.по маркировкам список двигателей производителя (Object-TEngine)
  end;

//---------------------------------------------------------------- Модельный ряд
  TModelLine = Class (TSubVisDirItem)
  private // FID, FName - Код, Наименование модельного ряда, State - Статус проверки,
          // FSubCode - Код TecDoc, { FLinks - Сортированный список линков на модели модельного ряда / заготовка}
          // IsVisible - Признак видимости модельного ряда, FParCode - Код производителя авто
    FYStart    : Word;      // Год начала производства
    FYEnd      : Word;      // Год окончания производства
    FMStart    : Byte;      // Месяц начала производства
    FMEnd      : Byte;      // Месяц окончания производства
    FTypeSys   : Byte;      // Тип системы 1 - Авто, 2 - Мото and etc.
    FMLModelsSort : TStringList; // Сортированный список моделей модельного ряда   (Object - Pointer(ID))
    FMLModelsTopUp: TStringList; // список моделей модельного ряда с топами вверху (Object - Pointer(ID))
    function GetIntML(const ik: T8InfoKinds): Integer;              // получить код
    procedure SetIntML(const ik: T8InfoKinds; Value: Integer);      // записать код
    function GetStrML(const ik: T8InfoKinds): String;               // получить строку
    function GetHasVisModels: Boolean;
  public
    constructor Create(pMLineID, pTDcode, pMFAID: Integer; pName: String);
    destructor Destroy; override;
    procedure ModelDelFromLine(pModelID: Integer); // Удалить модель из модельного ряда  из кеш
    procedure CheckModelsLists;                   // заполнить списки моделей модельного ряда
    function GetListModels(pTopsUp: Boolean=False): TStringList; // Получить список моделей, (Object - Pointer(ID))
    function GetMLModelIDByTDcode(pModTDnr: Integer): Integer; // получить код модели модельного ряда по коду TecDoc
    property MFAID       : Integer index ik8_2 read GetIntML write SetIntML; // Код производителя авто
    property ModelsCount : Integer index ik8_1 read GetIntML;  // количество моделей модельного ряда
    property YStart      : Integer index ik8_3 read GetIntML;  // Год начала производства
    property YEnd        : Integer index ik8_4 read GetIntML;  // Год окончания производства
    property MStart      : Integer index ik8_5 read GetIntML;  // Месяц начала производства
    property MEnd        : Integer index ik8_6 read GetIntML;  // Месяц окончания производства
    property TypeSys     : Integer index ik8_7 read GetIntML;  // Тип системы 1 - Авто, 2 - Мото and etc.
    property SortName    : String  index ik8_1 read GetStrML;  // Наименование модельного ряда для сортировки
    property MLHasWares  : Boolean index ik8_3 read GetDirBool write SetDirBool; // Признак наличия товаров по модельному ряду
    property IsTop       : Boolean index ik8_4 read GetDirBool write SetDirBool; // Топ
    property HasVisModels: Boolean read GetHasVisModels;     // признак наличия видимых моделей модельного ряда
  end;

  TModelLines = Class    // Список модельных рядов
  private
    FarModelLines: array of TModelLine;    // массив модельных рядов (индекс - ID)
  public
    CS_MLines: TCriticalSection; // для мод.рядов
    constructor Create;
    destructor Destroy; override;
    function GetModelLine(pModelLineID: Integer): TModelLine; // Получить ссылку на модельный ряд по коду
    function ModelLineExists(pID: Integer): Boolean; // Проверка существования модельного ряда
    function GetManufSysModelLinesList(pManufID, pTypeSys: Integer): TStringList; // must Free, список модельных рядов производителя по указанной системе (Object - Pointer(ID))
    procedure SetStates(pState: Boolean; pTypeSys: Integer=0);   // устанавливает флаг проверки всем элементам
    property Items[Index: Integer]: TModelLine read GetModelLine; default; // Безопасное получение ссылки на обьект модельного ряда
  end;

//-------------------------------------------------------- Производитель техники
  TManufacturer = Class (TSubDirItem)
  private // FID, FName, State - Код, Наименование производителя, Статус, FSubCode - Код TecDoc
    FManufSysMLsSort : TArraySysTypeLists; // Сортированные списки модельных рядов по системам учета, (Object - Pointer(ID))
    FManufSysMLsTopUp: TArraySysTypeLists; // списки модельных рядов с топами вверху по системам учета, (Object - Pointer(ID))
    FManufSearchONlist: TStringList;       // Сортированный список ориг.номеров производителя без спец/симв, (Object - Pointer(ID))
    FMfauOpts: set of T8InfoKinds;
    function GetBoolMf(ik: T8InfoKinds): boolean;        // получить признак
    procedure SetBoolMf(ik: T8InfoKinds; Value: boolean); // записать признак
  public
    constructor Create(pID: Integer; pName: String; pTDnr: Integer=0);  // Код, Имя, Код TecDoc
    destructor Destroy; override;
    procedure TopsSet(pTypeSys: Integer; IsTop: Boolean);    // Установить / снять ТОПовость
    procedure TypeSysSet(pTypeSys: Integer; IsSys: Boolean); // Установить / снять принадлежность системе
    procedure VisibleSet(pTypeSys: Integer; IsVis: Boolean); // Установить / снять видимость
    function CheckIsTypeSys(pTypeSys: Integer): Boolean;    // Проверка принадлежности производителя к системе Авто/ Мото
    function CheckIsVisible(pTypeSys: Integer): Boolean;    // Проверить видимость
    function CheckIsTop(pTypeSys: Integer): Boolean;        // Проверить производитель ТОПОВЫЙ для системы учета
    function CheckOtherTypeSys(pTypeSys: Integer): Boolean; // Проверка принадлежности к какой-нибудь системе кроме pTypeSys
    function CheckHasModelLines(pTypeSys: Integer = 0): Boolean;  // Проверка имеютеся ли у производителя модельные ряды
    procedure CheckModelLinesLists(pTypeSys: Integer);   // заполнить Списки модельных рядов производителя по выбранной системе
    function GetModelLinesList(pTypeSys: Integer; pTopsUp: Boolean): TStringList; // Список модельных рядов производителя по выбранной системе, сортированный по наименованию  (Object - Pointer(ID))
    function GetModelsList(pTypeSys: Integer): TStringList; // must Free, Список моделей производителя по выбранной системе (Object - Pointer(ID))
    function ModelLineAdd(var ModelLineID: Integer; pName: String; // Добавить модельный ряд в кеш и в базу к производителю к системе
             pTypeSys, pMS, pYS, pME, pYE, pUserID: Integer; pIsTop, pIsVis: Boolean; pMLTD: Integer=0): String;
    function ModelLineDel(pModelLineID: Integer): String;  // Удалить модельный ряд из кеша и из базы у производителя
    function ModelLineEdit(pModelLineID, pYS, pMS, pYE, pME, pUserID: Integer;
             pIsTop, pIsVis: Boolean; pName: String=''; pMLTD: Integer=0): String;
    function HasVisModelLines(pTypeSys: Integer): Boolean; // признак наличия видимых модельных рядов по выбранной системе
    function HasVisMLModels(pTypeSys: Integer): Boolean;   // признак наличия видимых моделей по выбранной системе
    function GetMfMLineIDByTDcode(pmlTDnr: Integer): Integer; // получить код модельного ряда авто по коду TecDoc
    property IsMF         : Boolean index ik8_1 read GetBoolMf  write SetBoolMf;  // Признак производителя
    property IsMfAUTO     : Boolean index ik8_2 read GetBoolMf  write SetBoolMf;  // Признак производителя авто
    property IsMfMOTO     : Boolean index ik8_3 read GetBoolMf  write SetBoolMf;  // Признак производителя мото
    property IsMfTopA     : Boolean index ik8_4 read GetBoolMf  write SetBoolMf;  // Признак топ-производителя авто
    property IsMfTopM     : Boolean index ik8_5 read GetBoolMf  write SetBoolMf;  // Признак топ-производителя мото
    property IsMfVisA     : Boolean index ik8_6 read GetBoolMf  write SetBoolMf;  // Признак видимости производителя авто
    property IsMfVisM     : Boolean index ik8_7 read GetBoolMf  write SetBoolMf;  // Признак видимости производителя мото
    property ManufHasWares: Boolean index ik8_3 read GetDirBool write SetDirBool; // Признак наличия товаров по производителю
    property MfHasEngWares: Boolean index ik8_4 read GetDirBool write SetDirBool; // Признак наличия товаров двигателей по производителю
    property ManufSearchONlist: TStringList read FManufSearchONlist;
  end;

  TManufacturers = Class  // Справочник производителей авто
  private
    FarManufacturers  : array of TManufacturer; // массив производителей авто (индекс - ID)
    FSysManufListSort : TArraySysTypeLists;     // Сортированные списки производителей по системам учета (Objects-Pointer TManufacturer)
    FSysManufListTopUp: TArraySysTypeLists;     // списки с топами вверху по системам учета (Objects-Pointer TManufacturer)
    function GetManufItem(pIndex: Integer): TManufacturer; // Получить элемент массива (производителя по коду)
  public
    CS_Mfaus: TCriticalSection;       // Контроль многопоточной работы
    constructor Create;
    destructor Destroy; override;
    function CheckManufItem(pCODE: Integer; var flNew: Boolean; pNAME: String; pTDnr: Integer=0): Boolean;
    function ManufExists(pID: Integer): Boolean;                          // Проверка существования Производителя с кодом
    function ManufExistsByName(pName: String; var pID: Integer): Boolean; // Проверка существования Производителя по имени
    function GetManufIDByTDcode(pTDnr: Integer): Integer;                 // получить код производителя по коду TecDoc
    function ManufAdd(var ManufID: Integer; pName: String; pTypeSys, pUserID: Integer;
             pIsTop, pIsVis: boolean; pTDnr: Integer=0): String;   // Создать производителя с занесеним в базу
    function ManufEdit(var pID: Integer; pTypeSys, pUserID: Integer; // Изменить производителя и в базе
             pIsTop, pIsVis: boolean; pName: String=''; pTDnr: Integer=0): String;
    function ManufDel(var pID: Integer; pTypeSys: Integer): String;  // Удалить производителя с удалением из базы
    procedure CheckManufLists(pTypeSys: Integer);                    // заполнить списки системы
    function GetSortedList(pTypeSys: Integer): TStringList;          // Получить сортированный список производителей системы (Object - Pointer TManufacturer)
    function GetSortedListWithTops(pTypeSys: Integer): TStringList;  // Получить список производителей системы с Топами вверху (Object - Pointer TManufacturer)
    procedure SetStates(pState: Boolean; pTypeSys: Integer=0); // устанавливает флаг проверки всем элементам
    function GetNotTestedList(pTypeSys: Integer=0; flTested: TBooleanDynArray=nil): TStringList; // must Free, получить список непроверенных производителей системы (Object - Pointer(ID))
    function GetEngManufList: TStringList;  // must Free, получить сортированный список производителей двигателей (Object - Pointer(ID))
    function GetOEManufList: TStringList;  // must Free, получить сортированный список производителей с ОН (Object - Pointer(ID))
    property Items[Index: Integer]: TManufacturer read GetManufItem; default; // Массив производителей
  end;

//------------------------------------------------------ Дерево узлов автомобиля
  TAutoTreeNode = Class (TSubVisDirItem)  // Узел дерева
  private // FID - Код узла, FName - Наименование, State - Статус, FSubCode - Код TecDoc,
          // FOrderNum - порядк.номер ноды в дереве своей системы
    FMainCode: Integer;         // код TRNACODE главной ноды
    FMeasID  : Byte;            // Код ед.изм.
    FTypeSys : Byte;            // Тип системы 1 - Авто, 2 - Мото and etc.
    FNameSys : String;          // Наименование системное
    FChildren: TStringList;     // Список подузлов, Object - Pointer TAutoTreeNode
    function GetIsEnding: boolean;
  public
    constructor Create(pID, pParentID, pMeasID, pSysID: Integer;
                pName, pNameSys: String; pMainCode: Integer=0; pCodeTD: Integer=0;
                pIsGATD: Boolean=False; pVisible: Boolean=True);
//                pIsGATD: Boolean=False; pVisible: Boolean=True; pIsEng: Boolean=False);
    destructor Destroy; override;
    property Children: TStringList read FChildren; // Список подузлов, Object - Pointer TAutoTreeNode
    property NameSys : String  read FNameSys;      // Наименование системное
    property ParentID: Integer read FParCode;      // Код родителя
    property MainCode: Integer read FMainCode write FMainCode; // код TRNACODE главной ноды из повторяющихся
    property OrderNum: Integer read FOrderNum write FOrderNum;  // порядк.номер ноды в дереве своей системы
    property MeasID  : Byte    read FMeasID;       // Код ед.изм.
    property TypeSys : Byte    read FTypeSys;      // Тип системы 1 - Авто, 2 - Мото and etc.
    property Visible : Boolean index ik8_2 read GetDirBool write SetDirBool; // признак видимости ноды
    property IsGATD  : Boolean index ik8_3 read GetDirBool write SetDirBool; // признак GA TecDoc (конечной ноды авто)
//    property IsEng   : Boolean index ik8_4 read GetDirBool write SetDirBool; // признак ноды двигателя
    property IsEnding: Boolean read GetIsEnding; // признак конечной ноды
  end;

{
  TTreeNodes = Class (TDirItems)         // Дерево узлов
  private // FItems - массив ссылок на узлы дерева, FItems[0] - ссылка на корневой узел дерева
    FSysNodesLists: TArraySysTypeLists;  // списки нод дерева по системам учета последовательные по всем ветвям (Objects-Pointer TAutoTreeNode)
    function GetNodeByID(pID: Integer): TAutoTreeNode;

  public
    constructor Create(LengthStep: Integer=10);
    destructor Destroy; override;
    property NodeItems[ID: Integer]: TAutoTreeNode read GetNodeByID; default;

    procedure CheckSysNodes(SysID: integer); // заполнить список дерева системы последовательный по всем ветвям
    function GetSysTree(SysID: integer): TStringList; // Получить список дерева системы SysID последовательный по всем ветвям
  end;
}
//  TAutoTreeNodesSys = Class;
  TAutoTreeNodes = Class          // Дерево узлов системы
  private
    FTypeSys  : Byte;            // Тип системы 1 - Авто, 2 - Мото and etc.
    FNodesList: TStringList;     // последовательный список нод дерева, Object - Pointer TAutoTreeNode
    FNodeItems: array of TAutoTreeNode; // список узлов дерева по кодам, FNodeItems[0] - корневой узел дерева
    function GetNodeByID(pID: Integer): TAutoTreeNode;
    function GetNodesList: TStringList;
  public
    CS_Nodes  : TCriticalSection; // Контроль FNodesList
    constructor Create(pSysID: Integer=0);
    destructor Destroy; override;
    function NodeAdd(var pID: Integer; pParentID, pMeasID: Integer; pName, pNameSys: String; // Добавить узел
             pMainCode: Integer=0; pCodeTD: Integer=0; pIsGATD: Boolean=False;
             pVisible: Boolean=True; pCheckTreeDup: Boolean=False; pReList: Boolean=False): String;
    function NodeValidCheckForAdd(pID, pParentID: Integer; pName, pNameSys: String; // Проверить валидность добавления узла
             var pNodeAdd, pNodeParent: TAutoTreeNode; pCheckTreeDup: Boolean=True): String; overload;
    function NodeDel(pNodeID: Integer): String;     // Удалить узел
    function NodeEdit(pNodeID, pMainCode, pVisible, pUserID: Integer; pName, pNameSys: String): String; // Изменить параметры узла
    function NodeGet(pID: Integer; var pNodeGet: TAutoTreeNode): Boolean; // Получить узел по коду
    function NodeExists(pID: Integer): Boolean; // проверить существование узла
    procedure FillNodesList;                    // заполнить список дерева последовательный по всем ветвям
    function NoteGetTree: TStringList; // must Free, последовательный список нод дерева, Object - Pointer TAutoTreeNode
    // must Free, список дубликатов ноды pNodeID с ветвями, Object - Pointer TAutoTreeNode (ByTDid=True - по коду TecDoc, False - по MainCode)
    function GetDuplicateNodes(pNodeID: Integer; ByTDid: Boolean=False): TStringList;
    function GetMainNodeIDByTDcode(pTDnr: Integer): Integer; // получить код главной ноды по коду TecDoc
    function GetNodeIDByTDcodes(nodeTD, parTD: Integer; IsGa: Boolean): Integer; // получить код ноды по кодам TecDoc
    function GetDuplicateNodeCodes(pMainNodeID: Integer; OnlyVisible: Boolean=False): Tai; // must Free, коды дублирующих нод главной ноды pMainNodeID
    property TypeSys : Byte    read FTypeSys;      // Тип системы 1 - Авто, 2 - Мото and etc.
    property Items[ID: Integer]: TAutoTreeNode read GetNodeByID; default;
    property NodesList: TStringList read GetNodesList;      // последовательный список нод дерева, Object - Pointer TAutoTreeNode
  end;

  TATN = array of TAutoTreeNodes;   // Кеш деревьев систем учета
  TAutoTreeNodesSys = Class
  private
    FarTreeNodes: TATN; // Массив деревьев систем учета
    function GetTreeNodes(pSys: Integer): TAutoTreeNodes; // Получить ссылку на дерево по коду системы
  public
    constructor Create;
    destructor Destroy; override;
    function GetTreeNode(pID: Integer): TAutoTreeNode; // Получить ссылку на ноду по коду без системы
    property arTreeNodes: TATN read FarTreeNodes;
    property Items[pSys: Integer]: TAutoTreeNodes read GetTreeNodes; default;
  end;

//----------------- набор связок со справочником пронумерованных нод TSubDirItem
  TNodeLinks = Class (TLinkLinks) // (поиск методом половинного деления с учетом dirNodes)
  protected
    function GetLinkItemByID(pID: Integer): Pointer; override;// Получить связку по коду связанного элемента
  public
    dirNodes: TAutoTreeNodes; // ссылка на справочник пронумерованных нод TSubDirItem
  end;

//---------------------------------------------------- Оригинальный номер товара
  TOriginalNumInfo = class (TSubDirItem) // FName - оригинальный номер, FSubCode - код производителя авто
  private                                // FLinks - связки с товарами, отсортированные по наименованию
    function GetIntON(const ik: T8InfoKinds): Integer;         // получить код
    procedure SetIntON(const ik: T8InfoKinds; Value: Integer); // записать код
    function GetStrON(const ik: T8InfoKinds): String;          // получить строку
    procedure SetStrON(const ik: T8InfoKinds; Value: String);  // записать строку
  public
    constructor Create(pID, pMfau: Integer; pNum: String; CS: TCriticalSection=nil);
    function ArAnalogs     : Tai;                                              // must Free, массив кодов товаров
    function GetAnalogTypes(WithoutEmpty: Boolean=False): Tai;                 // must Free, массив кодов типов аналогов
    property MfAutoID      : Integer index ik8_1 read GetIntON write SetIntON; // код производителя авто
    property TypeID        : Integer index ik8_2 read GetIntON;                // код типа по связанным товарам
    property OriginalNum   : string  index ik8_1 read GetStrON write SetStrON; // ориг.номер товара UpperCase б/спецсимволов
    property ManufName     : String  index ik8_2 read GetStrON;                // наименование произв.авто
    property SortString    : String  index ik8_3 read GetStrON;                // строка для сортировки по произв.авто
    property CommentWWW    : String  index ik8_4 read GetStrON;                // комментарий для Web с учетом типа товара
  end;

  arTOE = array of TOriginalNumInfo;

//--------------------------------------------- TYPESINFOMODEL - списки по типам
  TTypeInfoModel = Class (TBaseDirItem)
  private
    FTypeDir: Word;         // тип списка
    FTDcode : Word;         // код KEY_ENTRIES.KE_KEY as integer (TDT)
    FTDkt   : Word;         // код KEY_ENTRIES.KE_KT_ID (TDT)
  public
    constructor Create(pID, pType, pTDcode, pTDkt: Integer; pName: String);
    function SetTypeDir(pType: Word): Boolean;
    property TypeDir: Word read FTypeDir;     // тип списка
    property TDcode : Word read FTDcode;      // код KEY_ENTRIES.KE_KEY as integer (TDT)
    property TDkt   : Word read FTDkt;        // код KEY_ENTRIES.KE_KT_ID (TDT)
  end;

  TTypesInfoModel = Class (TDirItems)  //
  private
    FTypeTDcodes: Tai;
    FTypeLists: TArrayTypeLists;                        // набор списков по типам
    function GetList(pTypeDir: Word): TStringList;      // Получить Список элементов заданного типа (Object - Pointer(ID))
    function GetInfoItem(pID: Integer): TTypeInfoModel; // Получить типизированный элемент списка
  public
    constructor Create(LengthStep: Word=10);
    destructor Destroy; override;
    function GetTypeName(pID: Integer): String; // название типа списка  элемента
    procedure CheckInfoModelItem(pID: Integer; pType, pTDkey, pTDkt: Word; pName: String); // проверить / добавить элемент списка в кэш
    function AddInfoModelItem(var pID, pType: Integer; pTDkey, pTDkt: Word; pName: String; pUser: Integer): String; // добавить элемент списка в кэш и в базу
    function FindInfoItemByTDcodes(var pID, pTyp: Integer; pTDkey, pTDkt: Word): Boolean; // найти код типа списка и код элемента списка по кодам TecDoc
    property InfoModelList[TypeDir: Word]: TStringList read GetList;     // Список элементов заданного типа
    property InfoItems[index: Integer]: TTypeInfoModel read GetInfoItem; // получить типизированный элемент списка
  end;

//----------------- Дополнение к DataCache для учета оригинальных номеров и т.д.
  TDataCacheAdditionASON = class
  protected
    FManufacturers    : TManufacturers;    // Производители авто/мото
    FModelLines       : TModelLines;       // Модельные ряды
    FModels           : TModelsAuto;       // Модели
    FLinkSources      : TDirItems;         // справочник источников информации и связей
    FarOriginalNumInfo: arTOE;             // массив оригинальных номеров товара
    FAutoTreeNodesSys : TAutoTreeNodesSys; // Деревья узлов могосистемного использования
    FTypesInfoModel   : TTypesInfoModel;
//    FTreeNodes        : TTreeNodes;        // Дерево узлов
    CS_OrigNums       : TCriticalSection;
    FEngines          : TEngines;
  public
    constructor Create;
    destructor Destroy; override;
    procedure FillSourceLinks;   // заполнение/проверка списка источников оригинальных номеров и связей с аналогами.
    procedure FillTreeNodesAuto; // заполнение дерева узлов автомобиля
    procedure FillOriginalNums(fFill: Boolean=True); // заполнение/проверка оригинальных номеров товара
    procedure FillWareONLinks(fFill: Boolean=True; sLog: String=''); // Вызов наполнения связей оригинальных номеров
    procedure FillTypesInfoModel;
    procedure FillDirManuf(fFill: Boolean=True);      // Загрузка/Обновление справочника производителей из Базы
    procedure FillDirModelLines(fFill: Boolean=True); // Загрузка/ обновление списка модельных рядов
    procedure FillDirEngines(fFill: Boolean=True);    // Загрузка / обновление списка двигателей из базы в кеш
    procedure FillDirModels(fFill: Boolean=True);     // Загрузка / обновление списка моделей из базы в кеш
    procedure FillModelNodeLinks;                     // Загрузка связей моделей с деревом узлов (связка 2)
    procedure FillWareModelNodeLinks;                 // Загрузка связей товара со связкой 2 (связка 3)

    function GetModelTypeSys(pModelID: Integer): Integer;
    function TreeNodeAdd(pTypeSys, pParentID, pMainCode: Integer; pNodeName, pNodeNameSys: String;
             pUserID: Integer; var pNodeID: Integer; pVisible: Boolean=True; pMeasID: Integer=0;  // Добавить узел
             pCodeTD: Integer=0; pIsGATD: Boolean=False): String; // Добавление элемента в дерево узлов
    function SourceLinkExist(ID: Integer): Boolean;
    function OrigNumExist(ID: Integer): Boolean;
    function ManufAutoExist(ID: Integer): Boolean;
    function GetArLinkSources: Tas;                                        // must Free, справочник источников в массиве
    function GetSourceByGBcode(srcGB: Integer): Integer;                   // код источника по коду Grossbee
    function GetSourceGBcode(src: Integer): Integer;                       // код источника Grossbee

    function SearchOriginalNum(Manuf: Integer; OrigNum: String): Integer;  // Возвращает код оригинального номера, если в кэше нет - -1
    function GetOriginalNum(ID: Integer): TOriginalNumInfo;                // Возвращает обьект оригинального номера по коду
    procedure AddNewOrigNumToCache(OrigNum: TOriginalNumInfo; step: Integer=100);  // добавление оригинального номера в кеш
    function SearchWareOrigNums(Template: String; IgnoreSpec: Integer;   // must Free, Поиск оригинальных номеров
             sortManuf: Boolean; var TypeCodes: Tai): Tai;
    function fnGetListAnalogsWithManufacturer(pWareID, pManufID: Integer; var pAr1, pAr2: Tai): Integer;   // Получить список аналогов от связки оригинальных номеров с учетом производителя авто

    function CheckNotValidModelNodeLinkParams(ModelID, NodeID: Integer; // проверяем параметры связки 2
             var Model: TModelAuto; var Node: TAutoTreeNode; var SysID: Integer; var errmess: string): Boolean;
    function CheckModelNodeLinkDup(ModelID, NodeID: Integer; Value: String; // удаление / добавление / редактирование связки 2 по группе дубликатов нод
             var ResCode: Integer; srcID: Integer=0; userID: Integer=0): string;
    function CheckWareModelNodeLink(WareID, ModelID, NodeID: Integer;   // удаление / добавление / редактирование связки 3
             var ResCode: Integer; srcID: Integer=0; userID: Integer=0): string;
    function CheckWareModelNodeUsage(WareID, ModelID, NodeID: Integer;  // удаление / добавление условия применения связки 3
             UsageName, UsageValue: String; var ResCode: Integer; srcID: Integer=0; userID: Integer=0): string;
//    function CheckWareModelNodeUseList(WareID, ModelID, NodeID: Integer; // удаление / добавление порции условий применения связки 3
//             var UseList: TStringList; var ResCode: Integer; srcID: Integer=0; userID: Integer=0): string;
    function CheckOrigNumLink(var ResCode: Integer; WareID, pMfID: Integer; var onID: Integer; // Установить / снять связь с ориг.номером
             var pONum: String; srcID: Integer=0; userID: Integer=0): String;

     // must Free, список значений фильтров узла модели/двигателя, Strings - значение критерия, Object - Pointer(ID значения)
    function GetModelOrEngNodeFiltersList(NodeID, pID: integer; IsEngine: Boolean=False): TStringList;

    property Manufacturers    : TManufacturers read FManufacturers; // Производители авто/мото/двигателей
    property ModelLines       : TModelLines read FModelLines;       // Модельные ряды
    property Models           : TModelsAuto read FModels;           // Модели
    property arOriginalNumInfo: arTOE read FarOriginalNumInfo write FarOriginalNumInfo;
    property AutoTreeNodesSys : TAutoTreeNodesSys read FAutoTreeNodesSys;
    property TypesInfoModel   : TTypesInfoModel read FTypesInfoModel;
//    property TreeNodes        : TTreeNodes read FTreeNodes;
    property LinkSources      : TDirItems  read FLinkSources;
    property Engines          : TEngines  read FEngines;
  end;

//                              независимые функции
function GetNameForSort(pName: String; pY, pM: Integer): String; // Формирование строки для сортировки списков
function FindNodeIndex(NodeID: Integer; lstNodes: TStringList): Integer;  // ищем индекс ноды с кодом NodeID в lstNodes

const cMaxLinksWithOE = 50;

implementation
uses n_DataCacheInMemory, n_server_common, n_TD_functions,
     v_server_common, v_server_main, n_Functions;
//=================================== ищем индекс ноды с кодом NodeID в lstNodes
function FindNodeIndex(NodeID: Integer; lstNodes: TStringList): Integer;
begin
  for Result:= 0 to lstNodes.Count-1 do
    if TAutoTreeNode(lstNodes.Objects[Result]).ID=NodeID then exit;
  Result:= -1;
end;

//******************************************************************************
//  TNodeLinks - набор связок со справочником пронумерованных нод TSubDirItem
//******************************************************************************
//===================== Возвращает ссылку на элемент с кодом pID, если нет - nil
// поиск методом половинного деления
function TNodeLinks.GetLinkItemByID(pID: Integer): Pointer;
var i, SearchNum, iLow, iHigh, iNum: Integer;
begin
  Result:= nil;
  if not Assigned(self) or (pID<1) then Exit;
  if not Assigned(dirNodes) or not dirNodes.NodeExists(pID) then Exit;
  SearchNum:= GetDirItemOrdNum(dirNodes[pID]); // пор.№ искомого эл-та
  iLow:= 0;                     // нижний индекс
  iHigh:= FItems.Count-1;       // верхний индекс
  while (iHigh-iLow)>4 do begin
    i:= (iLow+iHigh) div 2;   // индекс среднего эл-та
    if (TLink(FItems[i]).LinkID=pID) then begin
      Result:= FItems[i];
      Exit;
    end;
    iNum:= GetDirItemOrdNum(TLink(FItems[i]).LinkPtr); // пор.№ среднего эл-та
    if (SearchNum<iNum) then iHigh:= i-1 else iLow:= i+1;
  end;
  for i:= iLow to iHigh do // ищем перебором в оставшемся интервале
    if TLink(FItems[i]).LinkID=pID then begin
      Result:= FItems[i];
      Exit;
    end;
end;

//******************************************************************************
//               TArraySysTypeLists - набор списков по системам учета
//******************************************************************************
constructor TArraySysTypeLists.Create(fSorted: Boolean=False; fDelimiter: Boolean=False);
var i: integer;
    item: TDirItem;
begin
  inherited Create(fSorted, fDelimiter);
  with SysTypes do for i:= 0 to Count-1 do begin // набор типов систем
    Item:= ItemsList[i];
    AddTypeOfList(Item.ID, Item.Name);
  end;
end;

//******************************************************************************
//                                 TSecondLink
//******************************************************************************
constructor TSecondLink.Create(pSrcID: Integer; pQty: Single; pNodePtr: Pointer=nil;
            pIsLink: Boolean=False; pHasWares: Boolean=False; pHasFilters: Boolean=False);
begin
  inherited Create(pSrcID, pNodePtr);
  IsLinkNode    := pIsLink;
  NodeHasWares  := pHasWares;
  NodeHasFilters:= pHasFilters;
  FQty          := RoundTo(pQty, -3);
end;
//===================================== must Free, массив кодов товаров связки 2
function TSecondLink.GetWareCodes: Tai;
begin
  SetLength(Result, 0);
  if not Assigned(self) then Exit;
  if Assigned(DoubleLinks) then Result:= DoubleLinks.GetLinkListCodes(lkLnkNone);
end;

//******************************************************************************
//                          TTypeInfoModel
//******************************************************************************
constructor TTypeInfoModel.Create(pID, pType, pTDcode, pTDkt: Integer; pName: String);
begin
  inherited Create(pID, pName);
  FTypeDir:= pType;
  FTDcode := pTDcode;
  FTDkt   := pTDkt;
end;
//========================================================== изменить тип списка
function TTypeInfoModel.SetTypeDir(pType: Word): Boolean;
begin
  Result:= False;
  if not Assigned(self) or (FTypeDir=pType) then Exit;
  FTypeDir:= pType;
  Result:= True;
end;

//******************************************************************************
//                          TTypesInfoModel
//******************************************************************************
//==============================================================================
constructor TTypesInfoModel.Create(LengthStep: Word=10);
begin
  inherited Create(LengthStep);
  FTypeLists:= TArrayTypeLists.Create(True);
  SetLength(FTypeTDcodes, 0);
end;
//==============================================================================
destructor TTypesInfoModel.Destroy;
begin
  if not Assigned(self) then Exit;
  if Assigned(FTypeLists) then prFree(FTypeLists);
  SetLength(FTypeTDcodes, 0);
  inherited Destroy;
end;
//============================ Возвращает сортированный список элементов по типу
function TTypesInfoModel.GetList(pTypeDir: Word): TStringList;
begin
  Result:= nil;
  if not Assigned(self) then Exit;
  Result:= FTypeLists[pTypeDir];
end;
//======================================= получить типизированный элемент списка
function TTypesInfoModel.GetInfoItem(pID: Integer): TTypeInfoModel;
begin
  if ItemExists(pID) then Result:= DirItems[pID] else Result:= DirItems[0];
end;
//================================================ название типа списка элемента
function TTypesInfoModel.GetTypeName(pID: Integer): String;
var i, pType: Integer;
begin
  Result:= '';
  if not Assigned(self) then Exit;
  pType:= GetInfoItem(pID).FTypeDir;
  with FTypeLists.ListTypes do for i:= 0 to Count-1 do // список типов списков
    if Integer(Objects[i])=pType then begin
      Result:= Strings[i];
      exit;
    end;
end;
//========================================== проверить тип списка и наименование
procedure TTypesInfoModel.CheckInfoModelItem(pID: Integer; pType, pTDkey, pTDkt: Word; pName: String);
var i: Integer;
    s: String;
    Item: Pointer;
begin
  if not Assigned(self) then Exit;
  if ItemExists(pID) then begin  // проверяем тип списка, наименование
    with TTypeInfoModel(DirItems[pID]) do begin
      i:= TypeDir;
      s:= Name;
      if i<>pType then SetTypeDir(pType);
      if (pTDkey>0) and (FTDcode<>pTDkey) then FTDcode:= pTDkey;
      if (pTDkt>0) and (FTDkt<>pTDkt) then FTDkt:= pTDkt;
    end;
    if i<>pType then begin
      FTypeLists.DelTypeListItem(i, pID);
      FTypeLists.AddTypeListItem(pType, pID, pName);
    end;
    if s<>pName then begin
      SetItemName(pID, pName);
      i:= FTypeLists[pType].IndexOfObject(Pointer(pID));
      if i>-1 then fnChangeStringOfList(FTypeLists[pType], i, pName);
    end;
  end else begin
    Item:= TTypeInfoModel.Create(pID, pType, pTDkey, pTDkt, pName);
    if CheckItem(Item) then  // добавляем элемент в нужный список
      FTypeLists.AddTypeListItem(pType, pID, pName);
  end;
end;
//======================================= добавить элемент списка в кэш и в базу
function TTypesInfoModel.AddInfoModelItem(var pID, pType: Integer;
         pTDkey, pTDkt: Word; pName: String; pUser: Integer): String;
const nmProc = 'AddInfoModelItem';
var ORD_IBS: TIBSQL;
    ORD_IBD: TIBDatabase;
begin
  Result:= '';
  if not Assigned(self) then Exit;
  if (pID>0) and ItemExists(pID) then Exit;
  pID:= 0;
  if (pTDkey>0) and (pTDkt>0) and FindInfoItemByTDcodes(pID, pType, pTDkey, pTDkt) then Exit;
  ORD_IBD:= nil;
  ORD_IBS:= nil;
  try
    if (pType<1) then raise Exception.Create('не задан тип списка');

    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise Exception.Create(MessText(mtkErrConnectToDB));
    ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite, True);

    ORD_IBS.SQL.Text:= 'insert into DIRINFOTYPEMODEL'+
      ' (DITMTYPEDIR, DITMNAME, DITMUSERID'+fnIfStr(pTDkey>0, ', DITMTDKEYENTR)', '')+
      ' values ('+IntToStr(pType)+', :DITMNAME, '+IntToStr(pUser)+
      fnIfStr(pTDkey>0, ', '+IntToStr(pTDkey), '')+
      ') returning DITMCODE';
    ORD_IBS.ParamByName('DITMNAME').AsString:= pName;
    ORD_IBS.ExecQuery;
    if not (ORD_IBS.Eof and ORD_IBS.Bof) then
      pID:= ORD_IBS.FieldByName('DITMCODE').AsInteger;
    if pID<1 then raise Exception.Create(MessText(mtkErrAddRecord));
    ORD_IBS.Transaction.Commit;
    ORD_IBS.Close;

    prMessageLOGS(nmProc+': DIRINFOTYPEMODEL add id, type, TDid, Name= '+ // выводим новые в лог
      IntToStr(pID)+', '+IntToStr(pType)+', '+IntToStr(pTDkey)+', '+pName, 'import', false);

    CheckInfoModelItem(pID, pType, pTDkey, pTDkt, pName);  // проверяем / добавляем элемент
  except
    on E: Exception do Result:= nmProc+': '+E.Message;
  end;
  prFreeIBSQL(ORD_IBS);
  cntsOrd.SetFreeCnt(ORD_IBD);
end;
//================== найти код типа списка и код элемента списка по кодам TecDoc
function TTypesInfoModel.FindInfoItemByTDcodes(var pID, pTyp: Integer; pTDkey, pTDkt: Word): Boolean;
const nmProc = 'FindInfoItemByTDcodes';
var i: Integer;
begin
  Result:= False;
  pID:= 0;
  if not Assigned(self) then Exit;
  if pTyp<1 then for i:= Low(FTypeTDcodes) to High(FTypeTDcodes) do
    if FTypeTDcodes[i]=pTDkt then begin // ищем код типа списка по коду TD
      pTyp:= i;
      break;
    end;
  if FTypeLists.TypeOfListExists(pTyp) then with InfoModelList[pTyp] do
    for i:= 0 to Count-1 do with TTypeInfoModel(DirItems[Integer(Objects[i])]) do begin
      Result:= (TDcode=pTDkey) and (TDkt=pTDkt);
      if Result then begin
        pID:= ID;
        Exit;
      end;
    end;
end;

//******************************************************************************
//                                  TAutoTreeNode
//******************************************************************************
constructor TAutoTreeNode.Create(pID, pParentID, pMeasID, pSysID: Integer;
            pName, pNameSys: String; pMainCode: Integer=0; pCodeTD: Integer=0;
            pIsGATD: Boolean=False; pVisible: Boolean=True);
//            pIsGATD: Boolean=False; pVisible: Boolean=True; pIsEng: Boolean=False);
begin
  inherited Create(pID, pCodeTD, 0, pName, 0, False); // TDirItem без линков
  State    := True;
  FParCode := pParentID;    // Код узла родителя
  FNameSys := pNameSys;     // Наименование узла (служебное)
//  FChildren:= nil;
  FChildren:= fnCreateStringList(True, dupIgnore); // Список подчиненных узлов
  FMeasID  := pMeasID;
  FMainCode:= pMainCode;    // код TRNACODE главной ноды
  FTypeSys := pSysID;
  FOrderNum:= 0;            // порядк.номер ноды в дереве своей системы
  Visible  := pVisible;     // признак видимости ноды
  IsGATD   := pIsGATD;      // признак GA TecDoc (конечной ноды)
//  IsEng    := pIsEng;       // признак ноды двигателя
end;
//==============================================================================
destructor TAutoTreeNode.Destroy;
begin
  if not Assigned(self) then Exit;
  if Assigned(FChildren) then prFree(FChildren);
  inherited Destroy;
end;
//======================================================== признак конечной ноды
function TAutoTreeNode.GetIsEnding: boolean;
begin
  if not Assigned(self) then Result:= False
  else Result:= not Assigned(FChildren) or (FChildren.Count<1);
end;

{
//******************************************************************************
//                               TTreeNodes
//******************************************************************************
constructor TTreeNodes.Create(LengthStep: Integer=10);
begin
  inherited Create(LengthStep);
  SetLength(FItems, 1);
  FItems[0]:= TAutoTreeNode.Create(0, -1, 0, 0, 'Root', 'Root'); // корневой узел дерева
  FSysNodesLists:= TArraySysTypeLists.Create(False, True);  // списки нод дерева по системам учета последовательные по всем ветвям (Objects-Pointer TAutoTreeNode)
end;
//==============================================================================
destructor TTreeNodes.Destroy;
begin
  if not Assigned(self) then Exit;
  prFree(FSysNodesLists);
  inherited Destroy;
end;
//======================================================== Получить узел по коду
function TTreeNodes.GetNodeByID(pID: Integer): TAutoTreeNode;
begin
  Result:= nil;
  if not Assigned(self) or not ItemExists(pID) then Exit;
  Result:= TAutoTreeNode(FItems[pID]);
end;
//============ заполнить список деревьев системы последовательный по всем ветвям
procedure TTreeNodes.CheckSysNodes(SysID: integer);
var ii: Integer;
    Node: TAutoTreeNode;
  //----------------------------
  procedure GetNodes(pNode: TAutoTreeNode; syslist: TStringList);
  var i: Integer;
  begin
    if not Assigned(syslist) then exit;
    syslist.AddObject(pNode.Name, pNode);
    if Assigned(pNode.Children) then
      for i:= 0 to pNode.Children.Count-1 do
        GetNodes(TAutoTreeNode(pNode.Children.Objects[i]), syslist);
  end;
  //-----------------------------
begin
  if not Assigned(self) or not Assigned(NodeItems[0].Children) then Exit;
  if not FSysNodesLists.TypeOfListExists(sysID) then Exit;
  if (FSysNodesLists[sysID].Delimiter=LCharGood) then Exit;

  FSysNodesLists.CS_ATLists.Enter;
  try
    FSysNodesLists[sysID].Clear; // чистим список
    with NodeItems[0].Children do for ii:= 0 to Count-1 do begin // формируем список
      Node:= TAutoTreeNode(Objects[ii]);
      if (sysID=Node.TypeSys) then GetNodes(Node, FSysNodesLists[sysID]);
    end;
  finally
    FSysNodesLists.CS_ATLists.Leave;
  end;
end;
//=============== Получить список дерева системы последовательный по всем ветвям
function TTreeNodes.GetSysTree(SysID: integer): TStringList;
begin
  Result:= nil;
  if not Assigned(self) or not FSysNodesLists.TypeOfListExists(sysID) then Exit;
  CheckSysNodes(SysID);
  FSysNodesLists.CS_ATLists.Enter;
  try
    Result:= FSysNodesLists[sysID];
  finally
    FSysNodesLists.CS_ATLists.Leave;
  end;
end;
}
//******************************************************************************
//                               TAutoTreeNodes
//******************************************************************************
constructor TAutoTreeNodes.Create(pSysID: Integer=0);
begin
  inherited Create;
  SetLength(FNodeItems, 1);
  FNodeItems[0]:= TAutoTreeNode.Create(0, -1, 0, 0, 'Root', 'Root');
  FTypeSys := pSysID;
  FNodesList:= TStringList.Create;
  CS_Nodes:= TCriticalSection.Create; // Контроль FNodesList
end;
//==============================================================================
destructor TAutoTreeNodes.Destroy;
var i: Integer;
begin
  if not Assigned(self) then Exit;
  prFree(FNodesList);
  for i:= Low(FNodeItems) to High(FNodeItems) do
    if Assigned(FNodeItems[i]) then try prFree(FNodeItems[i]); except end;
  SetLength(FNodeItems, 0);
  prFree(CS_Nodes);
  inherited Destroy;
end;
//======================================================== Получить узел по коду
function TAutoTreeNodes.GetNodeByID(pID: Integer): TAutoTreeNode;
begin
  Result:= nil;
  if not Assigned(self) or not NodeExists(pID) then Exit;
  Result:= FNodeItems[pID];
end;
//=========================================================== Получить NodesList
function TAutoTreeNodes.GetNodesList: TStringList;
begin
  CS_Nodes.Enter;
  try
    Result:= FNodesList;
  finally
    CS_Nodes.Leave;
  end;
end;
//================================================================ Добавить узел
function TAutoTreeNodes.NodeAdd(var pID: Integer; pParentID, pMeasID: Integer; pName, pNameSys: String;
         pMainCode: Integer=0; pCodeTD: Integer=0; pIsGATD: Boolean=False; pVisible: Boolean=True;
         pCheckTreeDup: Boolean=False; pReList: Boolean=False): String;
var pNodeAdd, pNodeParent: TAutoTreeNode;
    j, jj: Integer;
begin
  if not Assigned(self) then begin
    Result:= MessText(mtkErrProcess);
    Exit;
  end;
  pNodeAdd:= nil;
  Result:= NodeValidCheckForAdd(pID, pParentID, pName, pNameSys, pNodeAdd, pNodeParent, pCheckTreeDup);
  if Result<>'' then Exit;
  if not Assigned(pNodeParent.Children) then
    pNodeParent.FChildren:= fnCreateStringList(True, dupIgnore);
  if High(FNodeItems)<pID then begin
    jj:= Length(FNodeItems);         // добавляем длину массива
    SetLength(FNodeItems, pID+100);  // и инициируем элементы
    for j:= jj to High(FNodeItems) do if j<>pID then FNodeItems[j]:= nil;
  end;
  FNodeItems[pID]:= TAutoTreeNode.Create(pID, pParentID, pMeasID, TypeSys, pName, PNameSys, 
             pMainCode, pCodeTD, pIsGATD, pVisible);
  pNodeParent.Children.AddObject(pName, FNodeItems[pID]);
  if pReList then FillNodesList; // если надо - перестраиваем список отсортированных узлов системы
end;
//========================================= Проверить валидность добавления узла
function TAutoTreeNodes.NodeValidCheckForAdd(pID, pParentID: Integer; pName, pNameSys: String;
         var pNodeAdd, pNodeParent: TAutoTreeNode; pCheckTreeDup: Boolean): String;
const nmProc = 'NodeValidCheckForAdd';
var idx, i: Integer;
begin
  Result:= '';
  try
    if not Assigned(self) then raise Exception.Create(MessText(mtkErrProcess));
    if pName='' then raise Exception.Create(MessText(mtkEmptyName));
    if pNameSys='' then raise Exception.Create(MessText(mtkEmptySysName));
    if not NodeGet(pParentID, pNodeParent) then
      raise Exception.Create(MessText(mtkNotFoundNode, IntToStr(pParentID)));  // Узел родителя не найден

    if NodeGet(pID, pNodeAdd) then // Узел с таким кодом имеется
      Exception.Create(MessText(mtkErrAddRecord));
//      if pNodeAdd.ParentID=pParentID then Exception.Create(MessText(mtkErrAddRecord))//Result:= -8 // узел родителя тот же  ???
//      else Exception.Create(MessText(mtkErrAddRecord));//Result:= -9;                               // родители разные      ???

    if (Result<>'') or not pCheckTreeDup then Exit;
  //   проверка на наличие товаров у ноды родителя ???
    if Assigned(pNodeParent.Children) then
      if pNodeParent.Children.Find(pName, idx) then begin
        pNodeAdd:= TAutoTreeNode(pNodeParent.Children.Objects[idx]);
        raise Exception.Create('Имя нового узла имеет дубликат среди детей родителя.'); // Имеется узел с таким именем среди детей узла, дубликат
      end;

    with NodesList do for i:= 0 to Count-1 do
      if Assigned(Objects[i]) then with TAutoTreeNode(Objects[i]) do try
        idx:= ID;
        if AnsiUpperCase(NameSys)=AnsiUpperCase(pNameSys) then
          raise Exception.Create(MessText(mtkDuplicateSysNm, pNameSys));  // Имеется узел с таким же системным именем, дубликат
      except
        prMessageLOGS(nmProc+': except - id='+IntToStr(idx), fLogCache, false);
      end;
  except
    on E: Exception do Result:= E.Message;
  end;
end;
//================================================================= Удалить узел
function TAutoTreeNodes.NodeDel(pNodeID: Integer): String;
const nmProc = 'NodeDel';
var NodeAuto, NodeParent: TAutoTreeNode;
    idxParent: Integer;
    ORD_IBD: TIBDatabase;
    ORD_IBS: TIBSQL;
begin
  Result:= '';
//  ORD_IBD:= nil;
  ORD_IBS:= nil;
  try
    if not Assigned(self) then raise Exception.Create(MessText(mtkErrDelRecord));
    if not NodeGet(pNodeID, NodeAuto) then
      raise Exception.Create(MessText(mtkNotFoundNode, IntToStr(pNodeID)));

    if not NodeAuto.IsEnding then
      raise Exception.Create('Узел имеет подчиненные узлы.');
    if not NodeGet(NodeAuto.ParentID, NodeParent) then
      raise Exception.Create('Не найден родительский узел.');

    ORD_IBD:= cntsORD.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise Exception.Create(MessText(mtkErrConnectToDB));
    try
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite); //  удаление в базе
      ORD_IBS.SQL.Text:= 'delete from TREENODESAUTO where TRNACODE='+IntToStr(pNodeId);
      ORD_IBS.ExecQuery;
      ORD_IBS.Transaction.Commit;
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD);
    end;

    idxParent:= NodeParent.Children.IndexOfObject(NodeAuto);  // Находим узел в списке подузлов
    CS_Nodes.Enter;
    try
      NodeParent.Children.Delete(idxParent); // Удаляем из списка детей
//      if NodeParent.Children.Count<1 then    // Если больше подузлов нет
//        prFree(NodeParent.FChildren);        // удалаем список детей
    finally
      CS_Nodes.Leave;
    end;

    idxParent:= NodesList.IndexOfObject(NodeAuto);  // Находим узел в списке общем
    if idxParent>-1 then try
      CS_Nodes.Enter;
      FNodesList.Delete(idxParent);
    finally
      CS_Nodes.Leave;
    end;
    prFree(NodeAuto);                      // Удаляем узел из памяти
  except
    on E: Exception do begin
      Result:= E.Message;
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
    end;
  end;
end;
//====================================================== Изменить параметры узла
function TAutoTreeNodes.NodeEdit(pNodeID, pMainCode, pVisible, pUserID: Integer; pName, pNameSys: String): String;
const nmProc = 'NodeEdit';
// pMainCode<1 - не менять, pVisible<0 - не менять, pName, pNameSys='' - не менять
var Node, NodeParent: TAutoTreeNode;
    idxParent, idxParent2, ResCode: Integer;
    ORD_IBD: TIBDatabase;
    ORD_IBS: TIBSQL;
    flUpdName, flUpdSysName, flUpdVis, flUpdMain: Boolean;
    s: String;
begin
  Result:= '';
//  ORD_IBD:= nil;
  ORD_IBS:= nil;
  idxParent:= -1;
  try
    if not Assigned(self) then raise Exception.Create(MessText(mtkErrEditRecord));
    if not NodeGet(pNodeID, Node) then
      Exception.Create(MessText(mtkNotFoundNode, IntToStr(pNodeID))); // узел с кодом pID не найден
    pName:= Trim(pName);
    pNameSys:= Trim(pNameSys);
    if (pNameSys<>'') then pNameSys:= AnsiUpperCase(pNameSys);

    flUpdName   := (pName<>'')    and (Node.Name<>pName);
    flUpdSysName:= (pNameSys<>'') and (Node.NameSys<>pNameSys);
    flUpdMain   := (pMainCode>0)  and (Node.MainCode<>pMainCode);
    flUpdVis    := (pVisible>-1)  and (Node.Visible<>(pVisible=1));

    if not flUpdName and not flUpdSysName and not flUpdVis and not flUpdMain then
      raise Exception.Create(MessText(mtkNotChanges));

    if not NodeGet(Node.ParentID, NodeParent) then
      raise Exception.Create('Не найден родительский узел.');

    if flUpdName then begin
      idxParent:= NodeParent.Children.IndexOfObject(Node);
      if NodeParent.Children.Find(pName, idxParent2) and (idxParent <> idxParent2) then
        raise Exception.Create('Новое имя узла имеет дубликат среди детей родителя.');
    end;

    if flUpdSysName then with NodesList do for idxParent2:= 0 to Count-1 do
      if Assigned(Objects[idxParent2]) then with TAutoTreeNode(Objects[idxParent2]) do
        if (ID<>pNodeID) and (AnsiUpperCase(NameSys)=pNameSys) then
          raise Exception.Create(MessText(mtkDuplicateSysNm, pNameSys)); // Имеется узел с таким же системным именем, дубликат

//------------------------------------------- изменение наименований и видимости
    if flUpdName or flUpdSysName or flUpdVis then begin

      ORD_IBD:= cntsORD.GetFreeCnt;
      if not Assigned(ORD_IBD) then raise Exception.Create(MessText(mtkErrConnectToDB));
      try
        ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite, True);  // в базе
        ORD_IBS.SQL.Text:= 'update TREENODESAUTO set '+
          fnIfStr(flUpdName,    'TRNANAME=:TRNANAME, ', '')+
          fnIfStr(flUpdSysName, 'TRNANAMESYS=:TRNANAMESYS, ', '')+
          fnIfStr(flUpdVis,     'TRNAVISIBLE='+fnIfStr((pVisible=1), '"T"', '"F"')+', ', '')+
          'TRNAUSERID='+IntToStr(pUserID)+' where TRNACODE='+IntToStr(pNodeID);
        if flUpdName    then ORD_IBS.ParamByName('TRNANAME').AsString:= pName;
        if flUpdSysName then ORD_IBS.ParamByName('TRNANAMESYS').AsString:= pNameSys;
        ORD_IBS.ExecQuery;
        ORD_IBS.Transaction.Commit;
      finally
        prFreeIBSQL(ORD_IBS);
        cntsOrd.SetFreeCnt(ORD_IBD);
      end;

      CS_Nodes.Enter;
      try
        if flUpdName then begin // если меняли имя
          Node.Name:= pName;
          fnChangeStringOfList(NodeParent.Children, idxParent, pName);
          FillNodesList; // перестраиваем список отсортированных узлов системы
        end;
        if flUpdSysName then Node.FNameSys:= pNameSys;
        if flUpdVis     then Node.Visible:= (pVisible=1);
      finally
        CS_Nodes.Leave;
      end;
    end;

    if flUpdMain then begin //---------------------- изменение кода главной ноды
      if (Node.ID<>Node.MainCode) then begin // отвязка подчиненной ноды
        ResCode:= resDeleted;
        s:= Cache.CheckLinkMainAndDupNodes(pNodeID, Node.MainCode, pUserID, ResCode);
        if ResCode=resError then raise Exception.Create(s);
      end;
      if (pMainCode<>Node.MainCode) then begin // привязка главной ноды
        ResCode:= resAdded;
        s:= Cache.CheckLinkMainAndDupNodes(pNodeID, pMainCode, pUserID, ResCode);
        if ResCode=resError then raise Exception.Create(s);
      end;  
    end;
  except
    on E: Exception do begin
      Result:= E.Message;
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
    end;
  end;
end;
//======================================================== Получить узел по коду
function TAutoTreeNodes.NodeGet(pID: Integer; var pNodeGet: TAutoTreeNode): Boolean;
begin
  Result:= False;
  pNodeGet:= nil;
  if not Assigned(self) then Exit;
  Result:= NodeExists(pID);
  if Result then pNodeGet:= FNodeItems[pID];
end;
//================================================= проверить существование узла
function TAutoTreeNodes.NodeExists(pID: Integer): Boolean;
begin
  Result:= False;
  if not Assigned(self) then Exit;
//  if pID<1 then Exit;
  if (Low(FNodeItems)>pID) or (High(FNodeItems)<pID) then Exit;
  Result:= Assigned(FNodeItems[pID]);
end;
//====================== заполнить список дерева последовательный по всем ветвям
procedure TAutoTreeNodes.FillNodesList;
var j: Integer;
  //----------------------------
  procedure GetNodes(pNode: TAutoTreeNode);
  var i: Integer;
  begin
    if pNode.ParentID>-1 then FNodesList.AddObject(pNode.Name, pNode);
    if Assigned(pNode.Children) then
      for i:= 0 to pNode.Children.Count-1 do
        GetNodes(TAutoTreeNode(pNode.Children.Objects[i]));
  end;
  //-----------------------------
begin
  if not Assigned(self) then Exit;
  try
    CS_Nodes.Enter;
    with FNodesList do begin
      FNodesList.Clear;        // заполняем список отсортированных узлов системы
      GetNodes(FNodeItems[0]); // и проставляем порядковый номер нодам
      for j:= 0 to Count-1 do TAutoTreeNode(Objects[j]).OrderNum:= j+1;
    end;
  finally
    CS_Nodes.Leave;
  end;
end;
//======================= Получить список дерева последовательный по всем ветвям
function TAutoTreeNodes.NoteGetTree: TStringList; // must Free Result
var j: Integer;
begin
  Result:= TStringList.Create;
  if not Assigned(self) then Exit;
  with FNodesList do begin
    if Count<1 then Exit;
    Result.Capacity:= Count;
    for j:= 0 to Count-1 do Result.AddObject(Strings[j], Objects[j]);
  end;
end;
//----- список дубликатов ноды pNodeID с ветвями, Object - Pointer TAutoTreeNode
//-------------------------- (ByTDid=True - по коду TecDoc, False - по MainCode)
function TAutoTreeNodes.GetDuplicateNodes(pNodeID: Integer; ByTDid: Boolean=False): TStringList; // must Free Result
var Node: TAutoTreeNode;
    i, index, DupID: Integer;
  //----------------------------
  procedure AddParents; // добавляем ветки
  begin
    if Node.ParentID<1 then Exit;
    if not NodeGet(Node.ParentID, Node) then Exit;
    if Result.IndexOfObject(Node)>-1 then Exit;  // если такой родитель уже есть - выходим
    Result.InsertObject(index, Node.Name, Node); // вставляем родительскую ноду впереди
    AddParents;                                  // идем дальше вверх
  end;
  //-----------------------------
begin
  Result:= TStringList.Create;
  if not Assigned(self) then Exit;
  if not NodeGet(pNodeID, Node) then Exit; // получаем ноду по коду
  if ByTDid then DupID:= Node.SubCode else DupID:= Node.MainCode; // код отбора нод
  with NodesList do for i:= 0 to Count-1 do begin
    Node:= TAutoTreeNode(Objects[i]);   // ищем другие с таким же кодом отбора
    if (Node.ID<>pNodeID) and
      ((ByTDid and (Node.SubCode=DupID)) or (not ByTDid and (Node.MainCode=DupID))) then begin
      index:= Result.AddObject(Node.Name, Objects[i]);
      AddParents; // добавляем ветки
    end;
  end;
end;
//============= must Free, список кодов дублирующих нод главной ноды pMainNodeID
function TAutoTreeNodes.GetDuplicateNodeCodes(pMainNodeID: Integer; OnlyVisible: Boolean=False): Tai;
var Node: TAutoTreeNode;
    i, index: Integer;
begin
  setlength(Result, 0);
  if not Assigned(self) or not NodeGet(pMainNodeID, Node) then Exit; // получаем ноду по коду
  if Node.ID<>Node.MainCode then Exit; // нода дублирующая
  index:= 0;
  with NodesList do for i:= 0 to Count-1 do begin
    Node:= TAutoTreeNode(Objects[i]);   // ищем другие с таким же кодом главной ноды
    if (Node.MainCode=pMainNodeID) and (Node.ID<>pMainNodeID)
      and (not OnlyVisible or Node.Visible) then begin
      if length(Result)<(index+1) then setlength(Result, index+10);
      Result[index]:= Node.ID;
      inc(index);
    end;
  end;
  if length(Result)>index then setlength(Result, index);
end;
//===================================== получить код главной ноды по коду TecDoc
function TAutoTreeNodes.GetMainNodeIDByTDcode(pTDnr: Integer): Integer;
var i: Integer;
begin
  Result:= -1;
  for i:= 1 to length(FNodeItems)-1 do if NodeExists(i) then
    with FNodeItems[i] do if IsGATD and (SubCode=pTDnr) then begin
      Result:= MainCode;
      exit;
    end;
end;
//============================================ получить код ноды по кодам TecDoc
function TAutoTreeNodes.GetNodeIDByTDcodes(nodeTD, parTD: Integer; IsGa: Boolean): Integer;
var i, par: Integer;
begin
  Result:= -1;
  if not Assigned(self) then Exit;
  for i:= 1 to High(FNodeItems) do begin
    if not NodeExists(i) then Continue;
    if (FNodeItems[i].SubCode<>nodeTD) then Continue; // узел TecDoc определяем по коду узла
    if IsGa then begin // группу TecDoc определяем по кодам группы и узла
      par:= FNodeItems[i].ParentID;
      if not NodeExists(par) then Continue;
      if (FNodeItems[par].SubCode<>parTD) then Continue;
    end;
    Result:= FNodeItems[i].ID;
    exit;
  end;
end;

//******************************************************************************
//                          TAutoTreeNodesSys
//******************************************************************************
constructor TAutoTreeNodesSys.Create;
var i, ii, j, jj: Integer;
begin
  SetLength(FarTreeNodes, SysTypes.Count+1);
  for ii:= Low(FarTreeNodes) to High(FarTreeNodes) do FarTreeNodes[ii]:= nil;
  with SysTypes do for i:= 0 to Count-1 do begin
    j:= GetDirItemID(ItemsList[i]);
    if High(FarTreeNodes)<j then begin
      jj:= Length(FarTreeNodes);     // добавляем длину массива
      SetLength(FarTreeNodes, j+1);  // и инициируем элементы
      for ii:= jj to High(FarTreeNodes) do FarTreeNodes[ii]:= nil;
    end;
    if not Assigned(FarTreeNodes[j]) then FarTreeNodes[j]:= TAutoTreeNodes.Create(j);
  end;
end;
//==============================================================================
destructor TAutoTreeNodesSys.Destroy;
var i: Integer;
begin
  if not Assigned(self) then Exit;
  for i:= Low(FarTreeNodes) to High(FarTreeNodes) do
    if Assigned(FarTreeNodes[i]) then try prFree(FarTreeNodes[i]); except end;
  SetLength(FarTreeNodes, 0);
  inherited Destroy;
end;
//================================== Получить ссылку на ноду по коду без системы
function TAutoTreeNodesSys.GetTreeNode(pID: Integer): TAutoTreeNode;
var i, j: Integer;
begin
  with SysTypes.ItemsList do for j:= 0 to Count-1 do begin
    i:= TSysItem(Items[j]).ID;
    if CheckTypeSys(i) then if GetTreeNodes(i).NodeGet(pID, Result) then exit;
  end;
  Result:= nil;
end;
//==================================== Получить ссылку на дерево по коду системы
function TAutoTreeNodesSys.GetTreeNodes(pSys: Integer): TAutoTreeNodes;
var ii, jj: Integer;
begin
  Result:= nil;
  if not Assigned(self) then Exit;
  if not CheckTypeSys(pSys) then Exit;
  if (High(FarTreeNodes)<pSys) then begin
    jj:= Length(FarTreeNodes);     // добавляем длину массива
    SetLength(FarTreeNodes, pSys+1);  // и инициируем элементы
    for ii:= jj to High(FarTreeNodes) do FarTreeNodes[ii]:= nil;
  end;
  if not Assigned(FarTreeNodes[pSys]) then
    FarTreeNodes[pSys]:= TAutoTreeNodes.Create(pSys);
  Result:= FarTreeNodes[pSys];
end;

//******************************************************************************
//                        TOriginalNumInfo
//******************************************************************************
constructor TOriginalNumInfo.Create(pID, pMfau: Integer; pNum: String; CS: TCriticalSection=nil);
begin
  pNum:= AnsiUpperCase(fnDelSpcAndSumb(fnDelEndOfStr(pNum)));
  inherited Create(pID, pMfau, 0, pNum, 0);
  FLinks:= TLinks.Create(CS);
end;
//============================================================== получить строку
function TOriginalNumInfo.GetStrON(const ik: T8InfoKinds): String;
var i, pType: Integer;
    analog: TWareInfo;
    arTypes: Tai;
begin
  Result:= '';
  if not Assigned(self) then Exit else case ik of
    ik8_1 : Result:= FName;
    ik8_2 : if Assigned(Cache.FDCA) then // имя производителя авто
              Result:= Cache.FDCA.Manufacturers[FSubCode].Name;
    ik8_3 : if Assigned(Cache.FDCA) then // строка для сортировки по произв.авто
              Result:= fnMakeAddCharStr(ManufName, 100, '0', True)+'*****'+FName;
    ik8_4 : if Assigned(Links) then // комментарий для Web с учетом типа товара
              with Links do try // собираем в Result строку из названий типов
                SetLength(arTypes, 0); // массив кодов типов для отсева дубляжа
                for i:= 0 to LinkCount-1 do begin     // идем по аналогам
                  analog:= GetLinkPtr(ListLinks[i]);
                  if analog.IsINFOgr then Continue;   // инфо пропускаем
                  pType:= analog.TypeID;
                  if (pType<1) then Continue;                         // тип не задан
                  if (fnInIntArray(pType, arTypes)>-1) then Continue; // тип уже был
                  Result:= Result+fnIfStr(Result='', '', ' / ')+Cache.GetWareTypeName(pType);
                  prAddItemToIntArray(pType, arTypes);
                end;
                Result:= 'OE'+fnIfStr(Result='', '', ', '+Result); // добавляем 'OE'
              finally
                SetLength(arTypes, 0);
              end; // with Links ... if (LinkCount>0)
  end; // case ik of
end;
//============================================================== записать строку
procedure TOriginalNumInfo.SetStrON(const ik: T8InfoKinds; Value: String);
var s: String;
begin
  if not Assigned(self) then Exit else case ik of
    ik8_1: begin
      s:= AnsiUpperCase(fnDelSpcAndSumb(fnDelEndOfStr(Value)));
      if (FName<>s) then FName:= s;
    end;
  end; // case ik of
end;
//================================================================= получить код
function TOriginalNumInfo.GetIntON(const ik: T8InfoKinds): Integer;
var i, pType: Integer;
    analog: TWareInfo;
begin
  Result:= 0;
  if not Assigned(self) then Exit else case ik of
    ik8_1 : Result:= FSubCode;                  // код производителя авто
    ik8_2 : if Assigned(Links) then             // код типа по связанным товарам
              with Links do if (LinkCount>0) then begin
                analog:= GetLinkPtr(ListLinks[0]);
                pType:= analog.TypeID; // берем тип первого
                for i:= 1 to LinkCount-1 do begin
                  analog:= GetLinkPtr(ListLinks[i]);
                  if (analog.TypeID<>pType) then Exit; // нашли другой тип - выходим
                end;
                Result:= pType;
              end; // with Links ... if (LinkCount>0)
  end; // case ik of
end;
//============================================================== записать строку
procedure TOriginalNumInfo.SetIntON(const ik: T8InfoKinds; Value: Integer);
begin
  if not Assigned(self) then Exit else case ik of
    ik8_1: if (FSubCode<1) or (FSubCode<>Value) then FSubCode:= Value;
  end; // case ik of
end;
//========================================================= массив кодов товаров
function TOriginalNumInfo.ArAnalogs: Tai;  // must Free
begin
  SetLength(Result, 0);
  if not Assigned(self) then Exit;
  Result:= Links.GetLinkCodes;
end;
//================================================== массив кодов типов аналогов
function TOriginalNumInfo.GetAnalogTypes(WithoutEmpty: Boolean=False): Tai; // must Free
var i, pType: Integer;
    analog: TWareInfo;
begin
  SetLength(Result, 0);
  if not Assigned(self) or not Assigned(Links) or (Links.LinkCount<1) then Exit;
  for i:= 0 to Links.LinkCount-1 do begin    // идем по аналогам
    analog:= GetLinkPtr(Links.ListLinks[i]);
    if analog.IsINFOgr then Continue;        // инфо пропускаем
    pType:= analog.TypeID;
    if WithoutEmpty and (pType<1) then Continue;       // если нужны только ненулевые типы
    if (fnInIntArray(pType, Result)>-1) then Continue; // тип уже был
    prAddItemToIntArray(pType, Result);
  end;
end;

//******************************************************************************
//                      TDataCacheAdditionASON
//******************************************************************************
constructor TDataCacheAdditionASON.Create;
begin
  inherited Create;
  CS_OrigNums:= TCriticalSection.Create;
  FManufacturers:= TManufacturers.Create;    // Производители
  FModelLines   := TModelLines.Create;       // Модельные ряды
  FModels       := TModelsAuto.Create;       // Модели
  SetLength(FarOriginalNumInfo, 0);
  FAutoTreeNodesSys:= TAutoTreeNodesSys.Create;
  FTypesInfoModel  := TTypesInfoModel.Create;
//  FTreeNodes       := TTreeNodes.Create(100);  // Дерево узлов
  FLinkSources     := TDirItems.Create;        // справочник источников информации
  FEngines         := TEngines.Create(100);
end;
//==============================================================================
destructor TDataCacheAdditionASON.Destroy;
const nmProc = 'FDCA_Destroy'; // имя процедуры/функции
var i: Integer;
    LocalStart: TDateTime;
begin
  if not Assigned(self) then Exit;
  LocalStart:= now();
  if Length(FarOriginalNumInfo)>0 then
    for i:= Low(FarOriginalNumInfo) to High(FarOriginalNumInfo) do
      if Assigned(arOriginalNumInfo[i]) then try prFree(arOriginalNumInfo[i]); except end;
  SetLength(FarOriginalNumInfo, 0);
  if flTest then begin
    prMessageLOGS(nmProc+'_OrigNums: - '+GetLogTimeStr(LocalStart), fLogDebug, false);
    LocalStart:= now();
  end;
  prFree(FManufacturers);
  if flTest then begin
    prMessageLOGS(nmProc+'_Manufs: - '+GetLogTimeStr(LocalStart), fLogDebug, false);
    LocalStart:= now();
  end;
  prFree(FModelLines);
  if flTest then begin
    prMessageLOGS(nmProc+'_MLines: - '+GetLogTimeStr(LocalStart), fLogDebug, false);
    LocalStart:= now();
  end;
  prFree(FModels);
  if flTest then begin
    prMessageLOGS(nmProc+'_Models: - '+GetLogTimeStr(LocalStart), fLogDebug, false);
    LocalStart:= now();
  end;
  prFree(FAutoTreeNodesSys);
  if flTest then begin
    prMessageLOGS(nmProc+'_Nodes: - '+GetLogTimeStr(LocalStart), fLogDebug, false);
    LocalStart:= now();
  end;
  prFree(FTypesInfoModel);
//  prFree(FTreeNodes);
  prFree(FLinkSources);
  prFree(FEngines);
  if flTest then prMessageLOGS(nmProc+'_Engines: - '+
                            GetLogTimeStr(LocalStart), fLogDebug, false);
  prFree(CS_OrigNums);
  inherited Destroy;
end;
//============================================== справочник источников в массиве
function TDataCacheAdditionASON.GetArLinkSources: Tas; // must Free
var i, j: Integer;
begin
  SetLength(Result, 0);
  with FLinkSources do for i:= 0 to ItemsList.Count-1 do begin
    j:= GetDirItemID(ItemsList[i]);
    if High(Result)<j then SetLength(Result, j+1);
    Result[j]:= GetDirItemName(ItemsList[i]);
  end;
end;
//=============================================== код источника по коду Grossbee
function TDataCacheAdditionASON.GetSourceByGBcode(srcGB: Integer): Integer;
var i, j: Integer;
begin
  Result:= 0;
  with FLinkSources do for i:= 0 to ItemsList.Count-1 do begin
    j:= GetDirItemSubCode(ItemsList[i]);
    if (j=srcGB) then begin
      Result:= GetDirItemID(ItemsList[i]);
      Exit;
    end;
  end;
end;
//======================================================= код источника Grossbee
function TDataCacheAdditionASON.GetSourceGBcode(src: Integer): Integer;
begin
  Result:= 0;
  with FLinkSources do if ItemExists(src) then Result:= GetDirItemSubCode(ItemsList[src]);
end;
//============================================ Добавить оригинальный номер в кеш
procedure TDataCacheAdditionASON.AddNewOrigNumToCache(OrigNum: TOriginalNumInfo; step: Integer=100);
var i, ii, jj: Integer;
begin
  if not Assigned(self) then Exit;
  i:= OrigNum.ID;
  try
    CS_OrigNums.Enter;
    if (High(FarOriginalNumInfo)<i) then begin
      jj:= Length(FarOriginalNumInfo);        // добавляем длину массива
      SetLength(FarOriginalNumInfo, i+step);  // и инициируем элементы
      for ii:= jj to High(FarOriginalNumInfo) do
       if ii<>i then FarOriginalNumInfo[ii]:= nil;
    end;
    FarOriginalNumInfo[i]:= OrigNum;
  finally
    CS_OrigNums.Leave;
  end;
end;
//=========================================================== тип системы модели
function TDataCacheAdditionASON.GetModelTypeSys(pModelID: Integer): Integer;
begin
  Result:= 0;
  if not Assigned(self) then Exit;
  try
    Result:= ModelLines[Models[pModelID].ModelLineID].TypeSys;
  except end;
end;
//========================================== Загрузка / обновление из базы в кеш
procedure TDataCacheAdditionASON.FillTypesInfoModel;
const nmProc = 'FillTypesInfoModel';
var ORD_IBS: TIBSQL;
    ORD_IBD: TIBDatabase;
    pTypeID, pID, pTDkey, pTDkt: Integer;
    pName: String;
begin
  if not Assigned(self) then Exit;
//  ORD_IBD:= nil;
  ORD_IBS:= nil;
  with TypesInfoModel do try
    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise Exception.Create(MessText(mtkErrConnectToDB));
    try
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpRead, True);
      ORD_IBS.SQL.Text:= 'select * from DIRTYPESINFOMODEL order by DTIMNAME';
      ORD_IBS.ExecQuery;
      while not ORD_IBS.Eof do begin
        pTypeID:= ORD_IBS.FieldByName('DTIMCODE').AsInteger;  // тип списка
        pName  := ORD_IBS.FieldByName('DTIMNAME').AsString;   // наименование типа
        FTypeLists.AddTypeOfList(pTypeID, pName);             // Добавить тип списка
        prCheckLengthIntArray(FTypeTDcodes, pTypeID); // добавляем длину массива, если надо, и инициируем элементы
        FTypeTDcodes[pTypeID]:= ORD_IBS.FieldByName('DTIMTDKEYTAB').AsInteger; // код KeyTable TDT
        cntsORD.TestSuspendException;
        ORD_IBS.Next;
      end;
      ORD_IBS.Close;

      ORD_IBS.SQL.Text:= 'select * from DIRINFOTYPEMODEL'+
        ' left join DIRTYPESINFOMODEL on DTIMCODE=DITMTYPEDIR'+
        ' order by DITMTYPEDIR, DITMNAME';
      ORD_IBS.ExecQuery;
      while not ORD_IBS.Eof do begin
        pTypeID:= ORD_IBS.FieldByName('DITMTYPEDIR').AsInteger;  // тип списка
        pID    := ORD_IBS.FieldByName('DITMCODE').AsInteger;     // Код
        pName  := ORD_IBS.FieldByName('DITMNAME').AsString;      // наименование
        if ORD_IBS.FieldIndex['DITMTDKEYENTR']<0 then pTDkey := 0 else
        pTDkey := ORD_IBS.FieldByName('DITMTDKEYENTR').AsInteger; // Код TDT
        if ORD_IBS.FieldIndex['DTIMTDKEYTAB']<0 then pTDkt := 0 else
        pTDkt := ORD_IBS.FieldByName('DTIMTDKEYTAB').AsInteger; // Код TDT

        CheckInfoModelItem(pID, pTypeID, pTDkey, pTDkt, pName);  // проверяем / добавляем элемент

        cntsORD.TestSuspendException;
        ORD_IBS.Next;
      end;
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD);
    end;
  except
    on E: Exception do prMessageLOGS(nmProc+': '+E.Message, fLogCache);
  end;
  TestCssStopException;
end;
//============================ Заполнение/обновление кеша производителей из базы
procedure TDataCacheAdditionASON.FillDirManuf(fFill: Boolean=True);
const nmProc = 'FillDirManuf';
var ORD_IBS: TIBSQL;
    ORD_IBD: TIBDatabase;
    TimeProc: TDateTime;
    i, fMFAUCODE, fMFNR, Result: Integer;
    fMFAUNAME: String;
    flNew, fLists, fl: boolean;
//fs: TFileStream;
begin
  if not Assigned(self) then Exit;
  ORD_IBS:= nil;
  TimeProc:= Now;
  Result:= 0;
  with Manufacturers do try

//fs:= TFileStream.Create('test', fmCreate); // файл

    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise Exception.Create(MessText(mtkErrConnectToDB));
    fLists:= fFill;
    if not fFill then SetStates(False);
    try
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpRead, true);
      ORD_IBS.SQL.Text:= 'select * from MANUFACTURERAUTO';
      ORD_IBS.ExecQuery;
      while not ORD_IBS.Eof do begin
        fMFAUCODE:= ORD_IBS.FieldByName('MFAUCODE').AsInteger;
        fMFAUNAME:= ORD_IBS.FieldByName('MFAUNAME').AsString;
        fMFNR    := ORD_IBS.FieldByName('MFAUTDMFNR').AsInteger;
        CS_Mfaus.Enter;
        try
          flNew:= True;                          // Создание производителя авто
          if CheckManufItem(fMFAUCODE, flNew, fMFAUNAME, fMFNR) then  // +100
            with FarManufacturers[fMFAUCODE] do begin
              fLists:= fLists or flNew;
              if flNew then begin
                IsMF    := GetBoolGB(ORD_IBS, 'MFAUISMFAU');      // Признак производителя
                IsMfAUTO:= GetBoolGB(ORD_IBS, 'MFAUISMFAUTO');    // Признак производителя авто
                IsMfMOTO:= GetBoolGB(ORD_IBS, 'MFAUISMFMOTO');    // Признак производителя мото
                IsMfTopA:= GetBoolGB(ORD_IBS, 'MFAUISTOPAUTO');   // Признак топ-производителя авто
                IsMfTopM:= GetBoolGB(ORD_IBS, 'MFAUISTOPMOTO');   // Признак топ-производителя мото
                IsMfVisA:= GetBoolGB(ORD_IBS, 'MFAUVISIBLEAUTO'); // Признак видимости производителя авто
                IsMfVisM:= GetBoolGB(ORD_IBS, 'MFAUVISIBLEMOTO'); // Признак видимости производителя мото
              end else begin
                if (CompareStr(Name, fMFAUNAME)<>0) then begin
                  fLists:= fLists or True;
                  Name:= fMFAUNAME;
                end;
                fl:= GetBoolGB(ORD_IBS, 'MFAUISMFAU');      // Признак производителя
                if fl<>IsMF then begin
                  IsMF:= fl;
                  fLists:= fLists or True;
                end;
                fl:= GetBoolGB(ORD_IBS, 'MFAUISMFAUTO');    // Признак производителя авто
                if fl<>IsMfAUTO then begin
                  IsMfAUTO:= fl;
                  fLists:= fLists or True;
                end;
                fl:= GetBoolGB(ORD_IBS, 'MFAUISMFMOTO');    // Признак производителя мото
                if fl<>IsMfMOTO then begin
                  IsMfMOTO:= fl;
                  fLists:= fLists or True;
                end;
                fl:= GetBoolGB(ORD_IBS, 'MFAUISTOPAUTO');   // Признак топ-производителя авто
                if fl<>IsMfTopA then begin
                  IsMfTopA:= fl;
                  fLists:= fLists or True;
                end;
                fl:= GetBoolGB(ORD_IBS, 'MFAUISTOPMOTO');   // Признак топ-производителя мото
                if fl<>IsMfTopM then begin
                  IsMfTopM:= fl;
                  fLists:= fLists or True;
                end;
                fl:= GetBoolGB(ORD_IBS, 'MFAUVISIBLEAUTO'); // Признак видимости производителя авто
                if fl<>IsMfVisA then begin
                  IsMfVisA:= fl;
                  fLists:= fLists or True;
                end;
                fl:= GetBoolGB(ORD_IBS, 'MFAUVISIBLEMOTO'); // Признак видимости производителя мото
                if fl<>IsMfVisM then begin
                  IsMfVisM:= fl;
                  fLists:= fLists or True;
                end;
                if SubCode<>fMFNR then FSubCode:= fMFNR;
                State:= True;
              end;
{
fs.Write(longint(fMFAUCODE), sizeof(longint));
s:= fnCodeString(Byte('S'), Name);
fnStreamWriteStringToAnsi(fs, s); // записываем
}
            end;
        finally
          CS_Mfaus.Leave;
        end;
        cntsORD.TestSuspendException;
        ORD_IBS.Next;
        Inc(Result);
      end;
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD);
    end;

    if not fFill then for i:= 1 to High(FarManufacturers) do
      if Assigned(FarManufacturers[i]) and not FarManufacturers[i].State then try
        CS_Mfaus.Enter;
        prFree(FarManufacturers[i]);
        fLists:= True;
      finally
        CS_Mfaus.Leave;
      end;

    fMFAUCODE:= Length(FarManufacturers);
    for i:= High(FarManufacturers) downto 1 do if Assigned(FarManufacturers[i]) then begin
      fMFAUCODE:= FarManufacturers[i].ID+1;
      break;
    end;
    if Length(FarManufacturers)>fMFAUCODE then try
      CS_Mfaus.Enter;
      SetLength(FarManufacturers, fMFAUCODE); // обрезаем по мах.коду
    finally
      CS_Mfaus.Leave;
    end;

    if fLists then with FSysManufListSort.ListTypes do for i:= 0 to Count-1 do begin
      fMFAUCODE:= integer(Objects[i]); // тип системы
      FSysManufListSort.SetTypeListDelimiter(fMFAUCODE, LCharUpdate);
      FSysManufListTopUp.SetTypeListDelimiter(fMFAUCODE, LCharUpdate);
    end;
{
fs.Position:= 0;
while fs.Position<(fs.Size-1) do begin
  fs.Read(longint(fMFAUCODE), Sizeof(longint));   // считываем
  s:= String(fnStreamReadAnsiString(fs));
  s:= fnDecodeString(Byte('S'), s);
prMessageLOGS(nmProc+': kod='+IntToStr(fMFAUCODE)+' - '+s, fLogCache+'_test', False);
end;
}
  except
    on E: Exception do prMessageLOGS(nmProc+': '+E.Message, fLogCache);
  end;
//prFree(fs);
  prMessageLOGS(nmProc+': '+IntToStr(Result)+' произв. - '+
    GetLogTimeStr(TimeProc), fLogCache, false);
  TestCssStopException;
end;
//==================================== Загрузка/ обновление данных из базы в кеш
procedure TDataCacheAdditionASON.FillDirModelLines(fFill: Boolean=True);
const nmProc = 'FillDirModelLines';
var ORD_IBS: TIBSQL;
    ORD_IBD: TIBDatabase;
    TimeProc: TDateTime;
    Code, MFAUID, pTypeSys, pMStart, pYStart, pMEnd, pYEnd, i, Result, pMLTD, ii, jj: Integer;
    pName: String;
    Tops, Visi, flNew, flLists: Boolean;
    manuf: TManufacturer;
begin
  if not Assigned(self) then Exit;
  ORD_IBS:= nil;
  TimeProc:= Now;
  Result:= 0;
  with ModelLines do try
    if not fFill then SetStates(False);
    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise Exception.Create(MessText(mtkErrConnectToDB));
    try
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpRead, True);
      ORD_IBS.SQL.Text:= 'select * from DIRMODELLINES'+
        ' order by DRMLMFAUCODE, DRMLDTSYCODE';
      ORD_IBS.ExecQuery;
      while not ORD_IBS.Eof do begin
        MFAUID:= ORD_IBS.FieldByName('DRMLMFAUCODE').AsInteger;   // Код производителя
        if not ManufAutoExist(MFAUID) then begin
          while not ORD_IBS.Eof and
            (MFAUID=ORD_IBS.FieldByName('DRMLMFAUCODE').AsInteger) do ORD_IBS.Next;
          Continue;
        end;

        pTypeSys:= ORD_IBS.FieldByName('DRMLDTSYCode').AsInteger;   // Тип системы
        if not CheckTypeSys(pTypeSys) then begin
          while not ORD_IBS.Eof and (MFAUID=ORD_IBS.FieldByName('DRMLMFAUCODE').AsInteger)
            and (pTypeSys=ORD_IBS.FieldByName('DRMLDTSYCode').AsInteger) do ORD_IBS.Next;
          Continue;
        end;

        manuf:= Manufacturers[MFAUID];
        flLists:= False; // признак переформирования списков
        while not ORD_IBS.Eof and (MFAUID=ORD_IBS.FieldByName('DRMLMFAUCODE').AsInteger)
          and (pTypeSys=ORD_IBS.FieldByName('DRMLDTSYCode').AsInteger) do begin
          Visi:= GetBoolGB(ORD_IBS, 'DRMLISVISIBLE');             // Признак видимости модельного ряда

          if not (Cache.AllowWebArm or Visi) then begin // кроме WebArm заполняем только видимые
            ORD_IBS.Next;
            Continue;
          end;

          Code   := ORD_IBS.FieldByName('DRMLCode').AsInteger;       // Код модельного ряда
          pName  := ORD_IBS.FieldByName('DRMLName').AsString;        // Наименование модельного ряда
          pMStart:= ORD_IBS.FieldByName('DRMLMonthStart').AsInteger; // Месяц начала производства
          pYStart:= ORD_IBS.FieldByName('DRMLYearStart').AsInteger;  // Год начала производства
          pMEnd  := ORD_IBS.FieldByName('DRMLMonthEnd').AsInteger;   // Месяц окончания производства
          pYEnd  := ORD_IBS.FieldByName('DRMLYearEnd').AsInteger;    // Год окончания производства
          Tops   := GetBoolGB(ORD_IBS, 'DRMLISTOP');                 // Топ
          pMLTD  := ORD_IBS.FieldByName('DRMLTDCODE').AsInteger;     // код TecDoc

          flNew:= not ModelLineExists(Code);
          CS_MLines.Enter;
          try
            if flNew then begin
              if High(FarModelLines)<Code then begin
                jj:= Length(FarModelLines);            // добавляем длину массива
                SetLength(FarModelLines, Code+1000);   // и инициируем элементы
                for ii:= jj to High(FarModelLines) do
                 if ii<>Code then FarModelLines[ii]:= nil;
              end;
              FarModelLines[Code]:= TModelLine.Create(Code, pMLTD, MFAUID, pName);
            end;
            with FarModelLines[Code] do if flNew then begin
              FTypeSys := pTypeSys;
              FMStart  := pMStart;
              FYStart  := pYStart;
              FMEnd    := pMEnd;
              FYEnd    := pYEnd;
              IsTop    := Tops;
              IsVisible:= Visi;
              flLists  := True;
            end else begin
              flLists:= flLists or (Name<>pName) or (FMStart<>pMStart) or (FYStart<>pYStart);
              Name:= pName;
              if FMStart<>pMStart then begin
                FMStart:= pMStart;
                flLists:= True;
              end;
              if FYStart<>pYStart then begin
                FYStart:= pYStart;
                flLists:= True;
              end;
              if IsVisible<>Visi then begin
                IsVisible:= Visi;
                flLists:= True;
              end;
              if IsTop<>Tops then begin
                IsTop:= Tops;
                flLists:= True;
              end;
              if MFAID      <> MFAUID   then MFAID     := MFAUID;
              if FTypeSys   <> pTypeSys then FTypeSys  := pTypeSys;
              if FMEnd      <> pMEnd    then FMEnd     := pMEnd;
              if FYEnd      <> pYEnd    then FYEnd     := pYEnd;
              if FSubCode   <> pMLTD    then FSubCode  := pMLTD;
              State:= True; // устанавливаем статус проверки
            end;
          finally
            CS_MLines.Leave;
          end;
          Inc(Result);
          cntsORD.TestSuspendException;
          ORD_IBS.Next;
        end;

        if flLists then with manuf do begin
          FManufSysMLsSort.SetTypeListDelimiter(pTypeSys, LCharUpdate);
          FManufSysMLsTopUp.SetTypeListDelimiter(pTypeSys, LCharUpdate);
        end;
      end;
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD);
    end;

    if not fFill then for i:= 1 to High(FarModelLines) do // убираем удаленные
      if Assigned(FarModelLines[i]) and not FarModelLines[i].State then try
        MFAUID:= FarModelLines[i].MFAID;
        pTypeSys:= FarModelLines[i].FTypeSys;
        Code:= FarModelLines[i].FID;
        if Cache.FDCA.ManufAutoExist(MFAUID) and CheckTypeSys(pTypeSys) then
          with Cache.FDCA.Manufacturers[MFAUID] do begin
            if FManufSysMLsSort[pTypeSys].Delimiter=LCharGood then  // если списки уже заполнены
              FManufSysMLsSort.DelTypeListItem(pTypeSys, Code);
            if FManufSysMLsTopUp[pTypeSys].Delimiter=LCharGood then
              FManufSysMLsTopUp.DelTypeListItem(pTypeSys, Code);
          end;
        CS_MLines.Enter;
        try
          prFree(FarModelLines[i]);
        finally
          CS_MLines.Leave;
        end;
      except end;

    Code:= Length(FarModelLines);
    for i:= High(FarModelLines) downto 1 do if Assigned(FarModelLines[i]) then begin
      Code:= FarModelLines[i].ID+1;
      break;
    end;
    if Length(FarModelLines)>Code then try
      CS_MLines.Enter;
      SetLength(FarModelLines, Code); // обрезаем по мах.коду
    finally
      CS_MLines.Leave;
    end;
  except
    on E: Exception do prMessageLOGS(nmProc+': '+E.Message, fLogCache);
  end;
  prMessageLOGS(nmProc+': '+IntToStr(Result)+' мод.рядов - '+
    GetLogTimeStr(TimeProc), fLogCache, false);
  TestCssStopException;
end;
//======== сортировка TList линков моделей - видимость + порядк.№ + наименование
function ModelsLinksSortCompare(Item1, Item2: Pointer): Integer;
var i1, i2: Integer;
    Model1, Model2: TModelAuto;
begin
  try
    Model1:= GetLinkPtr(Item1);
    Model2:= GetLinkPtr(Item2);
    if (Model1.IsVisible=Model2.IsVisible) then begin
      i1:= Model1.ModelOrderNum;
      i2:= Model2.ModelOrderNum;
      if i1=i2 then Result:= AnsiCompareText(Model1.SortName, Model2.SortName)
      else if i1<i2 then Result:= -1 else Result:= 1;
    end else if Model1.IsVisible then Result:= -1 else Result:= 1;
  except
    Result:= 0;
  end;
end;
//======================== Загрузка / обновление списка двигателей из базы в кеш
procedure TDataCacheAdditionASON.FillDirEngines(fFill: Boolean=True);
const nmProc = 'FillDirEngines';
var ORD_IBS: TIBSQL;
    ORD_IBD: TIBDatabase;
    TimeProc: TDateTime;
    j, engID, mfID: Integer;
    eps: TEngParams;
    eng: TEngine;
    flMfWares, fl: Boolean;
begin
  if not Assigned(self) then Exit;
  ORD_IBD:= nil;
  ORD_IBS:= nil;
  TimeProc:= Now;
  eps:= TEngParams.Create;
  j:= 0;
  with Engines do try
    if not fFill then SetDirStates(False); // сбрасываем флажки проверки двигателей
    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then  raise Exception.Create(MessText(mtkErrConnectToDB));
    ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpRead, True);
    ORD_IBS.SQL.Text:= 'select e.*,'+
      ' iif(exists(select * from LINKENGINENODE where LENDENGCODE=e.DENGCODE), 1, 0) hasNodes,'+
      ' iif(exists(select * from LINKENGINENODE inner join LINKENGNODEWARE on LENWLENCODE=LENCODE'+
      '   where LENDENGCODE=e.DENGCODE), 1, 0) hasWares'+
      ' from (select * from DIRENGINES order by DENGMFAU) e';
    ORD_IBS.ExecQuery;
    if not fFill then Engines.SetDirStates(False);
    while not ORD_IBS.Eof do begin
      mfID:= ORD_IBS.FieldByName('DENGMFAU').AsInteger;
      flMfWares:= False;
      if not Cache.FDCA.ManufAutoExist(mfID) then begin
        while not ORD_IBS.Eof and (mfID=ORD_IBS.FieldByName('DENGMFAU').AsInteger) do ORD_IBS.Next;
        Continue;
      end;

      while not ORD_IBS.Eof and (mfID=ORD_IBS.FieldByName('DENGMFAU').AsInteger) do begin
        engID:= ORD_IBS.FieldByName('DENGCODE').AsInteger;
        eng:= TEngine.Create(engID, ORD_IBS.FieldByName('DENGTDCODE').AsInteger,
          mfID, ORD_IBS.FieldByName('DENGENGINEMARK').AsString);
        if CheckItem(Pointer(eng)) then begin
          with eps do begin
            pYearFrom   := ORD_IBS.FieldByName('DENGMODFR').AsInteger div 100; // Год выпуска от
            pMonFrom    := ORD_IBS.FieldByName('DENGMODFR').AsInteger mod 100; // Месяц выпуска от
            pYearTo     := ORD_IBS.FieldByName('DENGMODTO').AsInteger div 100; // Год выпуска до
            pMonTo      := ORD_IBS.FieldByName('DENGMODTO').AsInteger mod 100; // Месяц выпуска до
            pCompFrom   := ORD_IBS.FieldByName('DENGCOMPFR').AsInteger;        // Компрессор * 100 от
            pCompTo     := ORD_IBS.FieldByName('DENGCOMPTO').AsInteger;        // Компрессор * 100 до
            pRPMtorqFrom:= ORD_IBS.FieldByName('DENGRPMTORQFR').AsInteger;     // Момент вращения (Nm) при [об/мин] от
            pRPMtorqTo  := ORD_IBS.FieldByName('DENGRPMTORQTO').AsInteger;     // Момент вращения (Nm) при [об/мин] до
            pBore       := ORD_IBS.FieldByName('DENGBORE').AsInteger;          // Расточка * 1000
            pStroke     := ORD_IBS.FieldByName('DENGSTROKE').AsInteger;        // Ход поршня * 1000
            pKWfrom     := ORD_IBS.FieldByName('DENGKWFR').AsInteger;          // Мощность кВт от
            pRPMKWfrom  := ORD_IBS.FieldByName('DENGRPMKWFR').AsInteger;       // при [об/мин] от
            pKWto       := ORD_IBS.FieldByName('DENGKWTO').AsInteger;          // Мощность кВт до
            pRPMKWto    := ORD_IBS.FieldByName('DENGRPMKWTO').AsInteger;       // при [об/мин] до
            pHPfrom     := ORD_IBS.FieldByName('DENGHPFR').AsInteger;          // Мощность ЛС от
            pHPto       := ORD_IBS.FieldByName('DENGHPTO').AsInteger;          // Мощность ЛС до
            pCCtecFrom  := ORD_IBS.FieldByName('DENGCCTECFR').AsInteger;       // Тех.обьем в куб.см. от
            pCCtecTo    := ORD_IBS.FieldByName('DENGCCTECTO').AsInteger;       // Тех.обьем в куб.см. до
            pVal        := ORD_IBS.FieldByName('DENGVAL').AsInteger;           // Количество клапанов
            pCyl        := ORD_IBS.FieldByName('DENGCYL').AsInteger;           // Количество цилиндров
            pCrank      := ORD_IBS.FieldByName('DENGCRANK').AsInteger;         // Кол-во подшипников коленвала
            pDesign     := ORD_IBS.FieldByName('DENGDESIGN').AsInteger;        // Код, Исполнение двигателя   (KT 96) (TYPEDIR=13)
            pFuelType   := ORD_IBS.FieldByName('DENGFUELTYPE').AsInteger;      // Код, Вид топлива            (KT 88) (TYPEDIR=12)
            pFuelMixt   := ORD_IBS.FieldByName('DENGFUELMIXT').AsInteger;      // Код, Система впрыска        (KT 97) (TYPEDIR=5)
            pAspir      := ORD_IBS.FieldByName('DENGASPIR').AsInteger;         // Код, Подача воздуха         (KT 99) (TYPEDIR=14)
            pType       := ORD_IBS.FieldByName('DENGTYPE').AsInteger;          // Код, Вид двигателя          (KT 80) (TYPEDIR=3)
            pNorm       := ORD_IBS.FieldByName('DENGNORM').AsInteger;          // Код, Норма выхлопных газов  (KT 63) (TYPEDIR=15)
            pCylDesign  := ORD_IBS.FieldByName('DENGCYLDESIGN').AsInteger;     // Код, Тип привода ГРМ        (KT 79) (TYPEDIR=16)
            pManag      := ORD_IBS.FieldByName('DENGMANAG').AsInteger;         // Код, Привод распредвала     (KT 77) (TYPEDIR=17)
            pValCnt     := ORD_IBS.FieldByName('DENGVALCNT').AsInteger;        // Код, Привод клапана         (KT 78) (TYPEDIR=18)
            pCoolType   := ORD_IBS.FieldByName('DENGCOOLTYPE').AsInteger;      // Код, Тип системы охлаждения (KT 76) (TYPEDIR=19)
            pSalesDesc  := ORD_IBS.FieldByName('DENGSALESDESC').AsString;      // Обозначение продажи
          end;
          eng:= DirItems[engID];
          eng.SetParams(eps, fFill);
          fl:= (ORD_IBS.FieldByName('hasNodes').AsInteger=1);
          if eng.EngHasNodes<>fl then eng.EngHasNodes:= fl;
          fl:= (ORD_IBS.FieldByName('hasWares').AsInteger=1);
          if eng.EngHasWares<>fl then eng.EngHasWares:= fl;
          if eng.EngHasWares and not flMfWares then flMfWares:= True;
          inc(j);
        end;
        cntsORD.TestSuspendException;
        ORD_IBS.Next;
      end;
      with Cache.FDCA.Manufacturers[mfID] do
        if MfHasEngWares<>flMfWares then MfHasEngWares:= flMfWares;
    end;
    ORD_IBS.Close;
    if not fFill then DelDirNotTested;
  except
    on E: Exception do prMessageLOGS(nmProc+': '+E.Message, fLogCache);
  end;
  prFreeIBSQL(ORD_IBS);
  cntsOrd.SetFreeCnt(ORD_IBD);
  prFree(eps);
  prMessageLOGS(nmProc+': '+IntToStr(j)+' двигателей - '+
    GetLogTimeStr(TimeProc), fLogCache, false);
  TestCssStopException;
end;
//=========================== Загрузка / обновление списка моделей из базы в кеш
procedure TDataCacheAdditionASON.FillDirModels(fFill: Boolean=True);
const nmProc = 'FillDirModels';
var ORD_IBS: TIBSQL;
    ORD_IBD: TIBDatabase;
    TimeProc: TDateTime;
    pName: String;      // Наименование Модели
    Code, ModelLineID, OrdNum, Result, pModelTDcode, ii, jj, iHasWares: Integer;
    Vis, Top, fadd, fmlupd, flmlhas: Boolean;
//    pModels: TList;
    mps: TModelParams;
//    mlinks: TLinkItems;
    armf: TBooleanDynArray;
begin
  if not Assigned(self) then Exit;
  ORD_IBD:= nil;
  ORD_IBS:= nil;
  TimeProc:= Now;
//  pModels:= TList.Create;
  mps:= TModelParams.Create;
  Result:= 0;
  SetLength(armf, Length(Manufacturers.FarManufacturers));
  with Models do try
    if not fFill then SetStates(False); // сбрасываем флажки проверки моделей
    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then  raise Exception.Create(MessText(mtkErrConnectToDB));
    ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpRead, True);
    ORD_IBS.SQL.Text:= 'select m.*, iif(exists(select * from LINKDETAILMODEL'+
      ' inner join LINKDETMODWARE on LDMWLDEMCODE=LDEMCODE and LDMWWRONG="F"'+
      ' inner join WareOptions on wowarecode=ldmwwarecode'+
      ' where LDEMDMOSCODE=m.DMOSCODE and LDEMWRONG="F" and WOARHIVED="F"), 1, 0) hasWares'+
      ' from DIRMODELS m order by DMOSDRMLCODE';
    ORD_IBS.ExecQuery;
    while not ORD_IBS.Eof do begin
      ModelLineID:= ORD_IBS.FieldByName('DMOSDRMLCODE').AsInteger; // Код модельного ряда
      if not ModelLines.ModelLineExists(ModelLineID) then begin
        while not ORD_IBS.Eof and
          (ModelLineID=ORD_IBS.FieldByName('DMOSDRMLCODE').AsInteger) do ORD_IBS.Next;
        Continue;
      end;

      fmlupd:= False;
      flmlhas:= False;
//      mlinks:= Cache.FDCA.ModelLines[ModelLineID].FLinks;          // линки мод.ряда на модели
//      pModels.Clear;
      while not ORD_IBS.Eof and (ModelLineID=ORD_IBS.FieldByName('DMOSDRMLCODE').AsInteger) do begin
        Vis           := GetBoolGB(ORD_IBS, 'DMOSISVISIBLE');      // Видимость модели

        if not (Cache.AllowWebArm or Vis) then begin // кроме WebArm заполняем только видимые
          ORD_IBS.Next;
          Continue;
        end;

        Code           := ORD_IBS.FieldByName('DMOSCODE').AsInteger;           // Код модели
        pName          := ORD_IBS.FieldByName('DMOSNAME').AsString;            // Наименование Модели
        Top            := GetBoolGB(ORD_IBS, 'DMOSISTOP');                     // Топ модель
        pModelTDcode   := ORD_IBS.FieldByName('DMOSTDCODE').AsInteger;         // Код TecDoc
        mps.pMStart    := ORD_IBS.FieldByName('DMOSMONTHSTART').AsInteger;     // Месяц начала выпуска
        mps.pYStart    := ORD_IBS.FieldByName('DMOSYEARSTART').AsInteger;      // Год начала выпуска
        mps.pMEnd      := ORD_IBS.FieldByName('DMOSMONTHEND').AsInteger;       // Месяц конца выпуска
        mps.pYEnd      := ORD_IBS.FieldByName('DMOSYEAREND').AsInteger;        // Год конца выпуска
        mps.pKW        := ORD_IBS.FieldByName('DMOSKW').AsInteger;             // Мошность кВт.
        mps.pHP        := ORD_IBS.FieldByName('DMOSHP').AsInteger;             // Мощность ЛС
        mps.pCCM       := ORD_IBS.FieldByName('DMOSCCM').AsInteger;            // Тех. объем куб. см.
        mps.pCylinders := ORD_IBS.FieldByName('DMOSCYLINDERS').AsInteger;      // Кол. цилиндров
        mps.pValves    := ORD_IBS.FieldByName('DMOSVALVES').AsInteger;         // Кол. клапанов на одну камеру сгорания
        mps.pBodyID    := ORD_IBS.FieldByName('DMOSBODYCODE').AsInteger;       // Тип кузова
        mps.pDriveID   := ORD_IBS.FieldByName('DMOSDRIVECODE').AsInteger;      // Тип привода
        mps.pEngTypeID := ORD_IBS.FieldByName('DMOSENGINETYPECODE').AsInteger; // Тип двигателя
        mps.pFuelID    := ORD_IBS.FieldByName('DMOSFUELCODE').AsInteger;       // Тип топлива
        mps.pFuelSupID := ORD_IBS.FieldByName('DMOSFUELSUPCODE').AsInteger;    // Система впрыска
        mps.pBrakeID   := ORD_IBS.FieldByName('DMOSBRAKECODE').AsInteger;      // Тип тормозной системы
        mps.pBrakeSysID:= ORD_IBS.FieldByName('DMOSBRAKESYSCODE').AsInteger;   // Тормозная система
        mps.pCatalID   := ORD_IBS.FieldByName('DMOSCATALCODE').AsInteger;      // Тип катализатора
        mps.pTransID   := ORD_IBS.FieldByName('DMOSTRANSCODE').AsInteger;      // Вид коробки передач
        OrdNum         := ORD_IBS.FieldByName('DMOSORDERNUM').AsInteger;       // порядк.№ модели
        iHasWares      := ORD_IBS.FieldByName('hasWares').AsInteger;           //
        fadd:= not ModelExists(Code);
        CS_Models.Enter;
        try
          if fadd then begin
            if High(FarModels)<Code then begin
              jj:= Length(FarModels);            // добавляем длину массива
              SetLength(FarModels, Code+1000);   // и инициируем элементы
              for ii:= jj to High(FarModels) do
               if ii<>Code then FarModels[ii]:= nil;
            end;
            FarModels[Code]:= TModelAuto.Create(Code, pModelTDcode, OrdNum, ModelLineID, pName);
//            pModels.Add(TLinkItem.Create(Code, 0, 0, FarModels[Code]));
          end;
          with FarModels[Code] do begin
            if fadd then begin
              FNodeLinks.dirNodes:= Cache.FDCA.AutoTreeNodesSys[TypeSys];
              IsVisible:= Vis;
              IsTop    := Top;
              fmlupd:= True;
            end else begin
              if CompareStr(Name, pName)<>0 then begin
                Name:= pName;
                fmlupd:= True;
              end;
              if (ModelOrderNum<>OrdNum) then begin
                ModelOrderNum:= OrdNum;
                fmlupd:= True;
              end;
              if (IsVisible<>Vis) then begin
                IsVisible:= Vis;
                fmlupd:= True;
              end;
              if (IsTop<>Vis) then begin
                IsTop:= Top;
                fmlupd:= Top;
              end;
              fmlupd:= fmlupd or (FParams.pMStart<>mps.pMStart) or (FParams.pYStart<>mps.pYStart);
              if SubCode<>pModelTDcode then FSubCode:= pModelTDcode; // Код TecDoc
            end;
            SetModelParams(mps, fadd);
            ModelHasWares:= iHasWares>0;
            flmlhas:= flmlhas or ModelHasWares; // признак наличия товаров модели
            State:= True;
          end; // with FarModels[Code]
        finally
          CS_Models.Leave;
        end;
        cntsORD.TestSuspendException;
        ORD_IBS.Next;
        Inc(Result);
      end;
//      if pModels.Count>0 then begin
//        mlinks.AddLinkItems(pModels);
//        mlinks.LinkSort(ModelsLinksSortCompare);
//      end;
      with ModelLines[ModelLineID] do begin
        if not MLHasWares and flmlhas then MLHasWares:= flmlhas;  // признак наличия товаров модельного ряда
        if MLHasWares then armf[MFAID]:= True;
        if fFill or fmlupd then FMLModelsSort.Delimiter:= lCharUpdate; // перестроить списки модельного ряда
      end;
    end;
    ORD_IBS.Close;

    for Code:= 1 to High(armf) do // Признаки наличия товаров по произв.
      if Manufacturers.ManufExists(Code) and armf[Code] then
        Manufacturers[Code].ManufHasWares:= True;

    if not fFill then for Code:= 1 to High(FarModels) do  // удаляем непроверенные
      if ModelExists(Code) and not FarModels[Code].State then begin
         with ModelLines.GetModelLine(FarModels[Code].ModelLineID) do
           if FMLModelsSort.Delimiter=lCharGood then ModelDelFromLine(Code); // удалить из списков модельного ряда
        CS_Models.Enter;
         try
  //          Cache.FDCA.ModelLines[FarModels[Code].ModelLineID].FLinks.DeleteLinkItem(Code);
          FarModels[Code].ClearLinks;
          prFree(FarModels[Code]);
        finally
          CS_Models.Leave;
        end;
      end;
    Code:= Length(FarModels);
    for OrdNum:= High(FarModels) downto 1 do if Assigned(FarModels[OrdNum]) then begin
      Code:= FarModels[OrdNum].ID+1;
      break;
    end;
    if Length(FarModels)>Code then try
      CS_Models.Enter;
      SetLength(FarModels, Code); // обрезаем по мах.коду
    finally
      CS_Models.Leave;
    end;
                                     //------------------- маркировки двигателей
    ORD_IBS.SQL.Text:= 'select LMENDMOSCODE, LMENDENGCODE'+
      ' from LINKMODELSENGINES where LMENWRONG="F" order by LMENDMOSCODE';
    ORD_IBS.ExecQuery;         // проверяем / заполняем список маркировок модели
    while not ORD_IBS.Eof do begin
      Code:= ORD_IBS.FieldByName('LMENDMOSCODE').AsInteger; 
      if not ModelExists(Code) then begin
        while not ORD_IBS.Eof and (Code=ORD_IBS.FieldByName('LMENDMOSCODE').AsInteger) do ORD_IBS.Next;
        Continue;
      end;
      with FarModels[Code].EngLinks do begin
        if not fFill then SetLinkStates(False);
        while not ORD_IBS.Eof and (Code=ORD_IBS.FieldByName('LMENDMOSCODE').AsInteger) do begin
          ModelLineID:= ORD_IBS.FieldByName('LMENDENGCODE').AsInteger;   // Код ORD
          if Engines.ItemExists(ModelLineID) then
            CheckLink(ModelLineID, 0, Engines[ModelLineID]);
          cntsORD.TestSuspendException;
          ORD_IBS.Next;
        end;
        if not fFill then DelNotTestedLinks;
        SortByLinkName;
      end;
    end;
    ORD_IBS.Close;
//    for OrdNum:= High(FarModels) downto 1 do if Assigned(FarModels[OrdNum]) then
//      prMessageLOGS(nmProc+': код модели '+IntToStr(OrdNum)+
//        ' -  марк. '+FarModels[OrdNum].MarksCommaText, fLogCache+'_test', false);    // отладка
  except
    on E: Exception do prMessageLOGS(nmProc+': '+E.Message, fLogCache);
  end;
  prFreeIBSQL(ORD_IBS);
  cntsOrd.SetFreeCnt(ORD_IBD, True);
  SetLength(armf, 0);
//  prFree(pModels);
  prFree(mps);
  prMessageLOGS(nmProc+': '+IntToStr(Result)+' моделей - '+
    GetLogTimeStr(TimeProc), fLogCache, false);
  TestCssStopException;
end;

//======== проставить признак наличия товаров линкам на ноду и родительские ноды
procedure SetHasWaresModelNodeParentLinks(Model: TModelAuto; NodeID: Integer; fHas: Boolean=True);
var link: TSecondLink;
    Node: TAutoTreeNode;
    i, j, ci, ii: Integer;
    codes: Tai;
    fbreak: Boolean;
    mlinks: TNodeLinks;
begin
  setLength(codes, 0);
  try
    i:= Model.TypeSys;
    with Cache.FDCA.AutoTreeNodesSys[i] do begin
      if not NodeGet(NodeID, Node) then Exit;
      j:= Node.MainCode; // код главной ноды
      codes:= GetDuplicateNodeCodes(j, True); // коды видимых дублирующих нод
      prAddItemToIntArray(j, codes);    // добавляем код главной ноды
    end;
    mlinks:= Model.NodeLinks;
    for ii:= High(codes) downto 0 do begin        // идем по кодам нод, начиная с главной
      nodeID:= codes[ii];
      link:= mlinks[nodeID];                      // связка с нодой - связка 2
      if not Assigned(link) then Continue;

      if (link.NodeHasWares=fHas) then Continue; // если признак тот, что надо

      link.NodeHasWares:= fHas;                   // ставим нужный признак наличия товаров
      Node:= GetLinkPtr(link);                    // ссылка на ноду
      if not Assigned(Node) then Continue;

      if fHas and not Node.Visible then fHas:= False; // на родителей невидимых нод признак True не проставляем

      repeat  // отрабатываем изменение признака вверх по дереву
        i:= Node.ParentID;
        link:= mlinks[i];                         // связка с нодой родителя
        if not Assigned(link) then break;
        if link.NodeHasWares=fHas then break;     // если признак наличия товаров нужный - дальше вверх по дереву не идем
        Node:= GetLinkPtr(link);                  // ссылка на ноду родителя
        if not Assigned(Node) then break;

        if not fHas and Assigned(Node.Children) then // если снять признак
          with Node.Children do begin                // проверяем детей родительской ноды
            fbreak:= False;                          // флаг для выхода из 2-х циклов
            for j:= 0 to Count-1 do begin
              ci:= TAutoTreeNode(Objects[j]).ID;     // код дитяти
              fbreak:= mlinks.LinkExists(ci) and     // если есть линк на дитя с другим признаком - выходим
                (TSecondLink(mlinks[ci]).NodeHasWares<>fHas);
              if fbreak then break;                  // выходим из for
            end;
            if fbreak then break;                    // выходим из repeat
          end; // with Node.Children

        link.NodeHasWares:= fHas; // ставим нужный признак наличия товаров линку на ноду родителя
      until i<0;
    end; // for ii:= High(codes) downto 0
  finally
    setLength(codes, 0);
  end;  
end;
//========================= Загрузка / обновление связей моделей с деревом узлов
procedure TDataCacheAdditionASON.FillModelNodeLinks;
const nmProc = 'FillModelNodeLinks';
var ORD_IBS: TIBSQL;
    ORD_IBD: TIBDatabase;
    TimeProc: TDateTime;
    lstLinks: TList;       // Список связок для пакетного добавления
    pTypeSys, tnID, index, j, Result, iCount, pModelID, err1, err2: Integer;
    pNode: TAutoTreeNode;
    Model: TModelAuto;
    haswares, hasFilters, flMfDebug, flSleep: Boolean;
    link2: TSecondLink;
    sDebugManufs: String;
  //---------------------- добавляем родителей / проставляем признак у родителей
  procedure AddParentLink(idp: Integer; hasw: Boolean);
  begin
    if (idp<1) then Exit; // если ноды нет в дереве - выходим
    index:= -1;
    if FAutoTreeNodesSys[pTypeSys].NodeGet(idp, pNode) then index:= pNode.OrderNum-1;
    if (index<0) then Exit;
    if not assigned(lstLinks[index]) then  // если линка на ноду нет в списке - добавляем
      lstLinks[index]:= TSecondLink.Create(0, 0, pNode, False, hasw)
    else begin
      link2:= lstLinks[index];
      if not hasw or link2.NodeHasWares then Exit; // если признака нет или у ноды он уже есть - выходим
      link2.NodeHasWares:= hasw;
    end;
    AddParentLink(pNode.ParentID, hasw); // добавляем родителей / проставляем признак у родителей
  end;
  //----------------------------------
begin
  Result:= 0;
  if not Assigned(self) then Exit;
  SleepFillLinksInt:= GetIniParamInt(nmIniFileBOB, 'intervals', 'SleepFillLinksInt', 10); // интервал задержки заполнения связок
  flSleep:= not flDebug and (SleepFillLinksInt>0) and fnGetActionTimeEnable(caeOnlyWorkTime);
  ORD_IBS:= nil;
  TimeProc:= Now;
  pNode:= nil;
  lstLinks:= TList.Create; // список связок для записи
  err1:= 0;
  err2:= 0;

  if Cache.WebAutoLinks then begin
    sDebugManufs:= GetIniParam(nmIniFileBOB, 'Threads', 'DebugManufsAuto');
    flMfDebug:= sDebugManufs<>'';
  end else flMfDebug:= False;

  try
    ORD_IBD:= cntsOrd.GetFreeCnt('', '', '', True);
    if not Assigned(ORD_IBD) then raise Exception.Create(MessText(mtkErrConnectToDB));
    try
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpRead, True);
      if flMfDebug then begin
        ORD_IBS.ParamCheck:= False;
        ORD_IBS.SQL.Add('execute block returns (DTSY integer, DMOS integer, TRNA integer,');
        ORD_IBS.SQL.Add('  aCOUNT double precision, SRC integer, hasWares integer, hasFilters integer)');
        ORD_IBS.SQL.Add('as declare variable MainNode integer=0;');
        ORD_IBS.SQL.Add('  declare variable ldem integer=0;');
        ORD_IBS.SQL.Add('  declare variable ldem1 integer=0;');
        ORD_IBS.SQL.Add('begin haswares=0; hasFilters=0; SRC=0; aCOUNT=0; TRNA=0; DMOS=0;');
        ORD_IBS.SQL.Add('  for select DRMLDTSYCODE, DMOSCODE from');
        ORD_IBS.SQL.Add('    (select DRMLDTSYCODE, DRMLCODE from DIRMODELLINES');
        ORD_IBS.SQL.Add('      where DRMLMFAUCODE in ('+sDebugManufs+') order by DRMLDTSYCODE)');
        ORD_IBS.SQL.Add('    inner join DIRMODELS on DMOSDRMLCODE=DRMLCODE');
        ORD_IBS.SQL.Add('    where exists(select * from LINKDETAILMODEL where LDEMDMOSCODE=DMOSCODE and LDEMWRONG="F")');
        ORD_IBS.SQL.Add('  into :DTSY, :DMOS do begin');
        ORD_IBS.SQL.Add('    for select LDEMTRNACODE, LDEMCOUNT, LDEMSRCLECODE, ldemcode,');
        ORD_IBS.SQL.Add('      iif(ldemfilters = "T", 1, 0), trnamaincode');
        ORD_IBS.SQL.Add('      from LINKDETAILMODEL left join treenodesauto on trnacode=LDEMTRNACODE');
        ORD_IBS.SQL.Add('      where LDEMDMOSCODE=:DMOS and LDEMWRONG="F"');
        ORD_IBS.SQL.Add('    into :TRNA, :aCOUNT, :src, :ldem1, :hasFilters, :MainNode do begin');
        ORD_IBS.SQL.Add('      if (MainNode<>TRNA) then begin');
        ORD_IBS.SQL.Add('        select ldemcode, iif(ldemfilters="T", 1, 0) from LINKDETAILMODEL');
        ORD_IBS.SQL.Add('          where LDEMDMOSCODE=:DMOS and LDEMTRNACODE=:MainNode into :ldem, :hasFilters;');
        ORD_IBS.SQL.Add('      end else ldem=ldem1;');
        ORD_IBS.SQL.Add('      if (exists(select * from linkdetmodware lw');
        ORD_IBS.SQL.Add('        inner join WareOptions on wowarecode=LDMWWARECODE and WOARHIVED="F"');
        ORD_IBS.SQL.Add('        where lw.ldmwldemcode=:ldem and LDMWWRONG="F")) then hasWares = 1; else hasWares = 0;');
        ORD_IBS.SQL.Add('      suspend; end end end');
      end else begin
        ORD_IBS.SQL.Text:= 'select * from GetModelNodeLinks('+
          fnIfStr(Cache.AllowWebArm, '0', '1')+', '+fnIfStr(Cache.WebAutoLinks, '0', '1')+')';
      end;
      ORD_IBS.ExecQuery;
      while not ORD_IBS.Eof do begin
        pTypeSys:= ORD_IBS.FieldByName('DTSY').AsInteger; // система
        with FAutoTreeNodesSys[pTypeSys].NodesList do // список отсортированных узлов системы
          iCount:= Count; // количество нод в дереве системы

        with lstLinks do if Count>0 then Clear; // заготовка линков по кол-ву узлов системы

        while not ORD_IBS.Eof and (pTypeSys=ORD_IBS.FieldByName('DTSY').AsInteger) do begin
          pModelID:= ORD_IBS.FieldByName('DMOS').AsInteger;
          if Models.ModelExists(pModelID) then Model:= Models[pModelID] else Model:= nil;
                                       // кроме WebArm заполняем только видимые
          if not Assigned(Model) or not (Cache.AllowWebArm or Model.IsVisible) then begin
            while not ORD_IBS.Eof and (pTypeSys=ORD_IBS.FieldByName('DTSY').AsInteger)
              and (pModelID=ORD_IBS.FieldByName('DMOS').AsInteger) do ORD_IBS.Next;
            Inc(err1);
            Continue;
          end;

          if lstLinks.Count<1 then with lstLinks do begin
            Capacity:= iCount;  // заполняем заготовку линков
            for j:= 0 to iCount-1 do Add(nil);
          end;

  //----------------------------------- обрабатываем 1 модель
          while not ORD_IBS.Eof and (pTypeSys=ORD_IBS.FieldByName('DTSY').AsInteger)
            and (pModelID=ORD_IBS.FieldByName('DMOS').AsInteger) do begin
            tnID := ORD_IBS.FieldByName('TRNA').AsInteger;  // Код узла
            index:= -1;
            if FAutoTreeNodesSys[pTypeSys].NodeGet(tnID, pNode) and pNode.IsEnding // если нода есть и конечная
              and ((pNode.ID=pNode.MainCode) or pNode.Visible) then // нода главная или видимая дублирующая
              index:= pNode.OrderNum-1;
            if index>-1 then begin // признак наличия товаров проставляем здесь, при заполнении связок 3 
              haswares:= ORD_IBS.FieldByName('hasWares').AsInteger=1; // неверные уберем (так быстрее работает)
              hasFilters:= ORD_IBS.FieldByName('hasFilters').AsInteger=1; // признак наличия фильтров в узле модели
              lstLinks[index]:= TSecondLink.Create(ORD_IBS.FieldByName('SRC').AsInteger, // заносим связку узла
                ORD_IBS.FieldByName('aCOUNT').AsFloat, pNode, True, haswares, hasFilters);
              Inc(Result);
              AddParentLink(pNode.ParentID, haswares); // добавляем родителей
            end else begin
              prMessageLOGS(nmProc+': not node '+IntToStr(tnID), fLogCache+'_test', false);
              Inc(err2);
            end; // if index>-1
            cntsORD.TestSuspendException;
            ORD_IBS.Next;
          end; // while ... (pModelID=
          Models[pModelID].NodeLinks.AddLinkItems(lstLinks); // заносим пакет отсортированных линков в кэш
          if flSleep then sleep(SleepFillLinksInt);
  //----------------------------------- обработали 1 модель
        end; // while ... (pTypeSys=
      end; // while not ORD_IBS.Eof
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD, True);
    end;
  except
    on E: Exception do prMessageLOGS(nmProc+': '+E.Message, fLogCache);
  end;
  lstLinks.Clear;
  prMessageLOGS(nmProc+': '+IntToStr(Result)+' св.уз-мод - '+
    GetLogTimeStr(TimeProc), fLogCache, false);
  if (err1>0) or (err2>0) then prMessageLOGS(nmProc+': no'+
    fnIfStr(err1>0, ' Models='+IntToStr(err1), '')+
    fnIfStr(err2>0, ' Nodes='+IntToStr(err2), ''), fLogCache, false);
  TestCssStopException;
end;
//========================================== Загрузка связей товара со связкой 2
procedure TDataCacheAdditionASON.FillWareModelNodeLinks;
const nmProc = 'FillWareModelNodeLinks';
var ORD_IBS: TIBSQL;
    ORD_IBD: TIBDatabase;
    TimeProc: TDateTime;
    WareID, ModelID, srcID, nodeID, i, Result, wm, wl, err1, err2, err3, sysID, iSleep: Integer;
    Ware: TWareInfo;
    Model: TModelAuto;
    link2: TSecondLink;
    nlinks: TNodeLinks;
    mlinks: TLinkList;
    arml, armf: TBooleanDynArray;
    flMfDebug, flSleep: Boolean;
    sDebugManufs: String;
begin
  if not Assigned(self) then Exit;
  SleepFillLinksInt:= GetIniParamInt(nmIniFileBOB, 'intervals', 'SleepFillLinksInt', 10); // интервал задержки заполнения связок
  flSleep:= not flDebug and (SleepFillLinksInt>0) and fnGetActionTimeEnable(caeOnlyWorkTime);
  ORD_IBD:= nil;
  ORD_IBS:= nil;
  TimeProc:= Now;
  Result:= 0;
  err1:= 0;
  err2:= 0;
  err3:= 0;
  iSleep:= 0;
  SetLength(arml, Length(ModelLines.FarModelLines));
  SetLength(armf, Length(Manufacturers.FarManufacturers));

  if Cache.WebAutoLinks then begin
    sDebugManufs:= GetIniParam(nmIniFileBOB, 'Threads', 'DebugManufsAuto');
    flMfDebug:= sDebugManufs<>'';
  end else flMfDebug:= False;

  with Models do try
    try
      ORD_IBD:= cntsOrd.GetFreeCnt('', '', '', True);
      if not Assigned(ORD_IBD) then raise Exception.Create(MessText(mtkErrConnectToDB));
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpRead, true);
      if flMfDebug then begin
        ORD_IBS.ParamCheck:= False;
        ORD_IBS.SQL.Add('execute block returns (DMOS integer, TRNA integer, WARE integer, SRC integer)');
        ORD_IBS.SQL.Add('as declare variable MainNode integer=0; declare variable ldem integer=0;');
        ORD_IBS.SQL.Add('  declare variable DTSY integer=0; begin DMOS=0; TRNA=0; WARE=0; SRC=0;');
        ORD_IBS.SQL.Add('  for select LDEMDMOSCODE, LDEMTRNACODE, LDMWWARECODE, LDMWSRCLECODE from');
        ORD_IBS.SQL.Add('    (select LDEMDMOSCODE, LDEMTRNACODE, ldemcode from (select DMOSCODE from DIRMODELS');
        ORD_IBS.SQL.Add('      inner join DIRMODELLINES on DRMLCODE=DMOSDRMLCODE and DRMLMFAUCODE in ('+sDebugManufs+'))');
        ORD_IBS.SQL.Add('      inner join LINKDETAILMODEL on LDEMDMOSCODE=DMOSCODE and LDEMWRONG="F" )');
        ORD_IBS.SQL.Add('    inner join linkdetmodware on ldmwldemcode=ldemcode and LDMWWRONG="F"');
        ORD_IBS.SQL.Add('    inner join WareOptions on wowarecode=LDMWWARECODE and WOARHIVED="F"');
        ORD_IBS.SQL.Add('    into :DMOS, :TRNA, :WARE, :SRC do if (not WARE is null) then suspend; end');
      end else
        ORD_IBS.SQL.Text:= 'select * from GetModelNodeWareLinks('+
          fnIfStr(Cache.AllowWebArm, '0', '1')+', '+fnIfStr(Cache.WebAutoLinks, '0', '1')+')';
      ORD_IBS.ExecQuery;
      Result:= 0;
      while not ORD_IBS.Eof do begin
        ModelID:= ORD_IBS.FieldByName('DMOS').AsInteger; // Код модели
        if ModelExists(ModelID) then Model:= FarModels[ModelID] else Model:= nil;
        if not Assigned(Model) or not Model.NodesExists
          or not (Cache.AllowWebArm or Model.IsVisible) then begin // кроме WebArm заполняем только видимые
          while not ORD_IBS.Eof and (ModelID=ORD_IBS.FieldByName('DMOS').AsInteger) do ORD_IBS.Next;
          inc(err1);
          Continue;
        end;

        wm:= 0;
        nlinks:= Model.NodeLinks;
        while not ORD_IBS.Eof and (ModelID=ORD_IBS.FieldByName('DMOS').AsInteger) do begin // 1 модель
          nodeID:= ORD_IBS.FieldByName('TRNA').AsInteger; // Код узла

          link2:= nlinks[nodeID]; // линк на ноду
          if not Assigned(link2) or (link2.LinkID<1) then begin
            while not ORD_IBS.Eof and (ModelID=ORD_IBS.FieldByName('DMOS').AsInteger)
              and (nodeID=ORD_IBS.FieldByName('TRNA').AsInteger) do ORD_IBS.Next;
            inc(err2);
            Continue;
          end;

          wl:= 0;
          while not ORD_IBS.Eof and (ModelID=ORD_IBS.FieldByName('DMOS').AsInteger)
            and (nodeID=ORD_IBS.FieldByName('TRNA').AsInteger) do begin // 1 узел = 1 связка 2
            WareID:= ORD_IBS.FieldByName('WARE').AsInteger;  // Код товара
            srcID   := ORD_IBS.FieldByName('SRC').AsInteger; // Код источника связи
            Ware:= Cache.GetWare(WareID, True);
            if Ware=NoWare then inc(err3)
            else begin
              link2.CheckDoubleLinks(Model.CS_mlinks);
              link2.DoubleLinks.AddLinkListItem(TLink.Create(srcID, Ware), lkLnkNone, Model.CS_mlinks); // связка 3
              Inc(Result); // счетчик связок с товарами всего
              Inc(wm);     // счетчик связок с товарами у модели
              Inc(wl);     // счетчик связок с товарами у линка на ноду
            end;
            cntsORD.TestSuspendException;
            ORD_IBS.Next;
          end; // while ... ModelID= ... and (nodeID=

          if (wl<1) and link2.NodeHasWares then begin // снять признак наличия товаров
            sysID:= Model.TypeSys;                 // линкам на ноду и родительские ноды
            if cache.FDCA.AutoTreeNodesSys[sysID].GetNodeByID(NodeID).MainCode=NodeID then
              SetHasWaresModelNodeParentLinks(Model, NodeID, False);
//  prMessageLOGS(nmProc+': not HasWares Model='+IntToStr(ModelID)+', Node='+IntToStr(NodeID), fLogCache+'_test', false);
          end;
        end; // while ... (ModelID=

        with Model do begin
          ModelHasWares:= (wm>0); // Признак наличия товаров по модели
          if ModelHasWares then arml[ModelLineID]:= True;
        end;
        if flSleep then sleep(SleepFillLinksInt);
      end; // while not ORD_IBS.Eof
      ORD_IBS.Close;
      prMessageLOGS(nmProc+': '+IntToStr(Result)+' св.тов-уз-мод - '+
        GetLogTimeStr(TimeProc), fLogCache, false);
      TimeProc:= Now;
      Result:= 0;
//---------------------------------------------------- связки товаров с моделями
      SleepFillLinksInt:= GetIniParamInt(nmIniFileBOB, 'intervals', 'SleepFillLinksInt', 10); // интервал задержки заполнения связок
      flSleep:= not flDebug and (SleepFillLinksInt>0) and not fnGetActionTimeEnable(caeSmallWork);
      if flMfDebug then begin
        ORD_IBS.SQL.Clear;
        ORD_IBS.SQL.Add('execute block returns (LDEMDMOSCODE integer, LDMWWARECODE integer)');
        ORD_IBS.SQL.Add('as declare variable MainNode integer=0; declare variable ldem integer=0;');
        ORD_IBS.SQL.Add('  declare variable DTSY integer=0; begin LDEMDMOSCODE = 0; LDMWWARECODE = 0;');
        ORD_IBS.SQL.Add('  for select wowarecode from WareOptions where WOARHIVED="F" and ');
        ORD_IBS.SQL.Add('    exists(select * from LINKDETMODWARE ');
        ORD_IBS.SQL.Add('      inner join LINKDETAILMODEL on LDEMCODE=LDMWLDEMCODE and LDEMWRONG="F"');
        ORD_IBS.SQL.Add('      inner join DIRMODELS on DMOSCODE=LDEMDMOSCODE');
        ORD_IBS.SQL.Add('      inner join DIRMODELLINES on DRMLCODE=DMOSDRMLCODE and DRMLMFAUCODE in ('+sDebugManufs+')');
        ORD_IBS.SQL.Add('    where LDMWWARECODE = wowarecode and LDMWWRONG="F")');
        ORD_IBS.SQL.Add('  order by wowarecode into :LDMWWARECODE do begin');
        ORD_IBS.SQL.Add('    for select LDEMDMOSCODE from (select LDMWLDEMCODE');
        ORD_IBS.SQL.Add('      from LINKDETMODWARE where LDMWWARECODE=:LDMWWARECODE and LDMWWRONG="F")');
        ORD_IBS.SQL.Add('      inner join LINKDETAILMODEL on LDEMCODE=LDMWLDEMCODE and LDEMWRONG="F"');
        ORD_IBS.SQL.Add('      inner join DIRMODELS on DMOSCODE=LDEMDMOSCODE');
        ORD_IBS.SQL.Add('      inner join DIRMODELLINES on DRMLCODE=DMOSDRMLCODE');
        ORD_IBS.SQL.Add('      where DRMLMFAUCODE in ('+sDebugManufs+') group by LDEMDMOSCODE');
        ORD_IBS.SQL.Add('    into :LDEMDMOSCODE do ');
        ORD_IBS.SQL.Add('    if (not LDEMDMOSCODE is null) then suspend; end end');
      end else
        ORD_IBS.SQL.Text:= 'select * from GetWareModelLinks('+      // _new
          fnIfStr(Cache.AllowWebArm, '0', '1')+', '+fnIfStr(Cache.WebAutoLinks, '0', '1')+')';
      ORD_IBS.ExecQuery;
      while not ORD_IBS.Eof do begin
        WareID:= ORD_IBS.FieldByName('LDMWWARECODE').AsInteger;  // Код товара
        if not Cache.WareExist(WareID) then begin
          while not ORD_IBS.Eof and (WareID=ORD_IBS.FieldByName('LDMWWARECODE').AsInteger) do ORD_IBS.Next;
          Continue;
        end;

        Ware:= Cache.GetWare(WareID);
        if Ware.IsArchive then begin
          while not ORD_IBS.Eof and (WareID=ORD_IBS.FieldByName('LDMWWARECODE').AsInteger) do ORD_IBS.Next;
          Continue;
        end;

        mlinks:= Ware.ModelLinks;
        while not ORD_IBS.Eof and (WareID=ORD_IBS.FieldByName('LDMWWARECODE').AsInteger) do begin
          ModelID:= ORD_IBS.FieldByName('LDEMDMOSCODE').AsInteger; // Код модели
          if ModelExists(ModelID) then Model:= FarModels[ModelID] else Model:= nil;

          if Assigned(Model) and Model.NodesExists
            and (Cache.AllowWebArm or Model.IsVisible) then begin // кроме WebArm заполняем только видимые модели
            mlinks.AddLinkListItem(Model, lkDirNone, Ware.CS_wlinks);
            Inc(Result); // счетчик связок с моделями
          end;

          cntsORD.TestSuspendException;
          ORD_IBS.Next;
        end;
        if flSleep then begin
          if iSleep<10 then inc(iSleep)
          else begin
            sleep(SleepFillLinksInt);
            iSleep:= 0;
          end;
        end;
      end;
//------------------------------------------------------------------------------
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD, True);
    end;

    with ModelLines do for i:= 1 to Length(arml)-1 do // Признаки наличия товаров по мод.ряду
      if ModelLineExists(i) and arml[i] then with ModelLines[i] do begin
        MLHasWares:= True;
        armf[MFAID]:= True;
      end;
    with Manufacturers do for i:= 1 to Length(armf)-1 do // Признаки наличия товаров по произв.
      if ManufExists(i) and armf[i] then
        Manufacturers[i].ManufHasWares:= True;
  except
    on E: Exception do prMessageLOGS(nmProc+': '+E.Message, fLogCache);
  end;
  SetLength(arml, 0);
  SetLength(armf, 0);
  prMessageLOGS(nmProc+': '+IntToStr(Result)+' св.тов-мод - '+
    GetLogTimeStr(TimeProc), fLogCache, false);
  if (err1>0) or (err2>0) or (err3>0) then prMessageLOGS(nmProc+': no'+
    fnIfStr(err1>0, ' Models='+IntToStr(err1), '')+
    fnIfStr(err2>0, ' Nodes='+IntToStr(err2), '')+
    fnIfStr(err3>0, ' Wares='+IntToStr(err3), ''), fLogCache, false);
  TestCssStopException;
end;
//=============================================== проверяем параметры связки 2
function TDataCacheAdditionASON.CheckNotValidModelNodeLinkParams(ModelID, NodeID: Integer;
         var Model: TModelAuto; var Node: TAutoTreeNode; var SysID: Integer; var errmess: string): Boolean;
begin
  if not Assigned(self) then errmess:= MessText(mtkErrProcess)
  else if not CheckNotValidModel(ModelID, SysID, Model, errmess) and  // проверяем модель
    not Cache.FDCA.AutoTreeNodesSys[SysID].NodeGet(NodeID, Node) then // проверяем ноду
    errmess:= MessText(mtkNotFoundNode, IntToStr(NodeID));
  Result:= errmess<>'';
end;
//============================ удаление / добавление условия применения связки 3
function TDataCacheAdditionASON.CheckWareModelNodeUsage(WareID, ModelID, NodeID: Integer;
         UsageName, UsageValue: String; var ResCode: Integer; srcID: Integer=0; userID: Integer=0): string;
const nmProc = 'CheckWareModelNodeUsage';
// вид операции - ResCode - на входе (resAdded, resDeleted, resAddOrEdit)
// ResCode на выходе: resError- ошибка, resDoNothing - не менялось, resAdded - добавлено,
// resDeleted - удалено, resAddOrEdit - добавлено или изменено
// UsageName - название критерия условия, UsageValue - значение критерия условия
var OpCode, SysID: Integer;
    Model: TModelAuto;
//    errmess: string;
    fAdd: Boolean;
    ORD_IBD: TIBDatabase;
    ORD_IBS: TIBSQL;
begin
  Result:= '';
  OpCode:= ResCode;
  ResCode:= resError;
  ORD_IBD:= nil;
  ORD_IBS:= nil;
  try
    if not (OpCode in [resAdded, resDeleted, resAddOrEdit]) then // проверяем код операции
      raise EBOBError.Create(MessText(mtkNotValidParam)+' операции');

    if not Models.ModelExists(ModelID) then              // проверяем модель
      raise EBOBError.Create(MessText(mtkNotFoundModel, IntToStr(ModelID)))
    else Model:= Models[ModelID];

    if not Cache.WareExist(WareID) or Cache.GetWare(wareID).IsArchive then // проверяем товар
      raise EBOBError.Create(MessText(mtkNotFoundWare, IntToStr(WareID)));

    SysID:= Model.TypeSys;
    if not AutoTreeNodesSys[SysID].NodeExists(NodeID) then
      raise EBOBError.Create(MessText(mtkNotFoundNode, IntToStr(NodeID)));

    with AutoTreeNodesSys[SysID][NodeID] do begin // переадресуем на главную ноду
      fAdd:= ID<>MainCode;
      if fAdd then NodeID:= MainCode;
    end;
    if fAdd and not AutoTreeNodesSys[SysID].NodeExists(NodeID) then // проверяем главную ноду
      raise EBOBError.Create(MessText(mtkNotFoundNode, IntToStr(NodeID)));

    if not Model.NodeLinks.DoubleLinkExists(nodeID, WareID) then        // проверяем связку 3
      raise EBOBError.Create('Не найдена '+MessText(mtkWareModNodeLink));

    fAdd:= OpCode<>resDeleted;
    if fAdd and ((srcID<1) or (userID<1)) then  // если добавление - проверяем srcID, userID
      raise EBOBError.Create(MessText(mtkNotParams));

    try
      ORD_IBD:= cntsOrd.GetFreeCnt; //---------------------------- запись в базу
      if not Assigned(ORD_IBD) then raise EBOBError.Create(MessText(mtkErrConnectToDB));
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite, True);
      ORD_IBS.SQL.Text:= 'select linkID, errLink from CheckModelNodeWareUsageLink('+
        IntToStr(ModelID)+', '+IntToStr(NodeID)+', '+IntToStr(WareID)+
        ', 0, :CriName, :CriValue, '+IntToStr(OpCode)+', '+IntToStr(UserID)+', '+IntToStr(srcID)+')';
      ORD_IBS.ParamByName('CriName').AsString:= UsageName;
      ORD_IBS.ParamByName('CriValue').AsString:= UsageValue;
      ORD_IBS.ExecQuery;

      if fAdd then begin // если добавление
        if (ORD_IBS.Eof and ORD_IBS.Bof) then
          raise EBOBError.Create(MessText(mtkErrAddRecord))
        else if (ORD_IBS.Fields[1].AsInteger>0) then
          EBOBError.Create(MessText(mtkWareModNodeUse)+' в базе отмечено, как неверное')
        else if (ORD_IBS.Fields[1].AsInteger<0) then begin // уже есть
          ResCode:= resDoNothing;
          Result:= 'Такое '+MessText(mtkWareModNodeUse)+' есть';
          if (OpCode=resAddOrEdit) and (ORD_IBS.Fields[0].AsInteger=-resAddOrEdit) then
            Result:= Result+', другие значения удалены';
        end else if (ORD_IBS.Fields[0].AsInteger=0) then
          raise Exception.Create('error add use link Model='+IntToStr(ModelID)+
            ' Node='+IntToStr(NodeID)+' Ware='+IntToStr(WareID)+
            ' CriName='+UsageName+' CriValue='+UsageValue)
        else if (OpCode=resAddOrEdit) and (ORD_IBS.Fields[0].AsInteger=-resAddOrEdit) then begin
          ResCode:= resEdited;
          Result:= MessText(mtkWareModNodeUse)+' изменено';
        end else begin
          ResCode:= resAdded;
          Result:= MessText(mtkWareModNodeUse)+' добавлено';
        end;

      end else begin  // если удаление
        if (ORD_IBS.Eof and ORD_IBS.Bof) then
          raise EBOBError.Create(MessText(mtkErrDelRecord))
        else if (ORD_IBS.Fields[0].AsInteger<1) then begin
          ResCode:= resDoNothing;
          Result:= 'Такое '+MessText(mtkWareModNodeUse)+' не найдено';
        end else begin
          ResCode:= resDeleted;
          Result:= MessText(mtkWareModNodeUse)+' удалено';
        end;
      end;
      ORD_IBS.Transaction.Commit;
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD);
    end;

    if (ResCode=resError) then ResCode:= OpCode; // на всяк.случай
  except
    on E: EBOBError do Result:= E.Message;
    on E: Exception do begin
      if (ResCode<>resError) then ResCode:= resError; // на всяк.случай
      Result:= CutEMess(E.Message);
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
    end;
  end;
end;
{
//===================== удаление / добавление порции условий применения связки 3
function TDataCacheAdditionASON.CheckWareModelNodeUseList(WareID, ModelID, NodeID: Integer;
         var UseList: TStringList; var ResCode: Integer; srcID: Integer=0; userID: Integer=0): string;
const nmProc = 'CheckWareModelNodeUseList';
// вид операции - ResCode - на входе (resAdded, resDeleted)
// ResCode на выходе: resError- ошибка, resDoNothing - не менялось, resAdded - добавлено, resDeleted - удалено
// UsageName - название критерия условия, UsageValue - значение критерия условия
var OpCode, iCriValue, SysID: Integer;
    Model: TModelAuto;
    errmess: String;
    fAdd: Boolean;
begin
  Result:= '';
  OpCode:= ResCode;
  ResCode:= resError;
  try
    if not (OpCode in [resAdded, resDeleted]) then       // проверяем код операции
      raise Exception.Create(MessText(mtkNotValidParam)+' операции');

    if not Models.ModelExists(ModelID) then              // проверяем модель
      raise Exception.Create(MessText(mtkNotFoundModel, IntToStr(ModelID)))
    else Model:= Models[ModelID];

    if not Cache.WareExist(WareID) then                  // проверяем товар
      raise Exception.Create(MessText(mtkNotFoundWare, IntToStr(WareID)));

    SysID:= Model.TypeSys;
    if not AutoTreeNodesSys[SysID].NodeExists(NodeID) then
      raise Exception.Create(MessText(mtkNotFoundNode, IntToStr(NodeID)));

    with AutoTreeNodesSys[SysID][NodeID] do begin // переадресуем на главную ноду
      fAdd:= ID<>MainCode;
      if fAdd then NodeID:= MainCode; 
    end;
    if fAdd and not AutoTreeNodesSys[SysID].NodeExists(NodeID) then // проверяем главную ноду
      raise Exception.Create(MessText(mtkNotFoundNode, IntToStr(NodeID)));

    if not Model.NodeLinks.DoubleLinkExists(nodeID, WareID) then        // проверяем связку 3
      raise Exception.Create('Не найдена '+MessText(mtkWareModNodeLink));

    fAdd:= OpCode<>resDeleted;
    if fAdd then begin // если добавление
      if ((srcID=0) or (userID=0)) then  // проверяем srcID, userID
        raise Exception.Create(MessText(mtkNotParams));
                                     // добавляем линки порции условий применения со связкой 3 в базу
      errmess:= Cache.CheckModelNodeWareUseListLinks(ModelID, nodeID, WareID, UserID, srcID, UseList);
      if (errmess='') then begin
        ResCode:= resAdded;
        Result:= MessText(mtkWareModNodeUses)+' добавлен';
      end else if (errmess='exists') then begin
        ResCode:= resDoNothing;
        raise Exception.Create('Такой '+MessText(mtkWareModNodeUses)+' есть');
      end else raise Exception.Create(errmess);

    end else begin  // если удаляем
      errmess:= Cache.DelModelNodeWareUseListLinks(ModelID, nodeID, WareID, UseList);
      if (errmess='') then begin
        ResCode:= resDeleted;
        Result:= MessText(mtkWareModNodeUses)+' удален';
      end else if (pos('не найден', errmess)>0) then begin
        ResCode:= resDoNothing;
        raise Exception.Create('Не найден '+MessText(mtkWareModNodeUses));
      end else raise Exception.Create(errmess);
    end;
  except
    on E: Exception do Result:= E.Message;
  end;
end;
}
//============================== удаление / добавление / редактирование связки 3
function TDataCacheAdditionASON.CheckWareModelNodeLink(WareID, ModelID, NodeID: Integer;
         var ResCode: Integer; srcID: Integer=0; userID: Integer=0): string;
const nmProc = 'CheckWareModelNodeLink';
// вид операции - ResCode - на входе (resAdded, resDeleted, resWrong, resNotWrong)
// ResCode на выходе: resError- ошибка, resDoNothing - не менялось, resAdded - добавлена,
// resDeleted - удалена, resWrong - отмечена, как неверная, resNotWrong - восстановлена
var ORD_IBD: TIBDatabase;
    ORD_IBS: TIBSQL;
    SysID, OpCode, linkSrc: Integer;
    Ware: TWareInfo;
    Model: TModelAuto;
    Node: TAutoTreeNode;
    mess, mess1: string;
    fex: Boolean;
    link2: TSecondLink;
begin
  Result:= '';
  if not Assigned(self) then Exit;
  OpCode:= ResCode;
  ResCode:= resError;
  ORD_IBD:= nil;
  ORD_IBS:= nil;
  mess1:= '';
  try
    if not (OpCode in [resAdded, resDeleted, resWrong, resNotWrong]) then       // проверяем код операции
      raise Exception.Create(MessText(mtkNotValidParam)+' операции');
//------------------------------------------------------ проверяем связку в кеше
    if CheckNotValidModelNodeLinkParams(ModelID, NodeID, Model, Node, SysID, mess) then
      raise Exception.Create(mess);

    if Node.ID<>Node.MainCode then begin // переадресуем на главную ноду
      NodeID:= Node.MainCode;
      if not AutoTreeNodesSys[SysID].NodeGet(NodeID, Node) then
        raise Exception.Create(MessText(mtkNotFoundNode, IntToStr(NodeID)));
    end;

    if not Model.NodeLinks.LinkExists(nodeID) then         // проверяем связку 2
      raise Exception.Create('Не найдена '+MessText(mtkModelNodeLink));

    Ware:= Cache.GetWare(WareID, True);
    if (Ware=NoWare) or Ware.IsArchive then                   // проверяем товар
      raise Exception.Create(MessText(mtkNotFoundWare, IntToStr(WareID)));

    fex:= Model.NodeLinks.DoubleLinkExists(nodeID, WareID); // проверяем связку 3
    mess:= MessText(mtkWareModNodeLink);

    case OpCode of
      resAdded, resNotWrong:
        if fex then begin
          if (OpCode=resAdded) then mess:= 'Такая '+mess+' есть'
          else mess:= mess+' не отмечена, как ошибочная';
          ResCode:= resDoNothing;
          raise Exception.Create(mess);
        end else if (userID<1) then
          raise Exception.Create(MessText(mtkNotValidParam)+' юзера')
        else if (OpCode=resAdded) and (srcID<1) then
          raise Exception.Create(MessText(mtkNotValidParam)+' источника');

      resDeleted, resWrong:
        if not fex then begin
          ResCode:= resDoNothing;
          raise Exception.Create('Не найдена '+mess);
        end else if (OpCode=resWrong) then begin
          if (userID<1) then raise Exception.Create(MessText(mtkNotValidParam)+' юзера');
        end else if (OpCode=resDeleted) then begin // проверяем источник связки
          linkSrc:= GetLinkSrc(Model.NodeLinks.GetDoubleLinks(nodeID).GetLinkListItemByID(WareID, lkLnkNone));
          if (linkSrc in [soTecDocBatch, soTDparts]) then begin
            srcID:= linkSrc;
            OpCode:= resWrong; // связки TecDoc не удаляем, а отмечаем неверными
            mess1:= ' (TecDoc)';
          end;
        end;
    end; // case
//--------------------------------------------------- отрабатываем запись в базу
    try
      ORD_IBD:= cntsOrd.GetFreeCnt;
      if not Assigned(ORD_IBD) then raise EBOBError.Create(MessText(mtkErrConnectToDB));
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite, True);
      case OpCode of
      resAdded: begin                // добавляем
          ORD_IBS.SQL.Text:= 'select linkID, errLink from AddModelNodeWareLink('+
            IntToStr(ModelID)+', '+IntToStr(NodeID)+', '+IntToStr(WareID)+', '+
            IntToStr(UserID)+', '+IntToStr(srcID)+')';
          ORD_IBS.ExecQuery;
          if (ORD_IBS.Bof and ORD_IBS.Eof) then
            raise EBOBError.Create(MessText(mtkErrAddRecord))
          else if (ORD_IBS.Fields[1].AsInteger=1) then
            raise EBOBError.Create(MessText(mtkWareModNodeLink)+' в базе отмечена, как неверная')
          else if (ORD_IBS.Fields[0].AsInteger<1) then
            raise EBOBError.Create(MessText(mtkErrAddRecord));
        end; // resAdded

      resWrong, resNotWrong: begin // меняем признак Wrong
          ORD_IBS.SQL.Text:= 'execute procedure SetModelNodeWareLinkWrongMark("'+
            fnIfStr(OpCode=resWrong, 'T', 'F')+'", '+IntToStr(ModelID)+', '+
            IntToStr(NodeID)+', '+IntToStr(WareID)+', '+IntToStr(UserID)+', '+IntToStr(srcID)+')';
          ORD_IBS.ExecQuery;
        end; // resWrong, resNotWrong

      resDeleted: begin              // удаляем
          ORD_IBS.SQL.Text:= 'execute procedure DelModelNodeWareLink('+
            IntToStr(ModelID)+', '+IntToStr(NodeID)+', '+IntToStr(WareID)+')';
          ORD_IBS.ExecQuery;
        end; // resDeleted
      end; // case
      ORD_IBS.Transaction.Commit;
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD);
    end;
//------------------------------------------------------------- отрабатываем кэш
    case OpCode of
      resAdded, resNotWrong: begin                // добавляем
        link2:= Model.NodeLinks[NodeID];
        link2.CheckDoubleLinks(Model.CS_mlinks);
        link2.DoubleLinks.AddLinkListItem(TLink.Create(srcID, Ware), lkLnkNone, Model.CS_mlinks); // связка 3
        SetHasWaresModelNodeParentLinks(Model, NodeID); // проставить признак наличия товаров линкам на ноду и родительские ноды
        if not Model.ModelHasWares then begin
          Model.ModelHasWares:= True; // Признаки наличия товаров по модели, мод.ряду и произв.
          if ModelLines.ModelLineExists(Model.ModelLineID) then
            with ModelLines[Model.ModelLineID] do begin
              if not MLHasWares then MLHasWares:= True;
              if Manufacturers.ManufExists(MFAID) then with Manufacturers[MFAID] do
                if not ManufHasWares then ManufHasWares:= True;
            end;
        end;
        with Ware.ModelLinks do if not LinkListItemExists(ModelID, lkDirNone) then begin
          AddLinkListItem(Model, lkDirNone, Ware.CS_wlinks);
          if Ware.ModelsSorting then Sort(WareModelsSortCompare);
        end;
      end; // resAdded, resNotWrong

      resDeleted, resWrong: begin              // удаляем
        link2:= Model.NodeLinks[NodeID];
        with link2.DoubleLinks do begin
          DelLinkListItemByID(WareID, lkLnkNone, Model.CS_mlinks); // связка 3
          if Count<1 then begin    // если это был последний товар - снять признак наличия товаров у линка на ноду
            SetHasWaresModelNodeParentLinks(Model, NodeID, False); // и проверить родительские ноды
          end;
        end;                          // отрабатываем связки товара с моделями
        with Model.NodeLinks do for SysID:= 0 to ListLinks.Count-1 do begin
          NodeID:= GetLinkID(ListLinks[SysID]);
          if GetDoubleLinks(NodeID).LinkListItemExists(WareID, lkLnkNone) then begin
            ModelID:= 0; // если у модели есть еще линки на этот товар - обнуляем код модели
            break;
          end;
        end;
        if (ModelID>0) then Ware.ModelLinks.DelLinkListItemByID(ModelID, lkDirNone, Ware.CS_wlinks);
      end; // resDeleted, resWrong
    end; // case
    Ware.CheckHasModels(Model.TypeSys);

    case OpCode of
      resAdded:    Result:= mess+' добавлена';
      resDeleted:  Result:= mess+' удалена';
      resWrong:    Result:= mess+mess1+' отмечена, как неверная';
      resNotWrong: Result:= mess+' восстановлена';
    end;
    ResCode:= OpCode;
  except
    on E: Exception do Result:= CutEMess(E.Message, ResCode);
  end;
end;
//============================= редактирование связки 2 по группе дубликатов нод
function TDataCacheAdditionASON.CheckModelNodeLinkDup(ModelID, NodeID: Integer; Value: String;
         var ResCode: Integer; srcID: Integer=0; userID: Integer=0): string;
const nmProc = 'CheckModelNodeLinkDup';
// вид операции: Value='' - удаление, Value<>'': связка есть - редактирование, связки нет - добавление
// ResCode на выходе: resError- ошибка, resDoNothing - не менялось,
// resAdded - добавление, resEdited - редактирование, resDeleted - удаление
type
  TActionKind = (akAdd, akEdit, akDel, akNot, akSet, akNothing, akUnknown); // вид операции
  TNodeLinkData = record   // данные связки с нодой из группы дубликатов
    d_NodeID: Integer;     // ID ноды
    d_Act: TActionKind;    // вид операции в кэше, базе
    d_Link: TSecondLink;   // ссылка на связку 2 с заданной моделью
    d_Node: TAutoTreeNode; // ссылка на ноду
  end;
var ORD_IBD: TIBDatabase;
    ORD_IBS: TIBSQL;
    i, ii, j, iAll, SysID, NodeMainCode, DelCount, AddCount, EditCount, ErrCount: Integer;
    Model: TModelAuto;
    Node, NodeMain: TAutoTreeNode;
    errmess: string;
    pQty: Double;
    fDel: Boolean;
    NextLink: TSecondLink;
    lstNodes: TStringList; // Список узлов для сортировки
    codes: Tai;
    DataNodes: array of TNodeLinkData;
    ls: TLinkList;
begin
  Result:= '';
  ResCode:= resError;
  pQty:= 0;
  ORD_IBD:= nil;
  ORD_IBS:= nil;
  lstNodes:= nil;
  SetLength(codes, 0);
  SetLength(DataNodes, 0);
  DelCount:= 0;
  AddCount:= 0;
  EditCount:= 0;
  ErrCount:= 0;
  try
    Value:= trim(Value);
    fDel:= Value=''; // флаг удаления
    if not fDel then begin //----------------- проверяем корректность количества
      Value:= StrWithFloatDec(Value); // проверяем DecimalSeparator
      pQty:= StrToFloatDef(Value, 99999);
      if not fnNotZero(pQty-99999) then
        raise Exception.Create(MessText(mtkNotValidParam)+' количества');
    end;
    //------------------------------ получаем и проверяем систему, модель и входную ноду
    if CheckNotValidModelNodeLinkParams(ModelID, NodeID, Model, Node, SysID, errmess) then
      raise Exception.Create(errmess);

    NodeMainCode:= Node.MainCode; //--------------------- проверяем главную ноду
    if NodeID=NodeMainCode then NodeMain:= Node
    else if not AutoTreeNodesSys[SysID].NodeGet(NodeMainCode, NodeMain) then
      raise Exception.Create(MessText(mtkNotFoundNode, IntToStr(NodeMainCode)));
            // коды дублирующих нод (для удаления - всех, иначе только видимых)
    codes:= AutoTreeNodesSys[SysID].GetDuplicateNodeCodes(NodeMainCode, not fDel);

    //------------------------------ заполняем данные всех нод группы дубликатов
    SetLength(DataNodes, Length(codes)+1);
    with DataNodes[0] do begin  // сначала - главная нода
      d_NodeID:= NodeMainCode;
      d_Node:= NodeMain;
      d_link:= Model.NodeLinks[NodeMainCode]; // если нет - возвращает nil
    end;
    for i:= 0 to High(codes) do with DataNodes[i+1] do begin // дублирующие ноды
      d_NodeID:= codes[i];
      if d_NodeID=NodeID then d_Node:= Node // если входная нода была дублирующей
      else d_Node:= AutoTreeNodesSys[SysID][d_NodeID];
      d_link:= Model.NodeLinks[d_NodeID]; // если нет - возвращает nil
    end;

    //------------------------------------- проверяем все ноды: что нужно делать
    for i:= 0 to High(DataNodes) do with DataNodes[i] do
      if fDel then begin // проверяем удаление
        if not Assigned(d_link) or not d_link.IsLinkNode then begin // связки 2 нет
          d_Act:= akNothing;
          Continue;
        end;
        if Assigned(d_link.DoubleLinks) and (d_link.DoubleLinks.Count>0) then  // проверяем связки 3
          raise Exception.Create(MessText(mtkModelNodeLink)+' имеет связи с товарами');
        d_Act:= akDel;       // удалить связку 2
        inc(DelCount);

      end else if not Assigned(d_link) then begin // проверяем добавление
        if not Assigned(d_Node) then
          raise Exception.Create(MessText(mtkNotFoundNode, IntToStr(d_NodeID)));
        if not d_Node.IsEnding then
          raise Exception.Create('Узел '+d_Node.Name+' имеет дочерние узлы');
        d_Act:= akAdd;       // добавить связку 2
        inc(AddCount);

      end else if fnNotZero(d_link.Qty-pQty) then begin // редактирование - проверяем количество
        d_Act:= akEdit;      // изменить количество
        inc(EditCount);
      end else d_Act:= akNothing; // for i:= 0 to High(DataNodes)

    if (DelCount=0) and (AddCount=0) and (EditCount=0) then begin
      ResCode:= resDoNothing;
      raise Exception.Create(MessText(mtkNotChanges));
    end;
    if (AddCount>0) // если есть добавление - проверяем srcID, userID
      and ((srcID<1) or (userID<1)) then raise Exception.Create(MessText(mtkNotParams));

    //------- отрабатываем запись в базу по главной ноде, дублирующие - триггеры
    with DataNodes[0] do if (d_Act in [akAdd, akDel, akEdit]) then try
      ORD_IBD:= cntsOrd.GetFreeCnt;
      if not Assigned(ORD_IBD) then raise Exception.Create(MessText(mtkErrConnectToDB));
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite, True);
      case d_Act of
        akAdd: begin // добавляем
          ORD_IBS.SQL.Text:= 'insert into LINKDETAILMODEL (LDEMDMOSCODE, LDEMTRNACODE, '+
            ' LDEMCOUNT, LDEMSRCLECODE, LDEMUSERID) values ('+IntToStr(ModelID)+', '+
            IntToStr(d_NodeID)+', :LDEMCOUNT, '+IntToStr(srcID)+', '+IntToStr(userID)+')';
          ORD_IBS.ParamByName('LDEMCOUNT').AsFloat:= pQty;
        end;

        akDel:  // удаляем
          ORD_IBS.SQL.Text:= 'delete from LINKDETAILMODEL where LDEMTRNACODE='+
            IntToStr(d_NodeID)+' and LDEMDMOSCODE='+IntToStr(ModelID);

        akEdit: begin // меняем количество
          ORD_IBS.SQL.Text:= 'update LINKDETAILMODEL set LDEMCOUNT=:LDEMCOUNT, LDEMUSERID='+IntToStr(userID)+
            ' where LDEMTRNACODE='+IntToStr(d_NodeID)+' and LDEMDMOSCODE='+IntToStr(ModelID);
          ORD_IBS.ParamByName('LDEMCOUNT').AsFloat:= pQty;
        end;
      end; // case
      ORD_IBS.ExecQuery;
      ORD_IBS.Transaction.Commit;
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD);
    end; // with DataNodes[0]

    //--------------------------------------------------------- отрабатываем кэш
    if (AddCount>0) then
      lstNodes:= AutoTreeNodesSys[SysID].NodesList; // полный список отсортированных узлов дерева

    for i:= Low(DataNodes) to High(DataNodes) do with DataNodes[i] do try
      case d_Act of
        akAdd: begin // добавляем связку 2 и линки на родителей
          d_Link:= TSecondLink.Create(srcID, pQty, d_Node, True); // новый линк
          repeat // идем по дереву
//            iAll:= -1;
            NextLink:= nil;
//            if Assigned(d_Node) then iAll:= d_Node.OrderNum-1; // индекс ноды в общем дереве
            iAll:= FindNodeIndex(d_NodeID, lstNodes); // ищем индекс ноды в общем дереве
            if iAll>-1 then for ii:= iAll+1 to lstNodes.Count-1 do // ищем следующую ноду, на которую есть линк у модели
              if Assigned(lstNodes.Objects[ii]) then begin
                j:= TAutoTreeNode(lstNodes.Objects[ii]).ID; // проверяем наличие линков на ноду общего дерева
                if Model.NodeLinks.LinkExists(j) then begin
                  NextLink:= Model.NodeLinks[j]; // линк на следующую ноду общего дерева
                  break;
                end;
              end;
            Model.NodeLinks.InsertLink(d_Link, NextLink); // Insert перед NextLink, если NextLink=nil - Add
            d_NodeID:= d_Node.ParentID;
            if (d_NodeID<1) or Model.NodeLinks.LinkExists(d_NodeID) then break; // проверяем линк на ноду родителя
            d_Node:= AutoTreeNodesSys[SysID].GetNodeByID(d_NodeID); // нода родителя
            if not Assigned(d_Node) then break;
            d_Link:= TSecondLink.Create(0, 0, d_Node, False); // линк на ноду родителя
          until not Assigned(d_Node);
        end; // akAdd

        akDel: repeat // удаляем линк и пустую часть ветки
          ls:= d_link.DoubleLinks;
          prFree(ls);
          Model.NodeLinks.DeleteLinkItem(d_link); // удаляем линк
          d_NodeID:= d_Node.ParentID;         // код ноды родителя
          if (d_NodeID<1) or not Model.NodeLinks.LinkExists(d_NodeID) then break; // проверяем линк на ноду родителя
          d_link:= Model.NodeLinks[d_NodeID];  // линк на ноду родителя
          if d_link.IsLinkNode then break; // если связка 2 - оставляем
          d_Node:= GetLinkPtr(d_link);     // нода родителя
          if not Assigned(d_Node) then break;
          if Assigned(d_Node.Children) then with d_Node.Children do
            for j:= 0 to Count-1 do if Assigned(Objects[j]) and // проверяем наличие линков на детей родителя
              Model.NodeLinks.LinkExists(TAutoTreeNode(Objects[j]).ID) then begin
              d_Node:= nil; // если на дитя есть линк - дальше по дереву не идем
              break;
            end;
        until not Assigned(d_Node); // akDel

        akEdit: d_link.Qty:= RoundTo(pQty, -3); // редактируем - меняем количество
      end; // case

    except
      on E: Exception do begin
        Result:= Result+fnIfStr(Result='', '', #10)+'error NodeID='+IntToStr(d_NodeID)+': '+E.Message;
        inc(ErrCount);
        case d_Act of
         akAdd: Dec(AddCount);  // добавляли
         akDel: Dec(DelCount);  // удаляли
        akEdit: Dec(EditCount); // редактировали
        end;
        d_Act:= akNot;
      end;
    end; // for i:= 0 to High(DataNodes)

    if (ErrCount<1) then
      if fDel and (DelCount>0) then ResCode:= resDeleted
      else if (AddCount>0)     then ResCode:= resAdded
      else if (EditCount>0)    then ResCode:= resEdited;
    if (DelCount>0) then
      Result:= Result+fnIfStr(Result='', '', #10)+'удалено '+IntToStr(DelCount)+' связок модели с узлами';
    if (AddCount>0) then
      Result:= Result+fnIfStr(Result='', '', #10)+'добавлено '+IntToStr(AddCount)+' связок модели с узлами';
    if (EditCount>0) then
      Result:= Result+fnIfStr(Result='', '', #10)+'изменено '+IntToStr(EditCount)+' связок модели с узлами';
    if (ErrCount>0) then
      Result:= Result+fnIfStr(Result='', '', #10)+'ошибки обработки '+IntToStr(ErrCount)+' связок модели с узлами';
  except
    on E: Exception do Result:= Result+fnIfStr(Result='', '', #10)+CutEMess(E.Message);
  end;
  SetLength(codes, 0);
  SetLength(DataNodes, 0);
end;
//=============================== Установить / снять связь товара с ориг.номером
function TDataCacheAdditionASON.CheckOrigNumLink(var ResCode: Integer; WareID, pMfID: Integer; var onID: Integer;
         var pONum: String; srcID: Integer=0; userID: Integer=0): String;
// srcID, userID нужны только при добавлении, если задан onID - pONum игнорируется (кроме resNotWrong) !!!
const nmProc = 'CheckOrigNumLink';
// ResCode на входе - вид операции (resAdded, resDeleted, resWrong, resNotWrong)
// !!! для resNotWrong нужно задать pMfID, onID, pONum !!!     ???
// ResCode на выходе - результат: resError - ошибка, resDoNothing - не менялось, 
// resAdded - связка добавлена, resDeleted - связка удалена, 
// resWrong - отмечена ошибочная связка в базе и удалена из кеша
// resNotWrong - снята отметка ошибочной связки в базе и связка добавлена в кеш
var ORD_IBD: TIBDatabase;
    ORD_IBS: TIBSQL;
    OpCode, LinkSrc: Integer;
    s, sONid, sWid: String;
    OrigNum: TOriginalNumInfo;
    Ware: TWareInfo;
    pONumLinks: TLinkList;
    mflst: TStringList;
    flDelON: boolean;
begin
  Result:= '';
  OpCode:= ResCode;
  ResCode:= resError;
  if not Assigned(self) then Exit;
  ORD_IBS:= nil;
  flDelON:= False;
  with Cache do try
//-------------------------------------------------------------------- проверяем
    if not (OpCode in [resAdded, resDeleted, resWrong, resNotWrong]) then
      raise EBOBError.Create(MessText(mtkNotValidParam)+' операции');

    if (WareID<1) or not WareExist(WareID) or GetWare(WareID).IsArchive then
      raise EBOBError.Create(MessText(mtkNotValidParam)+' товара');

    s:= MessText(mtkNotValidParam)+' ориг.номера';
    if (onID<1) then begin
      if (pMfID<1) or not Manufacturers.ManufExists(pMfID) then
        raise EBOBError.Create(MessText(mtkNotValidParam)+' произв.');

      pONum:= AnsiUpperCase(fnDelSpcAndSumb(fnDelEndOfStr(pONum)));
      if (pONum='') then raise EBOBError.Create(s);

      onID:= SearchOriginalNum(pMfID, pONum);

    end else if (OpCode<>resNotWrong) and not OrigNumExist(onID) then
      raise EBOBError.Create(s);

    Ware:= GetWare(WareID);
    if ware.IsArchive then
      raise EBOBError.Create(MessText(mtkNotValidParam)+' товара');
    pONumLinks:= Ware.ONumLinks;

    if (OpCode=resNotWrong) then begin
      if (onID<1) then // для resNotWrong нужно задать onID
        raise EBOBError.Create(MessText(mtkNotValidParam)+' ориг.номера');
//      if (pMfID<1) or not Manufacturers.ManufExists(pMfID) then
//        raise EBOBError.Create(MessText(mtkNotValidParam)+' произв.');
      if pONumLinks.LinkListItemExists(onID, lkLnkByID) then begin
        ResCode:= resDoNothing;
        raise EBOBError.Create(MessText(mtkWareOrNumLink)+' не отмечена, как ошибочная'); // ???
      end;

    end else if (OpCode=resAdded) then begin // проверяем добавление
      if (onID>0) and pONumLinks.LinkListItemExists(onID, lkLnkByID) then begin
        ResCode:= resDoNothing;
        raise EBOBError.Create(MessText(mtkWareOrNumLink)+' уже есть');
      end;
      if (userID<1) or (srcID<1) then raise EBOBError.Create(MessText(mtkNotParams));

    end else begin
      if (onID<1) or not pONumLinks.LinkListItemExists(onID, lkLnkByID) then begin // проверяем удаление / пометку ошибочной
        ResCode:= resDoNothing;
        raise EBOBError.Create(MessText(mtkWareOrNumLink)+' не найдена');
      end;
      LinkSrc:= GetLinkSrc(pONumLinks.GetLinkListItemByID(onID, lkLnkByID));
      case OpCode of
      resDeleted: // удалять можно связки только с источником (Excel или вручную)
        if not (LinkSrc in [soExcel, soHand]) then
          raise Exception.Create(MessText(mtkFuncNotAvailabl));
      resWrong:  // пометить Wrong можно только связки с источником (TD)
        if not (LinkSrc in [soTecDocBatch]) then
          raise Exception.Create(MessText(mtkFuncNotAvailabl));
      end; // case
    end;

//--------------------------------------------------- отрабатываем запись в базу
    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise EBOBError.Create(MessText(mtkErrConnectToDB));
    try
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite, True);
//      ORD_IBS.ParamCheck:= False;
      sONid:= IntToStr(onID);
      sWid:= IntToStr(WareID);
      if OpCode=resAdded then begin // добавляем
        ORD_IBS.SQL.Text:= 'select * from AddWareOrigNumLink('+sONid+', '+
          sWid+', '+IntToStr(pMfID)+', "'+pONum+'", '+IntToStr(userID)+', '+IntToStr(srcID)+')';
        ORD_IBS.ExecQuery;
        if (ORD_IBS.Bof and ORD_IBS.Eof) then raise EBOBError.Create(MessText(mtkErrAddRecord));
        if (onID<1) then onID:= ORD_IBS.Fields[0].AsInteger; // код ОН

      end else if OpCode=resDeleted then begin // удаляем из базы
        ORD_IBS.SQL.Text:= 'select * from DelWareOrigNumLink('+sONid+', '+sWid+')';
        ORD_IBS.ExecQuery;
        if (ORD_IBS.Bof and ORD_IBS.Eof) then raise EBOBError.Create(MessText(mtkErrDelRecord));
        flDelON:= (ORD_IBS.Fields[0].AsInteger>0); // признак удаления ОН

      end else if OpCode=resWrong then begin // устанавливаем пометку ошибочной
        ORD_IBS.SQL.Text:= 'execute procedure SetWrongWareOrigNumLink('+
          sONid+', '+sWid+', '+IntToStr(userID)+', '+IntToStr(srcID)+')';
        ORD_IBS.ExecQuery;

      end else begin // снимаем пометку ошибочной
        ORD_IBS.SQL.Text:= 'select * from SetNotWrongWareONumLink('+
          sONid+', '+sWid+', '+IntToStr(userID)+', '+IntToStr(srcID)+')';
        ORD_IBS.ExecQuery;
        if (ORD_IBS.Bof and ORD_IBS.Eof) then raise EBOBError.Create(MessText(mtkErrEditRecord));
        pMfID:= ORD_IBS.Fields[0].AsInteger;
        pONum:= ORD_IBS.Fields[1].AsString;
      end;
      ORD_IBS.Transaction.Commit;
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD);
    end;
//---------------------------------------------------------- отрабатываем кеш ОН
    OrigNum:= GetOriginalNum(onID);
    if OpCode in [resAdded, resNotWrong] then begin // добавляем
      if not Assigned(OrigNum) then begin // если новый ОН - создаем
        OrigNum:= TOriginalNumInfo.Create(onID, pMfID, pONum, CS_OrigNums);
        AddNewOrigNumToCache(OrigNum);
      end;
      pONumLinks.AddLinkListItem(TLink.Create(srcID, OrigNum), lkLnkByID, Ware.CS_wlinks); // доб.связку к товару
      with OrigNum.Links do begin                 // доб.связку к ориг.номеру
        CheckLink(WareID, srcID, Ware);
        SortByLinkName;
      end;

    end else begin // удаляем
      pONumLinks.DelLinkListItemByID(onID, lkLnkByID, Ware.CS_wlinks); // удал.связку у товара
      OrigNum.Links.DeleteLinkItem(WareID); // удал.связку у ориг.номера
      pMfID:= OrigNum.MfAutoID;
      if flDelON then try // удаляем ориг.номер
        CS_OrigNums.Enter;
        prFree(FarOriginalNumInfo[onID]);
      finally
        CS_OrigNums.Leave;
      end;
    end;
//----------------------------------------- отрабатываем список ОН производителя
    mflst:= Manufacturers[pMfID].ManufSearchONlist;
    with Manufacturers.CS_Mfaus do try
      Enter;
      if OpCode in [resAdded, resNotWrong] then  // добавляем
        mflst.AddObject(pONum, Pointer(onID))
      else if mflst.Find(pONum, LinkSrc)         // удаляем
        and (Integer(mflst.Objects[LinkSrc])=onID) then mflst.Delete(LinkSrc);
    finally
      Leave;
    end;

    Result:= MessText(mtkWareOrNumLink);
    case OpCode of
      resAdded:    Result:= Result+' добавлена';
      resNotWrong: Result:= Result+' восстановлена';
      resDeleted:  Result:= Result+' удалена';
      resWrong:    Result:= Result+' отмечена, как ошибочная';
    end;
    ResCode:= OpCode;
  except
    on E: EBOBError do Result:= E.Message;
    on E: Exception do Result:= nmProc+' (Ware='+IntToStr(WareID)+fnIfStr(pONum='', '', ' ONum='+pONum)+
      fnIfStr(onID<1, '', ' onID='+IntToStr(onID))+'): '+CutEMess(E.Message, ResCode);
  end;
end;
//========================================= Наполнение кеша оригинальных номеров
procedure TDataCacheAdditionASON.FillOriginalNums(fFill: Boolean=True);
const nmProc = 'FillOriginalNums'; // имя процедуры/функции
var OrdIBS: TIBSQL;
    OrdIBD: TIBDatabase;
    i, j, iCount, delCount, imfau: Integer;
    localStart: TDateTime;
    snum: String;
    OrigNum: TOriginalNumInfo;
begin
  if not Assigned(self) then Exit;
  OrdIBS:= nil;
  localStart:= Now;
  iCount:= 0;
  delCount:= 0;
  try
    OrdIBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORDIBD) then raise EBOBError.Create(MessText(mtkErrConnectToDB));
    try
      OrdIBS:= fnCreateNewIBSQL(OrdIBD, 'OrdIBS_'+nmProc, -1, tpRead, true);
      OrdIBS.SQL.Text:= 'select ORNWCODE, ORNWMFAUCODE, ORNWORIGNUMWARE'+
                       ' from ORIGINALNUMWARE ORDER BY ORNWMFAUCODE, ORNWORIGNUMWARE';
      OrdIBS.ExecQuery;
      if not fFill then for i:= 1 to High(FarOriginalNumInfo) do
        if Assigned(FarOriginalNumInfo[i]) then FarOriginalNumInfo[i].State:= False;

      while not OrdIBS.Eof do begin
        imfau:= OrdIBS.fieldByName('ORNWMFAUCODE').AsInteger;
        if not ManufAutoExist(imfau) then begin
          while not OrdIBS.Eof and (imfau=OrdIBS.fieldByName('ORNWMFAUCODE').AsInteger) do OrdIBS.Next;
          Continue;
        end;
        while not OrdIBS.Eof and (imfau=OrdIBS.fieldByName('ORNWMFAUCODE').AsInteger) do begin
          i:= OrdIBS.fieldByName('ORNWCODE').AsInteger;
          snum:= OrdIBS.fieldByName('ORNWORIGNUMWARE').AsString;
          if not OrigNumExist(i) then begin
            OrigNum:= TOriginalNumInfo.Create(i, imfau, snum, CS_OrigNums);
            AddNewOrigNumToCache(OrigNum, 1000);
          end else with FarOriginalNumInfo[i] do try
            CS_OrigNums.Enter;
            MfAutoID:= imfau;
            OriginalNum:= snum;
            State:= True;
          finally
            CS_OrigNums.Leave;
          end;
          cntsORD.TestSuspendException;
          OrdIBS.Next;
          inc(iCount);
        end; // while... imfau= ...
      end; // while not OrdIBS.Eof
    finally
      prFreeIBSQL(OrdIBS);
      cntsOrd.SetFreeCnt(OrdIBD, True);
    end;

    if not fFill then for i:= 1 to High(FarOriginalNumInfo) do
      if Assigned(FarOriginalNumInfo[i]) and not FarOriginalNumInfo[i].State then begin
        with FarOriginalNumInfo[i].Links do for imfau:= LinkCount-1 downto 0 do begin
          j:= GetLinkID(ListLinks[imfau]);
          with Cache do if WareExist(j) then with GetWare(j) do
            ONumLinks.DelLinkListItemByID(i, lkLnkByID, CS_wlinks);
        end;
        CS_OrigNums.Enter;
        try
          prFree(FarOriginalNumInfo[i]);
          inc(delCount);
        finally
          CS_OrigNums.Leave;
        end;
      end;
    imfau:= Length(FarOriginalNumInfo);
    for i:= High(FarOriginalNumInfo) downto 1 do if Assigned(FarOriginalNumInfo[i]) then begin
      imfau:= FarOriginalNumInfo[i].ID+1;
      break;
    end;
    if Length(FarOriginalNumInfo)>imfau then try
      CS_OrigNums.Enter;  // Critical start
      SetLength(FarOriginalNumInfo, imfau); // обрезаем по мах.коду
    finally
      CS_OrigNums.Leave;  // Critical end
    end;

  except
    on E: Exception do prMessageLOGS(nmProc+': '+E.Message, fLogCache);
  end;
  prMessageLOGS(nmProc+': '+IntToStr(iCount)+' он'+
    fnIfStr(delCount=0, '', ', del '+IntToStr(delCount))+' - '+
    GetLogTimeStr(LocalStart), fLogCache , false); // пишем в log
  TestCssStopException;
end;
//============================== Наполнение массива справочника источников связи
procedure TDataCacheAdditionASON.FillSourceLinks;
const nmProc = 'FillSourceLinks'; // имя процедуры/функции
var ibs: TIBSQL;
    ibd: TIBDatabase;
    p: Pointer;
    i, j: Integer;
    arANDTCODEs: Tai;
    arAnDtNames: Tas;
begin
  if not Assigned(self) then Exit;
//  ibd:= nil;
  ibs:= nil;
  SetLength(arANDTCODEs, 0);
  SetLength(arAnDtNames, 0);
  j:= 0;
  try try
    ibd:= cntsGRB.GetFreeCnt;
    if not Assigned(ibd) then raise EBOBError.Create(MessText(mtkErrConnectToDB));
    try
      ibs:= fnCreateNewIBSQL(ibd, 'ibsGRB_'+nmProc, -1, tpRead, true);
      ibs.SQL.Text:=  'select ANDTCODE, AnDtName, (AnDtSyncCode-'+
                      Cache.GetConstItem(pcCrossAnalogsDeltaSync).StrValue+') SyncCode'+
                      ' from (select AnTpCode XDict, a1.AnDtCode XMaster from AnalitType'+
                      '  left join AnalitDict a1 on a1.AnDtAnalitTypeCode=AnTpCode'+
                      '  where AnTpLinkDictType=154 and a1.AnDtMasterCode is null)'+
                      ' left join AnalitDict on AnDtAnalitTypeCode=XDict'+
                      '  and AnDtMasterCode=XMaster order by AnDtSyncCode';
      ibs.ExecQuery;
      while not ibs.Eof do begin
        j:= ibs.fieldByName('SyncCode').AsInteger;
        if (j>0) then begin
          if (High(arANDTCODEs)<j) then begin
            SetLength(arANDTCODEs, j+10);
            for i:= j to High(arANDTCODEs) do arANDTCODEs[j]:= 0;
            SetLength(arAnDtNames, j+10);
            for i:= j to High(arAnDtNames) do arAnDtNames[j]:= '';
          end;
          arANDTCODEs[j]:= ibs.fieldByName('ANDTCODE').AsInteger;
          arAnDtNames[j]:= ibs.fieldByName('AnDtName').AsString;
        end;
        cntsGRB.TestSuspendException;
        ibs.Next;
      end;
      if High(arANDTCODEs)>j then begin
        SetLength(arANDTCODEs, j+1);
        SetLength(arAnDtNames, j+1);
      end;
    finally
      prFreeIBSQL(ibs);
      cntsGRB.SetFreeCnt(ibd);
    end;

    ibd:= cntsOrd.GetFreeCnt;
    if not Assigned(ibd) then raise EBOBError.Create(MessText(mtkErrConnectToDB));
    try
      ibs:= fnCreateNewIBSQL(ibd, 'ibsOrd_'+nmProc, -1, tpRead, true);
      ibs.SQL.Text:= 'Select SRCLCODE, SRCLNAME FROM SOURCELINK';
      ibs.ExecQuery;
      while not ibs.Eof do begin
        i:= ibs.fieldByName('SRCLCODE').AsInteger;
        j:= 0;
        if (i<Length(arANDTCODEs)) and (arANDTCODEs[i]>0) then begin  // нашли соответствующий источник
          j:= arANDTCODEs[i];
          arANDTCODEs[i]:= 0;
          arAnDtNames[i]:= '';
        end;
        p:= TSubDirItem.Create(i, j, 0, ibs.fieldByName('SRCLNAME').AsString);
        FLinkSources.CheckItem(p);
        cntsORD.TestSuspendException;
        ibs.Next;
      end;

      j:= 0; // проверяем, не появились ли новые источники в Grossbee
      for i:= 0 to High(arANDTCODEs) do
        if (arANDTCODEs[i]>0) and (arAnDtNames[i]<>'') then begin
          Inc(j);
          break;
        end;
      if (j>0) then begin // если появились - добавляем в SOURCELINK
        fnSetTransParams(ibs.Transaction, tpWrite, True);
        ibs.SQL.Text:= 'insert into SOURCELINK'+
          '(SRCLCODE, SRCLNAME) values (:srcode, :srname)';
        for i:= 0 to High(arANDTCODEs) do
          if (arANDTCODEs[i]>0) and (arAnDtNames[i]<>'') then begin
            ibs.ParamByName('srcode').AsInteger:= arANDTCODEs[i];
            ibs.ParamByName('srname').AsString:= arAnDtNames[i];
            ibs.ExecQuery;
            ibs.Close;
          end;
        ibs.Transaction.Commit;
      end;
    finally
      prFreeIBSQL(ibs);
      cntsOrd.SetFreeCnt(ibd);
    end;
  except
    on E: Exception do prMessageLOGS(nmProc+': '+E.Message, fLogCache);
  end;
  finally
    SetLength(arANDTCODEs, 0);
    SetLength(arAnDtNames, 0);
  end;

  if flDebug then with FLinkSources do for i:= 0 to ItemsList.Count-1 do  // для отладки
    prMessageLOGS(nmProc+': id='+IntToStr(TSubDirItem(ItemsList[i]).ID)+
      ' sub='+IntToStr(TSubDirItem(ItemsList[i]).SubCode)+
      ' nm='+TSubDirItem(ItemsList[i]).Name, fLogCache);

  TestCssStopException;
end;
//================ заполнение / проверка связок товаров с оригинальными номерами
procedure TDataCacheAdditionASON.FillWareONLinks(fFill: Boolean=True; sLog: String='');
const nmProc='FillWareONLinks';
var OrdIBS: TIBSQL;
    OrdIBD: TIBDatabase;
    i, j, errCount, WareID, OrigNumID, pSrcID, jj, iLinks, mf, ii: Integer;
    LocalStart: TDateTime;
    OrigNum: TOriginalNumInfo;
    Ware: TWareInfo;
    link: TLink;
    body, lst, lstM: TStringList;
    s: String;
    flMfLst: Boolean;
    manuf: TManufacturer;
begin
  if not Assigned(self) or not Assigned(Cache) then Exit;
  LocalStart:= Now;
  ORDIBS:= nil;
  body:= nil;
  lstM:= nil;
  manuf:= nil;
  iLinks:= 0;
  lst:= TStringList.Create;
  OrdIBD:= cntsOrd.GetFreeCnt;
  try
    if not Assigned(ORDIBD) then raise EBOBError.Create(MessText(mtkErrConnectToDB));
    if not fFill then begin
      with Cache do for i:= 1 to High(arWareInfo) do if WareExist(i) then
        with GetWare(i) do if not IsArchive then ONumLinks.SetLinkStates(False, CS_wlinks);
      for i:= 1 to length(arOriginalNumInfo)-1 do
        if OrigNumExist(i) then arOriginalNumInfo[i].Links.SetLinkStates(False);
      Manufacturers.SetStates(False);
    end;
    try
      OrdIBS:= fnCreateNewIBSQL(OrdIBD, 'OrdIBS_'+nmProc, -1, tpRead, true);
      OrdIBS.SQL.Text:= 'select ORNWCODE, ORNWMFAUCODE, ORLKCODEWARE, ORLKSOURCECODE'+
        ' from ORIGINALNUMWARE inner join ORIGINALLINKWARE on ORLKONUMCODE=ORNWCODE'+
        ' inner join WareOptions on wowarecode=ORLKCODEWARE and WOARHIVED="F"'+
        ' where ORLKWRONG="F" ORDER BY ORNWMFAUCODE, ORNWORIGNUMWARE';
      OrdIBS.ExecQuery;             // select count (*) from ORIGINALLINKWARE where ORLKONUMCODE=1
      j:= 0; // счетчик            // update ORIGINALLINKWARE set ORLKWRONG="T" where ORLKONUMCODE=1
      errCount:= 0;
      jj:= 0;
      while not OrdIBS.EOF do begin
        mf:= OrdIBS.FieldByName('ORNWMFAUCODE').AsInteger;
        flMfLst:= {(Cache.AllowWeb or Cache.AllowWebArm) and} ManufAutoExist(mf); // признак заполнения списка ОН
        if flMfLst then begin
          lst.Clear; // несортированный список ОН производителя
          manuf:= Manufacturers[mf];
          lstM:= manuf.FManufSearchONlist;
          if fFill then lstM.Sorted:= False; // заполнение - отключаем сортировку
        end;
        //---------------------------------------- обрабатываем 1 производителя
        while not OrdIBS.EOF and (mf=OrdIBS.FieldByName('ORNWMFAUCODE').AsInteger) do begin

          OrigNumID:= OrdIBS.FieldByName('ORNWCODE').AsInteger;
          if not OrigNumExist(OrigNumID) then begin
            while not OrdIBS.EOF and (OrigNumID=OrdIBS.FieldByName('ORNWCODE').AsInteger) do begin
              inc(errCount);
              OrdIBS.Next;
            end;
            Continue;
          end;

          OrigNum:= GetOriginalNum(OrigNumID);
          i:= 0; // счетчик товаров по 1 ориг.номеру
          if not fFill then iLinks:= OrigNum.Links.LinkCount;
          //---------------------------------------- обрабатываем 1 ориг.номер
          while not OrdIBS.EOF and (mf=OrdIBS.FieldByName('ORNWMFAUCODE').AsInteger)
            and (OrigNumID=OrdIBS.FieldByName('ORNWCODE').AsInteger) do begin
            WareID:= OrdIBS.FieldByName('ORLKCODEWARE').AsInteger;

            if Cache.WareExist(WareID) then begin
              pSrcID:= OrdIBS.FieldByName('ORLKSOURCECODE').AsInteger;
              Ware:= Cache.GetWare(WareID);
              if not ware.IsArchive and (Ware.PgrID>0) then begin

                with Ware.ONumLinks do begin
                  if fFill or not LinkListItemExists(OrigNumID, lkLnkByID) then begin
                    link:= TLink.Create(pSrcID, OrigNum);
                    AddLinkListItem(link, lkLnkByID, Ware.CS_wlinks);
                  end else begin
                    link:= GetLinkListItemByID(OrigNumID, lkLnkByID);
                    if link.SrcID<>pSrcID then link.SrcID:= pSrcID;
                    link.State:= True;
                  end;
                end;

                if OrigNum.Links.LinkCount<cMaxLinksWithOE then begin  // если кол-во ссылок меньше критического
                  if fFill then
                    OrigNum.Links.AddLinkItem(TLink.Create(pSrcID, Ware))
                  else OrigNum.Links.CheckLink(WareID, pSrcID, Ware);
                end;
                inc(i);
                inc(j);
                if Ware.IsMOTOWare then inc(jj);
              end else inc(errCount); // if Ware.IsArchive ...
            end else inc(errCount); // if WareExist ...

            cntsORD.TestSuspendException;
            OrdIBS.Next;
          end; // while ... (OrigNumID=

          if (fFill or (iLinks<OrigNum.Links.LinkCount)) then  // если что-то добавилось
            OrigNum.Links.SortByLinkName;      // сортируем связки

          if i>cMaxLinksWithOE then try // Отправка сообщения о превышении критич.кол-ва ссылок на ориг.номер
            body:= TStringList.Create;
            body.Add('на ориг.номер '+OrigNum.OriginalNum+' (код '+IntToStr(OrigNum.ID)+')');
            body.Add(' в базе '+IntToStr(i)+' ссылок');
            body.Add(' в кеш записано '+IntToStr(cMaxLinksWithOE));
            s:= n_SysMailSend(fnGetSysAdresVlad(caeOnlyWorkDay), 'Уведомление об ошибке заполнения кеша', Body, nil,  '', '', True);
            if (s<>'') and (Pos(MessText(mtkErrMailToFile), s)>0) then // если письмо не записано в файл
              prMessageLOGS(nmProc+'(send mail to admin): '+s);
          finally
            prFree(body);
          end;

          if flMfLst and (i>0) then begin // добавляем в список, если есть товары
            if fFill then body:= lstM else body:= lst;
            if body.Capacity=body.Count then body.Capacity:= body.Capacity+1024;
            body.AddObject(OrigNum.OriginalNum, Pointer(OrigNum.ID));
          end;
        //---------------------------------------- обработали 1 ориг.номер
        end; // while not OrdIBS.EOF and (mf=

        if flMfLst then try
          Manufacturers.CS_Mfaus.Enter;
          if fFill then begin
            lstM.Sort;
            lstM.Sorted:= True;  // заполнение - включаем сортировку
          end else begin // проверка - сравниваем списки ОН
            lst.Sort;             // из lstM удаляем те, что ушли
            lst.Sorted:= True;    // в lst оставляем те, что надо добавить
            for i:= lstM.Count-1 downto 0 do
              if lst.Find(lstM[i], ii) then lst.Delete(ii) else lstM.Delete(i);
            if lst.Count>0 then  // добавление
              for i:= 0 to lst.Count-1 do lstM.AddObject(lst[i], lst.Objects[i]);
          end;
          lstM.Capacity:= lstM.Count;
          manuf.State:= True;
        finally
          Manufacturers.CS_Mfaus.Leave;
        end;
      //---------------------------------------- обработали 1 производителя
      end; //  while not OrdIBS.EOF
    finally
      prFreeIBSQL(OrdIBS);
      cntsOrd.SetFreeCnt(OrdIBD, True);
    end;
    sLog:= sLog+IntToStr(j)+' связ.'+
      fnIfStr(errCount=0, '', '/отказ '+IntToStr(errCount))+'/m-'+IntToStr(jj);

    if not fFill then begin  // удаляем непроверенные линки
      with Cache do for i:= 1 to High(arWareInfo) do if WareExist(i) then
         with GetWare(i) do if not IsArchive then ONumLinks.DelNotTestedLinks(CS_wlinks);
      for i:= 1 to High(arOriginalNumInfo) do if OrigNumExist(i) then
        arOriginalNumInfo[i].Links.DelNotTestedLinks;

      lst.Clear; // чистим непроверенные списки ОН
      lst:= Manufacturers.GetNotTestedList;
      for i:= 0 to lst.Count-1 do begin
        mf:= Integer(lst.Objects[i]);
        if not ManufAutoExist(mf) then Continue;
        manuf:= Manufacturers[mf];
        if manuf.State then Continue;
        with manuf.ManufSearchONlist do if (Count>0) then try
          Manufacturers.CS_Mfaus.Enter;
          Clear;
        finally
          Manufacturers.CS_Mfaus.Leave;
        end;
      end;
    end; // if not fFill
  except
    on E: Exception do prMessageLOGS(nmProc+': '+E.Message, fLogCache);
  end;
  prFree(lst);
  prMessageLOGS(nmProc+': '+sLog+' - '+
    GetLogTimeStr(LocalStart), fLogCache, false); // пишем в log
  TestCssStopException;
end;
//================================= Получает обьект оригинального номера по коду
function TDataCacheAdditionASON.GetOriginalNum(ID: Integer): TOriginalNumInfo;
begin
  Result:= nil;
  if not Assigned(self) then Exit;
  if OrigNumExist(ID) then Result:= FarOriginalNumInfo[ID];
end;
//============================== Проверка корректности номеров элементов массива
function TDataCacheAdditionASON.ManufAutoExist(ID: Integer): Boolean;
begin
  Result:= False;
  if not Assigned(self) then Exit;
  Result:= Manufacturers.ManufExists(ID);
end;
//==============================================================================
function TDataCacheAdditionASON.OrigNumExist(ID: Integer): Boolean;
begin
  Result:= Assigned(self) and (ID>0) and
    (length(FarOriginalNumInfo)>ID) and Assigned(FarOriginalNumInfo[ID]);
end;
//==============================================================================
function TDataCacheAdditionASON.SourceLinkExist(ID: Integer): Boolean;
begin
  Result:= Assigned(self) and LinkSources.ItemExists(ID);
end;
//=================================================== Поиск оригинального номера
function TDataCacheAdditionASON.SearchOriginalNum(Manuf: Integer; OrigNum: String): Integer;
// Result - код (индекс), если в кэше нет, -1
var i: Integer;
begin
  Result:= -1;
  if not Assigned(self) or (Trim(OrigNum)='') or not ManufAutoExist(Manuf) then Exit;
  OrigNum:= AnsiUpperCase(OrigNum);
  with Manufacturers[Manuf].ManufSearchONlist do
    if (Count>0) and Find(OrigNum, i) then Result:= Integer(Objects[i]);
{  for Result:= 1 to Length(FarOriginalNumInfo)-1 do
    if OrigNumExist(Result) then with FarOriginalNumInfo[Result] do
      if (OriginalNum=OrigNum) and (MfAutoID=Manuf) then Exit;
  Result:= -1; }
end;
//====================================== Поиск оригинального номера по вхождению
function TDataCacheAdditionASON.SearchWareOrigNums(Template: String; // must Free
         IgnoreSpec: Integer; sortManuf: Boolean; var TypeCodes: Tai): Tai;
// возвращает массив кодов ориг.номеров, отсортированных по наименованию, имеющих товары
// если TypeCodes пустой - в TypeCodes собираем типы
// если TypeCodes не пустой - отсев рез-тов поиска по типам
var i, j, pType: Integer;
    s: String;
    flTypeSelection, flSelecting, flContaining: boolean;
    arTypes: Tai;
begin
  SetLength(Result, 0);
  if not Assigned(TypeCodes) then SetLength(TypeCodes, 0);
  if not Assigned(self) then Exit;
  Template:= AnsiUpperCase(fnDelSpcAndSumb(fnDelEndOfStr(Template)));
  if Template='' then Exit;
  flTypeSelection:= (Length(TypeCodes)>0); // признак - отсеивать по TypeCodes или собирать типы в TypeCodes
  with fnCreateStringList(False, dupAccept, 100) do try
    for i:= 1 to length(FarOriginalNumInfo)-1 do if OrigNumExist(i) then
      with FarOriginalNumInfo[i] do if Assigned(Links) and
        (Links.LinkCount>0) and (pos(Template, OriginalNum)>0) then begin
        flSelecting:= not flTypeSelection; // True для первичного поиска, False для отсева
        try
          arTypes:= GetAnalogTypes; // массив кодов типов аналогов (с нулевым)
          for j:= 0 to High(arTypes) do begin // перебираем типы аналогов
            pType:= arTypes[j];
            flContaining:= (fnInIntArray(pType, TypeCodes)>-1);
            if flTypeSelection and flContaining then begin
              flSelecting:= True;                    // отсев и тип есть в массиве типов
              break;
            end else if not flTypeSelection and not flContaining then
              prAddItemToIntArray(pType, TypeCodes); // первичный поиск - собираем типы
          end;
        finally
          SetLength(arTypes, 0);
        end;
        if not flSelecting then Continue; // если не подходит по типам при отсеве

        if sortManuf then s:= SortString else s:= OriginalNum;
        AddObject(s, Pointer(i));
      end; // with FarOriginalNumInfo[i]
    if Count>1 then Sort;
    if Count>0 then begin
      SetLength(Result, Count);
      for i:= 0 to Count-1 do Result[i]:= Integer(Objects[i]);
    end;
  finally Free; end;
end;
//==============================================================================
// Получить список аналогов от связки оригинальных номеров с учетом производителя авто
function TDataCacheAdditionASON.fnGetListAnalogsWithManufacturer(
         pWareID, pManufID: Integer; var pAr1, pAr2: Tai): Integer;
const nmProc = 'fnGetListAnalogsWithManufacturer';
//{ pWareID - Код товара, pManufID - Код производителя авто, ( -1 - не проверять)
//  pAr1 - Коды товаров аналогов по ориг.номеру и ManufID
//  pAr2 - Коды товаров аналогов по ориг.номеру
//  Result = 0 - результат пусто
//          -1 - ошибка pWareID - не найден в кеше
//          -2 - ошибка pManufID(<> -1) - не найден в кеше
//          -3 - Ошибка выполнения
//          >0 - сумма количесва элементов pAr1.Count+pAr2.Count
var i, j, idxAn, idxOE: Integer;
    lst1, lst2: TStringList;
//    TimeSection: TDateTime;
    arON, arW: Tai;
    flManuf: Boolean;
begin
//  TimeSection:= now;
  Result:= 0;
  SetLength(pAr1, 0);
  SetLength(pAr2, 0);
  if not Assigned(self) then Exit;
//  if not Cache.WareExist(pWareID) then Result:= -1
//  else if (pManufID>-1) and not (ManufAutoExist(pManufID)) then Result:= -2;
//  if Result<0 then Exit;
  lst1:= nil;
  lst2:= nil;
  try       // Список оригинальных номеров, на которые ссылается товар
    if not Cache.WareExist(pWareID) then
      raise Exception.Create(MessText(mtkNotFoundWare, IntToStr(pWareID)))
    else if (pManufID>0) and not (ManufAutoExist(pManufID)) then
      raise Exception.Create('Не найден произв., код='+IntToStr(pManufID));
    SetLength(arON, 0);

    lst1:= fnCreateStringList(True, dupIgnore, 50);
    lst2:= fnCreateStringList(True, dupIgnore, 50);

    with Cache.GetWare(pWareID) do if IsArchive then
      raise Exception.Create(MessText(mtkNotFoundWare, IntToStr(pWareID)))
    else arON:= ONumLinks.GetLinkListCodes(lkLnkByID); // коды ориг.номеров

    for i:= Low(arON) to High(arON) do begin
      idxOE:= arON[i];
      if not OrigNumExist(idxOE) then Continue;
      flManuf:= (pManufID>0) and (FarOriginalNumInfo[idxOE].MfAutoID=pManufID);

      SetLength(arw, 0); // коды товаров которые ссылаются на оригинальный номер
      arW:= FarOriginalNumInfo[idxOE].Links.GetLinkCodes;
      for j:= Low(arW) to High(arW) do begin
        idxAn:= arW[j];
        if not Cache.WareExist(idxAn) or (idxAn=pWareID) then Continue;
        with Cache.GetWare(IdxAn) do if IsArchive then Continue
        else if flManuf then lst1.AddObject(Name, Pointer(IdxAn))
        else lst2.AddObject(Name, Pointer(IdxAn));
      end;
    end;

//    if lst1.Count>1 then lst1.Sort;
//    if lst2.Count>1 then lst2.Sort;

    SetLength(pAr1, lst1.Count);
    for i:= 0 to lst1.Count-1 do begin
      pAr1[i]:= Integer(lst1.Objects[i]);
      j:= lst2.IndexOfObject(lst1.Objects[i]);
      if j>-1 then lst2.Delete(j);
    end;
    SetLength(pAr2, lst2.Count);
    for i:= 0 to lst2.Count-1 do pAr2[i]:= Integer(lst2.Objects[i]);
    Result:= Length(pAr1)+Length(pAr2);
  except
    on E: Exception do begin
      Result:= 0;
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
    end;
  end;
  prFree(lst1);
  prFree(lst2);
  SetLength(arON, 0);
  SetLength(arW, 0);
//  prMessageLOGS(nmProc+' : '+GetLogTimeStr(TimeSection), fLogCache, false);
end;
//====================================================== заполнение дерева узлов
procedure TDataCacheAdditionASON.FillTreeNodesAuto;
const nmProc = 'FillTreeNodesAuto';
var TimeProc: TDateTime;
    ORD_IBD: TIBDatabase;
    ORD_IBS: TIBSQL;
    i, j, k, MeasCode, Code, CodeParent, sysID, pMainCode, pCodeTD: Integer;
    pName, pNameSys, s: String;
    TempList: TList;
    pIsGATD, pVisible: Boolean;
    pNode: TAutoTreeNode;
begin
  if not Assigned(self) then Exit;
  TimeProc:= Now;
  ORD_IBS:= nil;
  TempList:= TList.Create;
  j:= 0; // общий счетчик
  ORD_IBD:= cntsORD.GetFreeCnt;
  try
    if not Assigned(ORD_IBD) then raise EBOBError.Create(MessText(mtkErrConnectToDB));
    ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpRead, True);
    ORD_IBS.SQL.Text:= 'select * from TREENODESAUTO'+
      ' order by TRNADTSYCODE, TRNACODEPARENT, TRNACODE';
    ORD_IBS.ExecQuery;

    while not ORD_IBS.Eof do begin
      sysID:= ORD_IBS.FieldByName('TRNADTSYCODE').asInteger;
//--------------------------------------------------------- 1 дерево = 1 система
      if TempList.Count>0 then TempList.Clear; // список сбойных узлов
      while not ORD_IBS.Eof and (sysID=ORD_IBS.FieldByName('TRNADTSYCODE').asInteger) do begin
        Code      := ORD_IBS.FieldByName('TRNACODE').asInteger;
        CodeParent:= ORD_IBS.FieldByName('TRNACODEPARENT').asInteger;
        pName     := ORD_IBS.FieldByName('TRNANAME').asString;
        pNameSys  := ORD_IBS.FieldByName('TRNANAMESYS').asString; // пробуем посадить узел в дерево с ходу
        MeasCode  := ORD_IBS.FieldByName('TRNAMEASCODE').asInteger;
        pMainCode := ORD_IBS.FieldByName('TRNAMainCode').asInteger;
        pCodeTD   := ORD_IBS.FieldByName('TRNATDCODE').asInteger;
        pVisible  := GetBoolGB(ORD_IBS, 'TRNAVISIBLE');
        pIsGATD   := GetBoolGB(ORD_IBS, 'TRNATDGA');

//        TreeNodes.CheckItem(pNode);
        s:= AutoTreeNodesSys[sysID].NodeAdd(Code, CodeParent, MeasCode,
            pName, pNameSys, pMainCode, pCodeTD, pIsGATD, pVisible, False, False);
        if s<>'' then begin // если не получилось - запоминаем в список
          pNode:= TAutoTreeNode.Create(Code, CodeParent, MeasCode, sysID,
                  pName, pNameSys, pMainCode, pCodeTD, pIsGATD, pVisible);
          TempList.Add(pNode)
        end else inc(j); // считаем посаженные узлы

        cntsORD.TestSuspendException;
        ORD_IBS.Next;
      end; // while ...DTSYCode=...

      k:= 0; // счетчик проходов
      if TempList.Count>0 then repeat // ходим по списку сбойных узлов
        for i:= TempList.Count-1 downto 0 do

          if not Assigned(TempList[i]) then TempList.Delete(i)
          else begin
            pNode:= TempList[i];
  //            TreeNodes.CheckItem(pNode);
            Code:= pNode.ID;  // нужно для var параметра
            with pNode do     // опять пробуем посадить узел в дерево
              s:= AutoTreeNodesSys[sysID].NodeAdd(Code, ParentID, MeasID, Name,
                NameSys, MainCode, SubCode, IsGATD, Visible, False, False);
            if s='' then begin // если получилось
              inc(j);          // считаем посаженные узлы
              prFree(pNode);      // чистим элемент списка
              TempList.Delete(i);
            end;
          end; // for

        inc(k);
      until (TempList.Count<1) or (k>RepeatCount); // пока все не посадим, но не более RepeatCount проходов

      if (TempList.Count>0) then begin // если не все село - пишем в лог
        prMessageLOGS(nmProc+': ошибка записи в кеш '+IntToStr(TempList.Count)+' узлов:', fLogCache, false);
        for i:= TempList.Count-1 downto 0 do begin
          pNode:= TAutoTreeNode(TempList[i]);
          prMessageLOGS(nmProc+':    код узла -'+IntToStr(pNode.ID)+', '+pNode.Name, fLogCache, false);
          prFree(pNode);
        end;
      end;

      with FAutoTreeNodesSys[sysID] do FillNodesList; // заполняем список отсортированных узлов системы
//--------------------------------------------------------- 1 дерево = 1 система
    end; //  while not ORD_IBS.Eof
  except
    on E: Exception do prMessageLOGS(nmProc+': '+E.Message, fLogCache);
  end;
  prFreeIBSQL(ORD_IBS);
  cntsOrd.SetFreeCnt(ORD_IBD);
  prFree(TempList);
  prMessageLOGS(nmProc+': '+IntToStr(j)+' узлов - '+GetLogTimeStr(TimeProc), fLogCache, false);
  TestCssStopException;
end;
//=========================================== Добавление элемента в дерево узлов
function TDataCacheAdditionASON.TreeNodeAdd(pTypeSys, pParentID, pMainCode: Integer;
         pNodeName, pNodeNameSys: String; pUserID: Integer; var pNodeID: Integer;
         pVisible: Boolean=True; pMeasID: Integer=0; pCodeTD: Integer=0; pIsGATD: Boolean=False): String;
const nmProc = 'TreeNodeAdd';
var NodeAuto, NodeParent: TAutoTreeNode;
    ORD_IBD: TIBDatabase;
    ORD_IBS: TIBSQL;
    errmess: String;
    kind: Integer;
begin
  Result:= '';
//  ORD_IBD:= nil;
  ORD_IBS:= nil;
  kind:= -1;
  try
    if not Assigned(self) then raise Exception.Create(MessText(mtkErrAddRecord));
    if not CheckTypeSys(pTypeSys) then
      raise Exception.Create(MessText(mtkNotFoundTypeSys, IntToStr(pTypeSys)));

    pNodeNameSys:= AnsiUpperCase(pNodeNameSys);
    if pNodeID<1 then pNodeID:= -1;
    errmess:= FAutoTreeNodesSys[pTypeSys].NodeValidCheckForAdd(
           pNodeID, pParentID, pNodeName, pNodeNameSys, NodeAuto, NodeParent);
    if errmess<>'' then raise Exception.Create(errmess);

    ORD_IBD:= cntsORD.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise Exception.Create(MessText(mtkErrConnectToDB));
    try
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite, True); // добавление в базу
{      ORD_IBS.SQL.Text:= 'insert into TREENODESAUTO ('+
        fnIfStr(pNodeID<1, '', 'TRNACODE, ')+
        fnIfStr(pMeasID<1, '', 'TRNAMEASCODE, ')+
        fnIfStr(pParentID<1, '', 'TRNACODEPARENT, ')+
        fnIfStr(pMainCode<1, '', 'TRNAMAINCODE, ')+
        fnIfStr(pCodeTD<1, '', 'TRNATDCODE, ')+
        fnIfStr(pIsGATD, 'TRNATDGA, ', '')+fnIfStr(not pVisible, 'TRNAVISIBLE, ', '')+
        'TRNADTSYCODE, TRNANAME, TRNANAMESYS, TRNAUSERID) values ('+
        fnIfStr(pNodeID<1, '', IntToStr(pNodeID)+', ')+
        fnIfStr(pMeasID<1, '', IntToStr(pMeasID)+', ')+
        fnIfStr(pParentID<1, '', IntToStr(pParentID)+', ')+
        fnIfStr(pMainCode<1, '', IntToStr(pMainCode)+', ')+
        fnIfStr(pCodeTD<1, '', IntToStr(pCodeTD)+', ')+
        fnIfStr(pIsGATD, '"T", ', '')+fnIfStr(not pVisible, '"F", ', '')+
        IntToStr(pTypeSys)+', :TRNANAME, :TRNANAMESYS, '+IntToStr(pUserID)+
        ') returning TRNACODE';  }
      ORD_IBS.SQL.Text:= 'select * from CheckTreeNode('+IntToStr(pTypeSys)+', '+
        IntToStr(pUserID)+', '+IntToStr(pCodeTD)+', '+IntToStr(pMainCode)+', '+
        IntToStr(pMeasID)+', '+IntToStr(pParentID)+', '+fnIfStr(pIsGATD, '"T", ', '"F", ')+
        fnIfStr(pVisible, '"T", ', '"F", ')+':TRNANAME, :TRNANAMESYS)';
      ORD_IBS.ParamByName('TRNANAME').AsString:= pNodeName;
      ORD_IBS.ParamByName('TRNANAMESYS').AsString:= pNodeNameSys;
      ORD_IBS.ExecQuery;
      if not (ORD_IBS.Bof and ORD_IBS.Eof) then begin
        kind:= ORD_IBS.FieldByName('rKind').asInteger; // kind= 0 - Ничего не менялось, 1 - Было добавление
        pNodeID:= ORD_IBS.FieldByName('rNode').asInteger;
        pCodeTD:= ORD_IBS.FieldByName('rNodeTD').asInteger;
        pMainCode:= ORD_IBS.FieldByName('rNodeMain').asInteger;
        pMeasID:= ORD_IBS.FieldByName('rmeasID').asInteger;
        pParentID:= ORD_IBS.FieldByName('rParent').asInteger;
        pIsGATD:= ORD_IBS.FieldByName('rIsGa').AsString='T';
        pVisible:= ORD_IBS.FieldByName('rIsVis').AsString='T';
        pNodeName:= ORD_IBS.FieldByName('rNodeName').AsString;
        pNodeNameSys:= ORD_IBS.FieldByName('rSysName').AsString;
      end;
      ORD_IBS.Transaction.Commit;
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD);
    end;
    if pNodeID<1 then raise Exception.Create('pNodeID<1');
    if pMainCode<1 then pMainCode:= pNodeID;             // добавление в дерево

    if (kind=0) and FAutoTreeNodesSys[pTypeSys].NodeExists(pNodeID) then
      with FAutoTreeNodesSys[pTypeSys][pNodeID] do begin
        if FMainCode<>pMainCode then NodeAuto.FMainCode:= pMainCode;
        if FParCode<>pParentID then FParCode:= pParentID;
        if FSubCode<>pCodeTD then FSubCode:= pCodeTD;
        if FMeasID<>pMeasID then FMeasID:= pMeasID;
        if IsGATD<>pIsGATD then IsGATD:= pIsGATD;
        if IsVisible<>pVisible then IsVisible:= pVisible;
        if Name<>pNodeName then Name:= pNodeName;
        if FNameSys<>pNodeNameSys then FNameSys:= pNodeNameSys;
      end
    else
      Result:= FAutoTreeNodesSys[pTypeSys].NodeAdd(pNodeID, pParentID, pMeasID,
        pNodeName, pNodeNameSys, pMainCode, pCodeTD, pIsGATD, pVisible, True, True);
  except
    on E: Exception do begin
      Result:= E.Message;
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
    end;
  end;
end;
//================== must Free, список значений фильтров узла модели / двигателя
function TDataCacheAdditionASON.GetModelOrEngNodeFiltersList(NodeID, pID: integer; IsEngine: Boolean=False): TStringList;
const nmProc='GetModelOrEngNodeFiltersList';
var ORD_IBD: TIBDatabase;
    ORD_IBS: TIBSQL;
    iVal: Integer;
    sVal: string;
begin
  Result:= TStringList.Create;
  if not Assigned(self) then Exit;
  ORD_IBS:= nil;
  ORD_IBD:= cntsOrd.GetFreeCnt;
  try
    if not Assigned(ORD_IBD) then raise Exception.Create(MessText(mtkErrConnectToDB));
    try
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpRead, true);
      if IsEngine then sVal:= 'GetEngNodeFilters' else sVal:= 'GetModelNodeFilters';
      ORD_IBS.SQL.Text:= 'select * from '+sVal+'('+IntToStr(NodeID)+', '+IntToStr(pID)+')'; //
      ORD_IBS.ExecQuery;
      while not ORD_IBS.Eof do begin
        iVal:= ORD_IBS.fieldByName('Rwcws').asInteger;
        sVal:= ORD_IBS.fieldByName('Rvalue').AsString;
        Result.AddObject(sVal, Pointer(iVal));
        cntsORD.TestSuspendException;
        ORD_IBS.Next;
      end;
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD);
    end;
  except
    on E: Exception do begin
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
      Result.Clear;
    end;
  end;       // если фильтры модели не найдены - отрабатываем кеш 
  if not IsEngine and (Result.Count<1) and Models.ModelExists(pID) then
    with Models[pID] do if NodeLinks.LinkExists(NodeID) then
      TSecondLink(NodeLinks[NodeID]).NodeHasFilters:= False;
end;

//******************************************************************************
//                          TModelLine - Модельный ряд
//******************************************************************************
//========= сортировка TStringList моделей - видимость + порядк.№ + наименование
function ModelsSortCompare(List: TStringList; Index1, Index2: Integer): Integer;
var i1, i2: integer;
    Model1, Model2: TModelAuto;
begin
  with Cache.FDCA do try
    Model1:= Models[Integer(List.Objects[Index1])];
    Model2:= Models[Integer(List.Objects[Index2])];
    if (Model1.IsVisible=Model2.IsVisible) then begin
      i1:= Model1.ModelOrderNum;
      i2:= Model2.ModelOrderNum;
      if i1=i2 then Result:= AnsiCompareText(Model1.SortName, Model2.SortName)
      else if i1<i2 then Result:= -1 else Result:= 1;
    end else if Model1.IsVisible then Result:= -1 else Result:= 1;
  except
    Result:= 0;
  end;
end;
//==============================================================================
constructor TModelLine.Create(pMLineID, pTDcode, pMFAID: Integer; pName: String);
begin
  Inherited Create(pMLineID, pTDcode, 0, pName);
  FParCode:= pMFAID;
//  FMLHasWares:= False;
  FMLModelsSort:= fnCreateStringList(False, LCharUpdate);  // список моделей модельного ряда
  FMLModelsTopUp:= fnCreateStringList(False, LCharUpdate); // список моделей модельного ряда с топами вверху
end;
//==============================================================================
destructor TModelLine.Destroy;
begin
  if not Assigned(self) then Exit;
  prFree(FMLModelsSort);
  prFree(FMLModelsTopUp);
  inherited Destroy;
end;
//================================================================= получить код
function TModelLine.GetIntML(const ik: T8InfoKinds): Integer;
begin
  Result:= 0;
  if not Assigned(self) then Exit else case ik of
    ik8_1: begin                    // количество моделей модельного ряда
           CheckModelsLists;
           Result:= FMLModelsSort.Count;
         end;
    ik8_2: Result:= FParCode;       // Код производителя авто
    ik8_3: Result:= FYStart;        // Год начала производства
    ik8_4: Result:= FYEnd;          // Год окончания производства
    ik8_5: Result:= FMStart;        // Месяц начала производства
    ik8_6: Result:= FMEnd;          // Месяц окончания производства
    ik8_7: Result:= FTypeSys;       // Тип системы 1 - Авто, 2 - Мото and etc.
  end;
end;
//================================================================= записать код
procedure TModelLine.SetIntML(const ik: T8InfoKinds; Value: Integer);
begin
  if not Assigned(self) then Exit else case ik of
    ik8_2: if FParCode<>Value  then FParCode:= Value;  // код производителя авто
  end;
end;
//============================== признак наличия видимых моделей модельного ряда
function TModelLine.GetHasVisModels: boolean;
var i, j: integer;
begin
  Result:= False;
  if not Assigned(self) then Exit;
   CheckModelsLists;
   with FMLModelsSort do for i:= 0 to Count-1 do begin
     j:= Integer(Objects[i]);
     if Cache.FDCA.Models[j].IsVisible then begin
       Result:= True;
       break;
     end;
   end;
end;
//============================================================== получить строку
function TModelLine.GetStrML(const ik: T8InfoKinds): String;
begin
  Result:= '';
  if not Assigned(self) then Exit else case ik of // Наименование модельного ряда для сортировки
    ik8_1: Result:= GetNameForSort(Name, YStart, MStart);
  end;
end;
//===================================== заполнить списки моделей модельного ряда
procedure TModelLine.CheckModelsLists;
var i, j, ii: Integer;
    ar: Tai;
    fLast: boolean;
    cs: TCriticalSection;
begin
  SetLength(ar, 0);
  if not Assigned(self) then Exit;
  cs:= Cache.FDCA.ModelLines.CS_MLines;

  with FMLModelsSort do if Delimiter=LCharUpdate then try
    cs.Enter;
    Clear;
    ar:= Cache.FDCA.Models.GetMLModelsList(FID);
    Capacity:= Capacity+Length(ar);
    for i:= Low(ar) to High(ar) do
      AddObject(Cache.FDCA.Models[ar[i]].SortName, Pointer(ar[i]));
    CustomSort(ModelsSortCompare);
    Delimiter:= LCharGood;
    FMLModelsTopUp.Delimiter:= LCharUpdate; // флажок некорректности списка с топами
  finally
    cs.Leave;
    SetLength(ar, 0);
  end;

  with FMLModelsTopUp do if Delimiter=LCharUpdate then try
    cs.Enter;
    Clear;
    for i:= 0 to FMLModelsSort.Count-1 do
      AddObject(FMLModelsSort.Strings[i], FMLModelsSort.Objects[i]);
    if Count>0 then begin  // переставляем топы
      j:= Integer(Objects[0]); // запоминаем 1-й элемент
      i:= Count-1;
      fLast:= False;
      while not fLast do begin         // перебираем с конца
        ii:= Integer(Objects[i]);
        fLast:= (ii=j); // true - последняя проверка
        if Cache.FDCA.Models[ii].IsTop then
          Move(i, 0)               // топ переносим в начало
        else Dec(i);
      end;
    end;
    Delimiter:= LCharGood; // флажок корректности списка с топами
  finally
    cs.Leave;
  end;
end;
//====================================== Получить список моделей модельного ряда
function TModelLine.GetListModels(pTopsUp: Boolean=False): TStringList;
begin
  Result:= nil;
  if not Assigned(self) then Exit else CheckModelsLists; // заполнить списки
  if pTopsUp then Result:= FMLModelsTopUp else Result:= FMLModelsSort;
end;
//=========================== получить код модели модельного ряда по коду TecDoc
function TModelLine.GetMLModelIDByTDcode(pModTDnr: Integer): Integer;
var i: integer;
begin
  Result:= -1;
  if not Assigned(self) or (TypeSys>1) then Exit; // по коду TecDoc ищем только авто !!!
  with GetListModels do for i:= 0 to Count-1 do begin
    Result:= Integer(Objects[i]);
    with Cache.FDCA do
      if Models.ModelExists(Result) and (Models[Result].SubCode=pModTDnr) then Exit;
  end;
  Result:= -1;
end;
//==============================Удалить модель из списка моделей модельного ряда
procedure TModelLine.ModelDelFromLine(pModelID: Integer);
const nmProc = 'ModelDelFromLine';
var i: Integer;
    p: Pointer;
  //---------------------------
  procedure DelModelItem(var lst: TStringList);
  begin
    i:= lst.IndexOfObject(p);
    if i>-1 then lst.Delete(i);
  end;
  //---------------------------
begin
  if not Assigned(self) then Exit;
  if FMLModelsSort.Delimiter=LCharUpdate then Exit;
  try
    p:= Pointer(pModelID);
    with Cache.FDCA.ModelLines.CS_MLines do try
      Enter;
      DelModelItem(FMLModelsSort);
      DelModelItem(FMLModelsTopUp);
    finally
      Leave;
    end;
  except
    on E: Exception do prMessageLOGS(nmProc+': '+E.Message, fLogCache);
  end;
end;

//******************************************************************************
//                  TModelLines - Список модельных рядов
//******************************************************************************
//==============================================================================
constructor TModelLines.Create;
begin
  SetLength(FarModelLines, 1);
  FarModelLines[0]:= TModelLine.Create(0, 0, 0, '');
  CS_MLines:= TCriticalSection.Create;
  Inherited Create;
end;
//==============================================================================
destructor TModelLines.Destroy;
var i: Integer;
begin
  if not Assigned(self) then Exit;
  for i:= Low(FarModelLines) to High(FarModelLines) do
    if Assigned(FarModelLines[i]) then try prFree(FarModelLines[i]); except end;
  SetLength(FarModelLines, 0);
  prFree(CS_MLines);
  inherited Destroy;
end;
//======================================= Проверка существования модельного ряда 
function TModelLines.ModelLineExists(pID: Integer): Boolean;
begin
  Result:= False;
  if not Assigned(self) then Exit;
  Result:= (pID>0) and (length(FarModelLines)>pID) and Assigned(FarModelLines[pID]);
end;
//===================================== Получить ссылку на модельный ряд по коду
function TModelLines.GetModelLine(pModelLineID: Integer): TModelLine;
begin
  Result:= nil;
  if not Assigned(self) then Exit;
  if ModelLineExists(pModelLineID) then Result:= FarModelLines[pModelLineID]
  else Result:= FarModelLines[0];
end;
//====== Возвращает список модельных рядов по указанному производителю и системе
function TModelLines.GetManufSysModelLinesList(pManufID, pTypeSys: Integer): TStringList; // must Free Result
var i: Integer;
begin
  Result:= TStringList.Create;
  if not Assigned(self) then Exit;
  for i:= 1 to High(FarModelLines) do
    if Assigned(FarModelLines[i]) then with FarModelLines[i] do
      if (MFAID=pManufID) and (FTypeSys=pTypeSys) then
        Result.AddObject(SortName, Pointer(i));
end;
//=================================== устанавливает флаг проверки всем элементам
procedure TModelLines.SetStates(pState: Boolean; pTypeSys: Integer=0);
var i: Integer;
begin
  if not Assigned(self) or (length(FarModelLines)<2) then Exit;
  CS_MLines.Enter;
  try
    for i:= 1 to High(FarModelLines) do if Assigned(FarModelLines[i]) then
      with FarModelLines[i] do if (pTypeSys<1) or (FTypeSys=pTypeSys) then State:= pState;
  finally
    CS_MLines.Leave;
  end;
end;

//******************************************************************************
//          TManufacturer - Производитель авто/мото/двигателей
//******************************************************************************
//============ сортировка TStringList модельных рядов - видимость + наименование
function ManufMLsSortCompare(List: TStringList; Index1, Index2: Integer): Integer;
var fl1, fl2: Boolean;
begin
  with Cache.FDCA do try
    fl1:= ModelLines[Integer(List.Objects[Index1])].IsVisible;
    fl2:= ModelLines[Integer(List.Objects[Index2])].IsVisible;
    if fl1=fl2 then Result:= AnsiCompareText(List.Strings[Index1], List.Strings[Index2])
    else if fl1 then Result:= -1 else Result:= 1;
  except
    Result:= 0;
  end;
end;
//==============================================================================
constructor TManufacturer.Create(pID: Integer; pName: String; pTDnr: Integer=0);  // Код, Имя, Код TecDoc
begin
  inherited Create(pID, pTDnr, 0, pName, 0);
  FManufSysMLsSort:= TArraySysTypeLists.Create(False, True);  // списки модельных рядов по системам учета (Objects-ID)
  FManufSysMLsTopUp:= TArraySysTypeLists.Create(False, True); // списки модельных рядов с топами вверху по системам учета (Objects-ID)
  FMfauOpts:= [];
  FManufSearchONlist:= fnCreateStringList(True, 10);          // Сортированный список ориг.номеров производителя без спец/симв
end;
//==============================================================================
destructor TManufacturer.Destroy;
begin
  if not Assigned(self) then Exit;
  prFree(FManufSysMLsSort);
  prFree(FManufSysMLsTopUp);
  FMfauOpts:= [];
  prFree(FManufSearchONlist);
  inherited Destroy;
end;
//============================================================= записать признак
procedure TManufacturer.SetBoolMf(ik: T8InfoKinds; Value: boolean);
begin
  if not Assigned(self) then Exit;
  if Value then FMfauOpts:= FMfauOpts+[ik] else FMfauOpts:= FMfauOpts-[ik];
end;
//============================================================= получить признак
function TManufacturer.GetBoolMf(ik: T8InfoKinds): boolean;
begin
  if not Assigned(self) then Result:= False else Result:= (ik in FMfauOpts);
end;
//================ Проверка принадлежности к какой-нибудь системе кроме pTypeSys
function TManufacturer.CheckOtherTypeSys(pTypeSys: Integer): Boolean;
var i, j: integer;
begin
  Result:= False;
  if not Assigned(self) then Exit;
  with SysTypes do for i:= 0 to Count-1 do begin
    j:= GetDirItemID(ItemsList[i]);
    if CheckIsTypeSys(j) and (j<>pTypeSys) then begin
      Result:= True;
      Exit;
    end;
  end;
end;
//======================== Проверка принадлежности производителя к системе учета
function TManufacturer.CheckIsTypeSys(pTypeSys: Integer): Boolean;
begin
  Result:= False;
  if not Assigned(self) then Exit;
  case pTypeSys of
    constIsAuto: Result:= IsMfAUTO;
    constIsMoto: Result:= IsMfMOTO;
    else Result:= False;
  end;
end;
//============================ Проверить производитель ТОПОВЫЙ для системы учета
function TManufacturer.CheckIsTop(pTypeSys: Integer): Boolean;
begin
  Result:= False;
  if not Assigned(self) then Exit;
  case pTypeSys of
    constIsAuto: Result:= IsMfAUTO and IsMfTopA;
    constIsMoto: Result:= IsMfMOTO and IsMfTopM;
    else Result:= False;
  end;
end;
//========================================================== Проверить видимость
function TManufacturer.CheckIsVisible(pTypeSys: Integer): Boolean;
begin
  Result:= False;
  if not Assigned(self) then Exit;
  case pTypeSys of
    constIsAuto: Result:= IsMfAUTO and IsMfVisA;
    constIsMoto: Result:= IsMfMOTO and IsMfVisM;
    else Result:= False;
  end;
end;
//==================================== Установить / снять принадлежность системе
procedure TManufacturer.TypeSysSet(pTypeSys: Integer; IsSys: Boolean);
begin
  if not Assigned(self) then Exit;
  case pTypeSys of
    constIsAuto: IsMfAUTO:= IsSys;
    constIsMoto: IsMfMOTO:= IsSys;
  end;
end;
//================================================= Установить / снять ТОПОВОСТЬ
procedure TManufacturer.TopsSet(pTypeSys: Integer; IsTop: Boolean);
begin
  if not Assigned(self) then Exit;
  case pTypeSys of
    constIsAuto: if IsMfAUTO then IsMfTopA:= IsTop;
    constIsMoto: if IsMfMOTO then IsMfTopM:= IsTop;
  end;
end;
//================================================= Установить / снять видимость
procedure TManufacturer.VisibleSet(pTypeSys: Integer; IsVis: Boolean);
begin
  if not Assigned(self) then Exit;
  case pTypeSys of
    constIsAuto: if IsMfAUTO then IsMfVisA:= IsVis;
    constIsMoto: if IsMfMOTO then IsMfVisM:= IsVis;
  end;
end;
//=========================== Проверка имеются ли у производителя модельные ряды
function TManufacturer.CheckHasModelLines(pTypeSys: Integer): Boolean;
begin
  Result:= False;
  if not Assigned(self) then Exit;
  CheckModelLinesLists(pTypeSys);
  Result:= CheckIsTypeSys(pTypeSys) and (FManufSysMLsSort[pTypeSys].Count>0);
end;
//================= признак наличия видимых модельных рядов по выбранной системе
function TManufacturer.HasVisModelLines(pTypeSys: Integer): Boolean;
var i: integer;
begin
  Result:= False;
  if not Assigned(self) then Exit;
  CheckModelLinesLists(pTypeSys);
  with FManufSysMLsSort[pTypeSys] do for i:= 0 to Count-1 do
    if Cache.FDCA.ModelLines[Integer(Objects[i])].IsVisible then begin
      Result:= True;
      break;
    end;
end;
//========================= признак наличия видимых моделей по выбранной системе
function TManufacturer.HasVisMLModels(pTypeSys: Integer): Boolean;
var i: integer;
begin
  Result:= False;
  if not Assigned(self) then Exit else CheckModelLinesLists(pTypeSys);
  with FManufSysMLsSort[pTypeSys] do for i:= 0 to Count-1 do
    with Cache.FDCA.ModelLines[Integer(Objects[i])] do
      if IsVisible and HasVisModels then begin
        Result:= True;
        break;
      end;
end;
//==================== Список модельных рядов производителя по выбранной системе
function TManufacturer.GetModelLinesList(pTypeSys: Integer; pTopsUp: Boolean): TStringList;
begin
  if not Assigned(self) or not CheckIsTypeSys(pTypeSys) then begin
    Result:= EmptyStringList;
    Exit;
  end;
  CheckModelLinesLists(pTypeSys);
  if pTopsUp then Result:= FManufSysMLsTopUp[pTypeSys]
  else Result:= FManufSysMLsSort[pTypeSys];
end;
//============================= получить код модельного ряда авто по коду TecDoc
function TManufacturer.GetMfMLineIDByTDcode(pmlTDnr: Integer): Integer;
var i: integer;
begin
  Result:= -1;
  if not Assigned(self) then Exit;
  with GetModelLinesList(constIsAuto, False) do for i:= 0 to Count-1 do begin
    Result:= Integer(Objects[i]);
    if Cache.FDCA.ModelLines[Result].SubCode=pmlTDnr then Exit;
  end;
  Result:= -1;
end;

//============================ Список моделей производителя по выбранной системе
function TManufacturer.GetModelsList(pTypeSys: Integer): TStringList; // must Free Result
var i, j: Integer;
begin
  Result:= TStringList.Create;
  if not Assigned(self) then Exit;
  with GetModelLinesList(pTypeSys, False) do for i:= 0 to Count-1 do
    with Cache.FDCA.ModelLines[Integer(Objects[i])].GetListModels do
      for j:= 0 to Count-1 do Result.AddObject(Strings[j], Objects[j]);
end;
//========== заполнить списки модельных рядов производителя по выбранной системе
procedure TManufacturer.CheckModelLinesLists(pTypeSys: Integer);
const nmProc = 'CreateModelLinesList';
var lst: TStringList;
    i, j: integer;
    fLast: boolean;
begin
  if not Assigned(self) then Exit;
  if not CheckIsTypeSys(pTypeSys) then Exit;
  if (FManufSysMLsSort[pTypeSys].Delimiter=LCharGood) and
    (FManufSysMLsTopUp[pTypeSys].Delimiter=LCharGood) then Exit;

  lst:= nil;
  try
    with FManufSysMLsSort do
      if Items[pTypeSys].Delimiter=LCharUpdate then try // если список некорректный
        lst:= Cache.FDCA.ModelLines.GetManufSysModelLinesList(FID, pTypeSys);
        AddTypeListItems(pTypeSys, lst, True);
        Items[pTypeSys].CustomSort(ManufMLsSortCompare);
        FManufSysMLsTopUp.SetTypeListDelimiter(pTypeSys, LCharUpdate); // флажок некорректности списку с топами
      finally
        prFree(lst);
      end;

    with FManufSysMLsTopUp do
      if Items[pTypeSys].Delimiter=LCharUpdate then begin // если список с топами некорректный
        AddTypeListItems(pTypeSys, FManufSysMLsSort[pTypeSys], True);
        CS_ATLists.Enter;
        try
          with Items[pTypeSys] do if Count>0 then begin  // переставляем топы
            j:= Integer(Objects[0]); // запоминаем 1-й элемент
            i:= Count-1;
            fLast:= False;
            while not fLast do begin         // перебираем с конца
              fLast:= Integer(Objects[i])=j; // true - последняя проверка
              if Cache.FDCA.ModelLines[Integer(Objects[i])].IsTop then
                Move(i, 0)               // топ переносим в начало
              else inc(i, -1);
            end;
          end;
        finally
          CS_ATLists.Leave;
        end;
      end;
  except
    on E: Exception do prMessageLOGS(nmProc+': '+E.Message, fLogCache);
  end;
end;
//======================= Добавить модельный ряд в кеш к производителю к системе
function TManufacturer.ModelLineAdd(var ModelLineID: Integer; pName: String;
         pTypeSys, pMS, pYS, pME, pYE, pUserID: Integer; pIsTop, pIsVis: Boolean; pMLTD: Integer=0): String;
const nmProc='ModelLineAdd';
var ORD_IBS: TIBSQL;
    ORD_IBD: TIBDatabase;
    ModelLine: TModelLine;
    sName: String;
    ii, jj: Integer;
begin
  Result:= '';
  ORD_IBS:= nil;
  try
    if not Assigned(self) then raise EBOBError.Create(MessText(mtkErrAddRecord));
    if not CheckIsTypeSys(pTypeSys) then
      raise EBOBError.Create(MessText(mtkNotFoundTypeSys, IntToStr(pTypeSys)));

    sName:= GetNameForSort(pName, pYS, pMS);
    if CheckHasModelLines(pTypeSys) and
      (FManufSysMLsSort.GetTypeListItemIDByName(pTypeSys, sName)>-1) then
      raise EBOBError.Create(MessText(mtkDuplicateName, sName)); // Дубликат

    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise EBOBError.Create(MessText(mtkErrConnectToDB));

    ModelLineID:= 0;
    try
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite, True);
      ORD_IBS.SQL.Text:= 'insert into DIRMODELLINES (DRMLDTSYCODE, '+
        ' DRMLMFAUCODE, DRMLISTOP, DRMLNAME, '+
        fnIfStr(pMS=0,   '', 'DRMLMONTHSTART, ')+
        fnIfStr(pYS=0,   '', 'DRMLYEARSTART, ')+
        fnIfStr(pME=0,   '', 'DRMLMONTHEND, ')+
        fnIfStr(pYE=0,   '', 'DRMLYEAREND, ')+
        fnIfStr(pMLTD=0, '', 'DRMLTDCODE, ')+
        ' DRMLISVISIBLE, DRMLUSERID) values ('+
        IntToStr(pTypeSys)+', '+IntToStr(FID)+', :isTop, :DRMLNAME, '+
        fnIfStr(pMS=0,   '', IntToStr(pMS)+', ')+
        fnIfStr(pYS=0,   '', IntToStr(pYS)+', ')+
        fnIfStr(pME=0,   '', IntToStr(pME)+', ')+
        fnIfStr(pYE=0,   '', IntToStr(pYE)+', ')+
        fnIfStr(pMLTD=0, '', IntToStr(pMLTD)+', ')+
        ':IsVis, '+IntToStr(pUserID)+') RETURNING DRMLCODE';
      ORD_IBS.ParamByName('DRMLNAME').AsString:= pName;
      ORD_IBS.ParamByName('isTop').AsString:= fnIfStr(pIsTop, 'T', 'F');
      ORD_IBS.ParamByName('IsVis').AsString:= fnIfStr(pIsVis, 'T', 'F');;
      ORD_IBS.ExecQuery;
      if not (ORD_IBS.Bof and ORD_IBS.Eof) then
        ModelLineID:= ORD_IBS.Fields[0].asInteger;
      ORD_IBS.Transaction.Commit;
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD);
    end;
    if ModelLineID<1 then raise EBOBError.Create(MessText(mtkErrAddRecord));

    ModelLine:= TModelLine.Create(ModelLineID, pMLTD, FID, pName);
    with ModelLine do begin
      FTypeSys:= pTypeSys;
      IsTop   := pIsTop;
      FYStart := pYS;
      FMStart := pMS;
      FYEnd   := pYE;
      FMEnd   := pME;
      IsVisible:= pIsVis;
    end;
    with Cache.FDCA.ModelLines do try
      CS_MLines.Enter;
      try
        if High(FarModelLines)<ModelLineID then begin
          jj:= Length(FarModelLines);            // добавляем длину массива
          SetLength(FarModelLines, ModelLineID+100);   // и инициируем элементы
          for ii:= jj to High(FarModelLines) do
           if ii<>ModelLineID then FarModelLines[ii]:= nil;
        end;
        FarModelLines[ModelLineID]:= ModelLine;
      finally
        CS_MLines.Leave;
      end;
    except
      prFree(ModelLine);
      raise EBOBError.Create(MessText(mtkErrAddRecord));
    end;
    FManufSysMLsSort.SetTypeListDelimiter(pTypeSys, LCharUpdate);
    FManufSysMLsTopUp.SetTypeListDelimiter(pTypeSys, LCharUpdate);
  except
    on E: Exception do begin
      Result:= E.Message;
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
    end;
  end;
end;
//================================== Удалить модельный ряд из кеша производителя
function TManufacturer.ModelLineDel(pModelLineID: Integer): String;
const nmProc='ModelLineDel';
var ORD_IBS: TIBSQL;
    ORD_IBD: TIBDatabase;
    SysID  : Integer;
begin
  Result:= '';
  ORD_IBS:= nil;
  try
    if not Assigned(self) then raise EBOBError.Create(MessText(mtkErrDelRecord));
    if not Cache.FDCA.ModelLines.ModelLineExists(pModelLineID) then
      raise EBOBError.Create(MessText(mtkNotFoundModLine, IntToStr(pModelLineID)));  // Модельный ряд с таким кодом отсутствует
    if Cache.FDCA.ModelLines[pModelLineID].ModelsCount>0 then
      raise EBOBError.Create('Модельный ряд имеет модели.');
    SysID:= Cache.FDCA.ModelLines[pModelLineID].FTypeSys;
    if CheckHasModelLines(SysID) and // Модельный ряд отсутствует в списке модельных рядов производителя по коду системы модельного ряда
      not FManufSysMLsSort.TypeListItemExists(SysID, pModelLineID) then  // ???
      raise EBOBError.Create(MessText(mtkNotFoundModLine, IntToStr(pModelLineID)));

    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise EBOBError.Create(MessText(mtkErrConnectToDB));
    try
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite, true);
      ORD_IBS.SQL.Text:= 'delete from DIRMODELLINES where DRMLCODE='+IntToStr(pModelLineID);
      ORD_IBS.ExecQuery;
      ORD_IBS.Transaction.Commit;
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD);
    end;

    with Cache.FDCA.ModelLines do   // Удаление модельного ряда из кеша
      if ModelLineExists(pModelLineID) then try
        CS_MLines.Enter;
        prFree(FarModelLines[pModelLineID]);
      finally
        CS_MLines.Leave;
      end;

    if FManufSysMLsSort[SysID].Delimiter=LCharGood then       // если список заполнен
      FManufSysMLsSort.DelTypeListItem(SysID, pModelLineID);  // Удаление модельного ряда в кеше из списка МР производителя
    if FManufSysMLsTopUp[SysID].Delimiter=LCharGood then      // если список заполнен
      FManufSysMLsTopUp.DelTypeListItem(SysID, pModelLineID); // Удаление модельного ряда в кеше из списка с топами
  except
    on E: Exception do begin
      Result:= E.Message;
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
    end;
  end;
end;
//============== Изменить модельный ряд в кеш и в базу к производителю к системе
function TManufacturer.ModelLineEdit(pModelLineID, pYS, pMS, pYE, pME, pUserID: Integer;
         pIsTop, pIsVis: Boolean; pName: String=''; pMLTD: Integer=0): String;
const nmProc='ModelLineEdit';
var ORD_IBS: TIBSQL;
    ORD_IBD: TIBDatabase;
    sName, sSql: String;
    SysID, i: Integer;
    fUp, fTop, fVis: Boolean;
    ModelLine: TModelLine;
begin
  Result:= '';
  ORD_IBS:= nil;
  try
    if not Assigned(self) then raise Exception.Create(MessText(mtkErrProcess));
    if not Cache.FDCA.ModelLines.ModelLineExists(pModelLineID) then
      Exception.Create(MessText(mtkNotFoundModLine, IntToStr(pModelLineID))); // Модельный ряд с таким кодом отсутствует
    ModelLine:= Cache.FDCA.ModelLines[pModelLineID];
    SysID:= ModelLine.FTypeSys;
    if CheckHasModelLines(SysID) and // Модельный ряд отсутствует в списке модельных рядов производителя по коду системы модельного ряда
      not FManufSysMLsSort.TypeListItemExists(SysID, pModelLineID) then
      Exception.Create(MessText(mtkNotFoundModLine, IntToStr(pModelLineID)));  // ???
    if not Assigned(ModelLine) then raise Exception.Create(MessText(mtkErrProcess));

    if pName<>'' then sName:= GetNameForSort(pName, pYS, pMS)
    else sName:= GetNameForSort(ModelLine.Name, pYS, pMS);
    i:= FManufSysMLsSort.GetTypeListItemIDByName(SysID, sName);
    if (i>-1) and (i<>pModelLineID) then
      raise Exception.Create(MessText(mtkDuplicateName, sName));  // Дубликат наименования

    sSql:= '';
    with ModelLine do begin
      if (pName<>'') and (Name<>pName) then sSql:= sSql+'DRMLNAME=:DRMLNAME, ';
      if (FYStart<>pYS) then sSql:= sSql+'DRMLYEARSTART='+fnIfStr(pYS>0, IntToStr(pYS), 'null')+', ';
      if (FMStart<>pMS) then sSql:= sSql+'DRMLMONTHSTART='+fnIfStr(pYS>0, IntToStr(pMS), 'null')+', ';
      fUp:= sSql<>''; // признак изменения наименования в списках
      if (FYEnd<>pYE) then sSql:= sSql+'DRMLYEAREND='+fnIfStr(pYS>0, IntToStr(pYE), 'null')+', ';
      if (FMEnd<>pME) then sSql:= sSql+'DRMLMONTHEND='+fnIfStr(pYS>0, IntToStr(pME), 'null')+', ';
      if (pMLTD>0) and (SubCode<>pMLTD) then sSql:= sSql+'DRMLTDCODE='+IntToStr(pMLTD)+', ';
      fTop:= IsTop<>pIsTop;
      if fTop then sSql:= sSql+'DRMLISTOP=:IsTop, ';
      fVis:= IsVisible<>pIsVis;
      if fVis then sSql:= sSql+'DRMLISVISIBLE=:isVis, ';
    end;
    if sSql='' then raise Exception.Create(MessText(mtkNotParams));

    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise Exception.Create(MessText(mtkErrConnectToDB));
    try
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite, True);
      ORD_IBS.SQL.Text:= 'update DIRMODELLINES set '+sSql+' DRMLUSERID='+IntToStr(pUserID)+
                         ' where DRMLCODE='+IntToStr(pModelLineID);
      if ModelLine.Name<>pName then ORD_IBS.ParamByName('DRMLNAME').AsString:=pName;
      if fTop then ORD_IBS.ParamByName('IsTop').AsString:= fnIfStr(pIsTop, 'T', 'F');
      if fVis then ORD_IBS.ParamByName('isVis').AsString:= fnIfStr(pIsVis, 'T', 'F');
      ORD_IBS.ExecQuery;
      ORD_IBS.Transaction.Commit;
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD);
    end;

    with ModelLine do begin
      if (pName<>'') and (FName<>pName) then FName:= pName;
      if (FYStart<>pYS) then FYStart:= pYS;
      if (FMStart<>pMS) then FMStart:= pMS;
      if (FYEnd<>pYE)   then FYEnd  := pYE;
      if (FMEnd<>pME)   then FMEnd  := pME;
      if (pMLTD>0) and (SubCode<>pMLTD) then FSubCode:= pMLTD;
      if fTop then IsTop    := pIsTop;
      if fVis then IsVisible:= pIsVis;
    end;
    if fUP or fVis then FManufSysMLsSort.SetTypeListDelimiter(SysID, LCharUpdate)
    else if fTop then FManufSysMLsTopUp.SetTypeListDelimiter(SysID, LCharUpdate);
  except
    on E: Exception do begin
      Result:= E.Message;
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
    end;
  end;
end;

//******************************************************************************
//                           TManufacturers
//******************************************************************************
//===== сортировка TStringList производителей системы - видимость + наименование
function ManufSort(List: TStringList; Index1, Index2, SysID: Integer): Integer;
var fl1, fl2: Boolean;
begin
  with List do try
    fl1:= TManufacturer(Objects[Index1]).CheckIsVisible(SysID);
    fl2:= TManufacturer(Objects[Index2]).CheckIsVisible(SysID);
    if fl1=fl2 then Result:= AnsiCompareText(Strings[Index1], Strings[Index2])
    else if fl1 then Result:= -1 else Result:= 1;
  except
    Result:= 0;
  end;
end;
//======== сортировка TStringList производителей авто - видимость + наименование
function AutoManufSortCompare(List: TStringList; Index1, Index2: Integer): Integer;
begin
  Result:= ManufSort(List, Index1, Index2, constIsAuto);
end;
//======== сортировка TStringList производителей мото - видимость + наименование
function MotoManufSortCompare(List: TStringList; Index1, Index2: Integer): Integer;
begin
  Result:= ManufSort(List, Index1, Index2, constIsMoto);
end;
//==============================================================================
constructor TManufacturers.Create;
begin
  inherited;
  CS_Mfaus:= TCriticalSection.Create;
  SetLength(FarManufacturers, 1);               // Массив производителей
  FarManufacturers[0]:= TManufacturer.Create(0, '');
  FSysManufListSort:= TArraySysTypeLists.Create(False, True);  // списки по системам учета (Objects-Pointer)
  FSysManufListTopUp:= TArraySysTypeLists.Create(False, True); // списки с топами вверху по системам учета (Objects-Pointer)
end;
//==============================================================================
destructor TManufacturers.Destroy;
var i: Integer;
begin
  if not Assigned(self) then Exit;
  for i:= Low(FarManufacturers) to High(FarManufacturers) do
    if Assigned(FarManufacturers[i]) then try prFree(FarManufacturers[i]); except end;
  SetLength(FarManufacturers, 0);
  prFree(CS_Mfaus);
  prFree(FSysManufListSort);  // Сортированные списки по системам учета
  prFree(FSysManufListTopUp); // списки с топами вверху по системам учета
  inherited Destroy;
end;
//=================================== устанавливает флаг проверки всем элементам
procedure TManufacturers.SetStates(pState: Boolean; pTypeSys: Integer=0);
var i: Integer;
begin
  if not Assigned(self) or (length(FarManufacturers)<2) then Exit;
  CS_Mfaus.Enter;
  try
    for i:= 1 to High(FarManufacturers) do
      if Assigned(FarManufacturers[i]) and ((pTypeSys<1) or
        FarManufacturers[i].CheckIsTypeSys(pTypeSys)) then
        FarManufacturers[i].State:= pState;
  finally
    CS_Mfaus.Leave;
  end;
end;
//================================================ получить список непроверенных
function TManufacturers.GetNotTestedList(pTypeSys: Integer=0; flTested: TBooleanDynArray=nil): TStringList; // must Free Result
var i: Integer;
    fl: Boolean;
begin
  Result:= nil;
  if not Assigned(self) then Exit;
  Result:= TStringList.Create;
  for i:= 1 to High(FarManufacturers) do
    if Assigned(FarManufacturers[i]) then with FarManufacturers[i] do begin
      if Assigned(flTested) then // если есть массив флагов - проверяем по ним
        fl:= not ((length(flTested)>i) and flTested[i])
      else fl:= not State;
      if fl and ((pTypeSys<1) or CheckIsTypeSys(pTypeSys)) then
        Result.AddObject(Name, Pointer(i));
    end;
  if Result.Count>1 then Result.Sort;
end;
// получить сортированный список производителей двигателей (Object - Pointer(ID))
function TManufacturers.GetEngManufList: TStringList; // must Free Result
var i: Integer;
begin
  Result:= TStringList.Create;
  if not Assigned(self) then Exit;
  for i:= 1 to High(FarManufacturers) do
    if Assigned(FarManufacturers[i]) then with FarManufacturers[i] do begin
      if MfHasEngWares then Result.AddObject(Name, Pointer(i));
    end;
  if Result.Count>1 then Result.Sort;
end;
//----- получить сортированный список производителей с ОН (Object - Pointer(ID))
function TManufacturers.GetOEManufList: TStringList;  // must Free
var i: Integer;
begin
  Result:= TStringList.Create;
  if not Assigned(self) then Exit;
  for i:= 1 to High(FarManufacturers) do
    if Assigned(FarManufacturers[i]) then with FarManufacturers[i] do begin
      if ManufSearchONlist.Count>0 then Result.AddObject(Name, Pointer(i));
    end;
  if Result.Count>1 then Result.Sort;
end;

//============================================= проверить существование элемента
function TManufacturers.CheckManufItem(pCODE: Integer; var flNew: Boolean; pNAME: String; pTDnr: Integer=0): Boolean;
// flNew=True - создавать новый элемент, на выходе признак нового элемента
var ii, jj: Integer;
begin
  Result:= False;
  if not Assigned(self) then Exit;
  Result:= Cache.FDCA.ManufAutoExist(pCODE);
  if Result then flNew:= False else if flNew then begin
    if (High(FarManufacturers)<pCODE) then begin
      jj:= Length(FarManufacturers);            // добавляем длину массива
      SetLength(FarManufacturers, pCode+100);   // и инициируем элементы
      for ii:= jj to High(FarManufacturers) do
       if ii<>pCode then FarManufacturers[ii]:= nil;
    end;
    FarManufacturers[pCODE]:= TManufacturer.Create(pCODE, pNAME, pTDnr); // Создание производителя авто
    Result:= Assigned(FarManufacturers[pCODE]);
  end;
end;
//======== Возвращает указатель на производителя с кодов pIndex или на 0 элемент
function TManufacturers.GetManufItem(pIndex: Integer): TManufacturer;
begin
  Result:= nil;
  if not Assigned(self) then Exit;
  if ManufExists(pIndex) then Result:= FarManufacturers[pIndex]
  else Result:= FarManufacturers[0];
end;
//================================================ получить сортированный список
function TManufacturers.GetSortedList(pTypeSys: Integer): TStringList;
begin
  Result:= nil;
  if not Assigned(self) then Exit;
  if FSysManufListSort[pTypeSys].Delimiter=LCharUpdate then // если надо формировать
    CheckManufLists(pTypeSys);
  Result:= FSysManufListSort[pTypeSys];
//  if pTypeSys=2 then for i:= 0 to FSysManufListSort[2].Count-1 do
//    prMessageLOGS('FSysManufListSort - '+FSysManufListSort[2].Strings[i], 'test', false);
end;
//============================================== получить список с топами вверху
function TManufacturers.GetSortedListWithTops(pTypeSys: Integer): TStringList;
begin
  Result:= nil;
  if not Assigned(self) then Exit;
  if (FSysManufListSort[pTypeSys].Delimiter=LCharUpdate) or
    (FSysManufListTopUp[pTypeSys].Delimiter=LCharUpdate) then // если надо переформировать
    CheckManufLists(pTypeSys);
  Result:= FSysManufListTopUp[pTypeSys];
//  if pTypeSys=2 then for i:= 0 to FSysManufListTopUp[2].Count-1 do
//    prMessageLOGS('FSysManufListTopUp - '+FSysManufListTopUp[2].Strings[i], 'test', false);
end;
//===================================================== заполнить списки системы
procedure TManufacturers.CheckManufLists(pTypeSys: Integer);
var i   : Integer;
    Flst: TStringList;
    p: Pointer;
    fLast: Boolean;
begin
  if not Assigned(self) then Exit;
  Flst:= nil;
  try
    if FSysManufListSort[pTypeSys].Delimiter=LCharUpdate then begin // если надо формировать
      Flst:= TStringList.Create;
      for i:= 1 to High(FarManufacturers) do // выбираем
        if Assigned(FarManufacturers[i]) and FarManufacturers[i].CheckIsTypeSys(pTypeSys) then
          Flst.AddObject(FarManufacturers[i].Name, FarManufacturers[i]);
      FSysManufListSort.AddTypeListItems(pTypeSys, Flst, True); // добавляем
      with FSysManufListSort[pTypeSys] do
        case pTypeSys of // сортируем с учетом видимости
          constIsAuto: CustomSort(AutoManufSortCompare);
          constIsMoto: CustomSort(MotoManufSortCompare);
        end;
      FSysManufListTopUp.SetTypeListDelimiter(pTypeSys, LCharUpdate);
    end;

    with FSysManufListTopUp do
      if Items[pTypeSys].Delimiter=LCharUpdate then begin // если надо переформировать
        AddTypeListItems(pTypeSys, FSysManufListSort[pTypeSys], True); // добавляем сортированный список
        CS_ATLists.Enter;
        with Items[pTypeSys] do try
          if Count>0 then begin // переставляем топы
            p:= Objects[0];    // запоминаем 1-й элемент
            i:= Count-1;
            fLast:= False;
            while not fLast do begin    // перебираем с конца
              fLast:= Objects[i]=p;     // true - последняя проверка
              if TManufacturer(Objects[i]).CheckIsTop(pTypeSys) then
                Move(i, 0)               // топ переносим в начало
              else inc(i, -1);
            end;
          end;
        finally
          CS_ATLists.Leave;
        end;
      end;
  finally
    if Assigned(Flst) then prFree(Flst);
  end;
end;
//======================================================= добавить производителя
function TManufacturers.ManufAdd(var ManufID: Integer; pName: String; pTypeSys, pUserID: Integer;
         pIsTop, pIsVis: boolean; pTDnr: Integer=0): String;
const nmProc='ManufAdd';
var ORD_IBS: TIBSQL;
    ORD_IBD: TIBDatabase;
    s: string;
    flNew: Boolean;
begin
  Result:= '';
  ORD_IBD:= nil;
  ORD_IBS:= nil;
  try
    if not Assigned(self) then raise EBOBError.Create(MessText(mtkErrAddRecord));
    ManufID:= -1;
    if ManufExistsByName(pName, ManufID) and
      FarManufacturers[ManufID].CheckIsTypeSys(pTypeSys) then
      raise EBOBError.Create(MessText(mtkDuplicateName, pName));

    if ManufID>0 then begin // Если производитель есть по другой системе
      Result:= ManufEdit(ManufID, pTypeSys, pUserID, pIsTop, True, pName, pTDnr);
      Exit;

    end else try  // Если производитель действительно новый
      case pTypeSys of
        constIsAuto: s:= ', MFAUISMFAUTO, MFAUISTOPAUTO, MFAUVISIBLEAUTO';
        constIsMoto: s:= ', MFAUISMFMOTO, MFAUISTOPMOTO, MFAUVISIBLEMOTO';
        else s:= '';
      end;
      ORD_IBD:= cntsOrd.GetFreeCnt;
      if not Assigned(ORD_IBD) then raise EBOBError.Create(MessText(mtkErrConnectToDB));
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite, True);
      ORD_IBS.SQL.Text:= 'insert into MANUFACTURERAUTO'+
        ' (MFAUNAME, MFAUISMFAU, '+fnIfStr(pTDnr<1, '', ' MFAUTDMFNR, ')+
        'MFAUUSERID'+s+
        ') values (:MFAUNAME, "T", '+fnIfStr(pTDnr<1, '', ' '+IntToStr(pTDnr)+', ')+
        IntToStr(pUserID)+fnIfStr(s='', '', ', "T", '+fnIfStr(pIsTop, '"T"', '"F"')+
        ', '+fnIfStr(pIsVis, '"T"', '"F"')+'')+') RETURNING MFAUCODE';
      ORD_IBS.ParamByName('MFAUNAME').AsString:= pName;
      ORD_IBS.ExecQuery;
      if not (ORD_IBS.Bof and ORD_IBS.Eof) then ManufID:= ORD_IBS.Fields[0].asInteger;
      ORD_IBS.Transaction.Commit;
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD);
    end;
    if ManufID<1 then raise EBOBError.Create(MessText(mtkErrAddRecord));

    flNew:= True;
    CS_Mfaus.Enter;
    try
      if CheckManufItem(ManufID, flNew, pName, pTDnr) then begin // Создание производителя авто
        with FarManufacturers[ManufID] do begin
          IsMF:= True;
          TypeSysSet(pTypeSys, True);   // принадлежность системе
          TopsSet(pTypeSys, pIsTop);    // Топовость
          VisibleSet(pTypeSys, pIsVis); // видимость
        end;
        FSysManufListSort[pTypeSys].Delimiter:= LCharUpdate;
        FSysManufListTopUp[pTypeSys].Delimiter:= LCharUpdate;
      end;
    finally
      CS_Mfaus.Leave;
    end;
  except
    on E: EBOBError do Result:= E.Message;
    on E: Exception do begin
      Result:= E.Message;
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
    end;
  end;
  if (Result<>'') and (ManufID>0) and ManufExists(ManufID) then 
    prFree(FarManufacturers[ManufID]);
end;
//======================================================== удалить производителя
function TManufacturers.ManufDel(var pID: Integer; pTypeSys: Integer): String;
const nmProc='ManufDel';
var ORD_IBS: TIBSQL;
    ORD_IBD: TIBDatabase;
    s: string;
    CheckOther: boolean;
begin
  Result:= '';
  ORD_IBS:= nil;
  try
    if not Assigned(self) then raise EBOBError.Create(MessText(mtkErrDelRecord));
    if not ManufExists(pID) then
      raise EBOBError.Create(MessText(mtkNotFoundManuf, IntToStr(pID))); // нечего удалять - производителя нет в кеше
    if FarManufacturers[pID].CheckHasModelLines(pTypeSys) then
      raise EBOBError.Create('Производитель имеет модельные ряды.'); // есть модельные ряды по данной системе учета
    if (FarManufacturers[pID].ManufSearchONlist.Count>0) then
      raise EBOBError.Create('Производитель имеет оригинальные номера.'); // есть оригинальные номера

    CheckOther:= FarManufacturers[pID].CheckOtherTypeSys(pTypeSys);
    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise EBOBError.Create(MessText(mtkErrConnectToDB));
    try
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite, True);
      if CheckOther then begin // если числится по другой системе
        case pTypeSys of
          constIsAuto: s:= 'MFAUISMFAUTO="F", MFAUISTOPAUTO="F", MFAUVISIBLEAUTO="F"';
          constIsMoto: s:= 'MFAUISMFMOTO="F", MFAUISTOPMOTO="F", MFAUVISIBLEMOTO="F"';
        end;
        ORD_IBS.SQL.Text:= 'update MANUFACTURERAUTO set '+s+' where MFAUCODE='+IntToStr(pID);
      end else
        ORD_IBS.SQL.Text:= 'delete from MANUFACTURERAUTO where MFAUCODE='+IntToStr(pID);
      ORD_IBS.ExecQuery;
      ORD_IBS.Transaction.Commit;
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD);
    end;

    CS_Mfaus.Enter;
    try
      if FSysManufListSort[pTypeSys].Delimiter=LCharGood then
        FSysManufListSort.DelTypeListItem(pTypeSys, FarManufacturers[pID]);
      if FSysManufListTopUp[pTypeSys].Delimiter=LCharGood then
        FSysManufListTopUp.DelTypeListItem(pTypeSys, FarManufacturers[pID]);

      if CheckOther then with FarManufacturers[pID] do begin
        TypeSysSet(pTypeSys, False); // снять признак принадлежности системы
        TopsSet(pTypeSys, False);    // снять топ признак удаляемой системы
        VisibleSet(pTypeSys, False); // снять признак видимости системы
      end else begin
        prFree(FarManufacturers[pID]);    // Освобождение памяти от производителя
        pID:= 0;
      end;
    finally
      CS_Mfaus.Leave;
    end;
  except
    on E: EBOBError do Result:= E.Message;
    on E: Exception do begin
      Result:= E.Message;
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
    end;
  end;
end;
//======================================================= изменить производителя
function TManufacturers.ManufEdit(var pID: Integer; pTypeSys, pUserID: Integer;
         pIsTop, pIsVis: boolean; pName: String=''; pTDnr: Integer=0): String;
const nmProc = 'ManufEdit';
var ORD_IBS: TIBSQL;
    ORD_IBD: TIBDatabase;
    ManufID: Integer;
    s: string;
    SetSys, SetTop, SetNm, SetVis, SetTDnr: Boolean;
begin
  Result:= '';
  ORD_IBS:= nil;
  try
    if not Assigned(self) then raise EBOBError.Create(MessText(mtkErrEditRecord));
    if not ManufExists(pID) then raise EBOBError.Create(MessText(mtkNotFoundManuf, IntToStr(pID)));
    if (pName<>'') and (AnsiCompareText(FarManufacturers[pID].FName, pName)<>0)
      and ManufExistsByName(pName, ManufID) then
      raise EBOBError.Create(MessText(mtkDuplicateName, pName));

    with FarManufacturers[pID] do begin
      SetSys:= not CheckIsTypeSys(pTypeSys); // проверяем, надо ли что-то менять
      SetNm:= (pName<>'') and (Name<>pName);
      SetTDnr:= (pTDnr>0) and (SubCode<>pTDnr);
      if SetSys then begin
        SetTop:= True;
        SetVis:= True;
      end else begin
        SetTop:= CheckIsTop(pTypeSys)<>pIsTop;
        SetVis:= CheckIsVisible(pTypeSys)<>pIsVis;
      end;
    end;
    if not (SetTop or SetVis or SetNm or SetSys or SetTDnr) then
      raise EBOBError.Create(MessText(mtkNotParams));

    case pTypeSys of
      constIsAuto: s:= fnIfStr(SetTop, ', MFAUISTOPAUTO='+fnIfStr(pIsTop, '"T"', '"F"'), '')+
                       fnIfStr(SetVis, ', MFAUVISIBLEAUTO='+fnIfStr(pIsVis, '"T"', '"F"'), '')+
                       fnIfStr(SetSys, ', MFAUISMFAUTO="T"', '');
      constIsMoto: s:= fnIfStr(SetTop, ', MFAUISTOPMOTO='+fnIfStr(pIsTop, '"T"', '"F"'), '')+
                       fnIfStr(SetVis, ', MFAUVISIBLEMOTO='+fnIfStr(pIsVis, '"T"', '"F"'), '')+
                       fnIfStr(SetSys, ', MFAUISMFMOTO="T"', '');
      else s:= '';
    end;
    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise EBOBError.Create(MessText(mtkErrConnectToDB));
    try
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite, True);
      ORD_IBS.SQL.Text:= 'update MANUFACTURERAUTO set MFAUUSERID='+IntToStr(pUserID)+
        fnIfStr(SetNm, ', MFAUNAME=:MFAUNAME', '')+
        fnIfStr(SetTDnr, ', MFAUTDMFNR=:MFAUTDMFNR', '')+
        s+' where MFAUCODE='+IntToStr(pID);
      if SetNm   then ORD_IBS.ParamByName('MFAUNAME').AsString:= pName;
      if SetTDnr then ORD_IBS.ParamByName('MFAUTDMFNR').AsInteger:= pTDnr;
      ORD_IBS.ExecQuery;
      ORD_IBS.Transaction.Commit;
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD);
    end;

    CS_Mfaus.Enter;
    try
      with FarManufacturers[pID] do begin
        if SetNm then FName:= pName;
        if SetTDnr then FSubCode:= pTDnr;
        if SetSys then TypeSysSet(pTypeSys, True); // ??? старая система
        if SetTop then TopsSet(pTypeSys, pIsTop);
        if SetVis then VisibleSet(pTypeSys, pIsVis);
      end;
      if SetNm or SetSys or SetVis then FSysManufListSort.SetTypeListDelimiter(pTypeSys, LCharUpdate)
      else if SetTop then FSysManufListTopUp.SetTypeListDelimiter(pTypeSys, LCharUpdate);
    finally
      CS_Mfaus.Leave;
    end;
  except
    on E: EBOBError do Result:= E.Message;
    on E: Exception do begin
      Result:= E.Message;
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
    end;
  end;
end;
//================================= Проверка существования Производителя по коду 
function TManufacturers.ManufExists(pID: Integer): Boolean;
begin
  Result:= Assigned(self) and (pID>0) and (length(FarManufacturers)>pID) and Assigned(FarManufacturers[pID]);
end;
//================================ Проверка существования Производителя по имени
function TManufacturers.ManufExistsByName(pName: String; var pID: Integer): Boolean;
var SysID, i, index: Integer;
begin
  Result:= False;
  if not Assigned(self) then Exit;
  pName:= AnsiUpperCase(pName);
  with FSysManufListSort do
    for i:= 0 to ListTypes.Count-1 do begin
      SysID:= Integer(ListTypes.Objects[i]);
      CheckManufLists(SysID);
      index:= Items[SysID].IndexOf(pName);
      Result:= index>-1;
      if Result then begin
        pID:= TManufacturer(Items[SysID].Objects[index]).ID;
        Exit;
      end;
    end;
end;
//==================================== получить код производителя по коду TecDoc
function TManufacturers.GetManufIDByTDcode(pTDnr: Integer): Integer;
begin
  Result:= 0;
  if not Assigned(self) then Exit;
  for Result:= 1 to High(FarManufacturers) do
    if ManufExists(Result) and (FarManufacturers[Result].SubCode=pTDnr) then Exit;
  Result:= 0;
end;

//******************************************************************************
//                      TModelAuto - Модель авто/мото
//******************************************************************************
constructor TModelAuto.Create(pModelID, pModelTDcode, pOrdNum, pModelLineID: Integer; pName: String);
begin
  inherited Create(pModelID, pModelTDcode, pOrdNum, pName);
  CS_mlinks:= TCriticalSection.Create;
//  FModelHasWares:= False;
  FParCode  := pModelLineID;
  FParams   := TModelParams.Create;               // параметры модели
  FNodeLinks:= TNodeLinks.Create(CS_mlinks); // связки 2 узлов дерева с моделью
  FLinks    := TLinks.Create(CS_mlinks);     // связки с маркировками двигателей
end;
//==============================================================================
destructor TModelAuto.Destroy;
begin
  if not Assigned(self) then Exit;
  prFree(FParams);
  prFree(FNodeLinks);
  prFree(CS_mlinks);
  inherited Destroy;
end;
//==============================================================================
procedure TModelAuto.ClearLinks;
var i, j, modID: integer;
    ware: TWareInfo;
begin
  modID:= ID;
  for i:= 0 to FNodeLinks.LinkCount-1 do try // удаление из линков товаров
    with TSecondLink(FNodeLinks.ListLinks[i]) do if assigned(DoubleLinks) then
      with DoubleLinks do for j:= Count-1 downto 0 do try
        ware:= GetLinkPtr(Items[j]);
        if assigned(ware) and assigned(ware.ModelLinks) then
          ware.ModelLinks.DelLinkListItemByID(modID, lkDirNone, ware.CS_wlinks);
      except end;
  except end;
end;
//================================================================= получить код
function TModelAuto.GetIntM(const ik: T8InfoKinds): Integer;
var iSys: Integer;
begin
  Result:= 0;
  if not Assigned(self) then Exit else case ik of // 1
    ik8_3: Result:= FParCode;            // код модельного ряда
    ik8_4: Result:= FOrderNum;           // порядк.№ модели
    else with Cache.FDCA.ModelLines do if ModelLineExists(FParCode) then
      case ik of // 2
        ik8_2: Result:= GetModelLine(FParCode).MFAID;   // код производителя
        else begin
          iSys:= GetModelLine(FParCode).TypeSys;
          case ik of // 3
            ik8_1: Result:= iSys;                              // Тип системы 1 - Авто, 2 - Мото and etc.
          end; // case 3
        end;
      end; // case 2
  end; // case 1
end;
//================================================================= записать код
procedure TModelAuto.SetIntM(const ik: T8InfoKinds; Value: Integer);
begin
  if not Assigned(self) then Exit else case ik of // 1
    ik8_3: if FParCode<>Value  then FParCode:= Value;  // код модельного ряда
    ik8_4: if FOrderNum<>Value then FOrderNum:= Value; // порядк.№ модели
  end; // case 1
end;
//============================================================== получить строку
function TModelAuto.GetStrM(const ik: T8InfoKinds): String;
var i: integer;
begin
  Result:= '';
  if not Assigned(self) then Exit else case ik of // Наименование модели для сортировки
    ik8_1: with Params do Result:= GetNameForSort(Name, pYStart, pMStart);
                                                     // список маркировок двигателей
    ik8_2: if (EngLinks.LinkCount>0) then with EngLinks do for i:= 0 to LinkCount-1 do
           Result:= Result+fnIfStr(Result='', '', ', ')+GetLinkName(ListLinks[i]);
    ik8_3: with Cache.FDCA.ModelLines do               // Название модельного ряда модели
           if ModelLineExists(ModelLineID) then Result:= GetModelLine(ModelLineID).Name;
    ik8_4: begin
           i:= ModelMfauID;                          // Название производителя модели
           with Cache.FDCA.Manufacturers do if ManufExists(i) then Result:= Items[i].Name;
         end;                                        
    ik8_5: begin                                       // Название модели для вывода в Web
           Result:= ModelMfauName+' | '+ModelLineName+' | '+Name;
           if (TypeSys=constIsAuto) and Assigned(Params) then with Params do
             Result:= Result+' '+fnGetYMBE(pYStart, pMStart, pYEnd, pMEnd)+
                      ' | '+IntToStr(pHP)+'лс | '+MarksCommaText;
         end;
  end;
end;
//============================================================= наличие связок 2
function TModelAuto.GetNodesExists: boolean;
begin
  Result:= False;
  if not Assigned(self) then Exit;
  Result:= Assigned(NodeLinks) and (NodeLinks.LinkCount>0);
end;
//==================================================== Изменить видимость модели
function TModelAuto.SetModelVisible(isVis: Boolean): String;
const nmProc = 'SetModelVisible';
var ORD_IBD: TIBDatabase;
    ORD_IBS: TIBSQL;
begin
  Result:= '';
  if not Assigned(self) then Exit;
//  ORD_IBD:= nil;
  ORD_IBS:= nil;
  try
    if IsVisible=isVis then raise EBOBError.Create(MessText(mtkNotParams));
    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise EBOBError.Create(MessText(mtkErrConnectToDB));
    try
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite, true);
      ORD_IBS.SQL.Text:= 'update DIRMODELS set DMOSISVISIBLE=:vis where DMOSCODE='+IntToStr(ID);
      ORD_IBS.ParamByName('vis').AsString:= fnIfStr(isVis, 'T', 'F');
      ORD_IBS.ExecQuery;
      ORD_IBS.Transaction.Commit;
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD);
    end;

    IsVisible:= isVis;
    with Cache.FDCA.ModelLines[ModelLineID] do FMLModelsSort.Delimiter:= lCharUpdate;
  except
    on E: EBOBError do Result:= E.Message;
    on E: Exception do begin
      Result:= MessText(mtkErrEditRecord);
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
    end;
  end;
end;
//============================================================= Изменение модели
function TModelAuto.ModelEdit(pName: String; pVisible, pIsTop: Boolean; pUserID: Integer;
  mps: TModelParams; pOrdNum: Integer=-1; pTDcode: Integer=-1; marks: TStringList=nil): String;
const nmProc = 'ModelEdit';
// pOrdNum, pTDcode = -1 - не учитывать (не заданы или сбой при считывании)
var ORD_IBD: TIBDatabase;
    ORD_IBS: TIBSQL;
    sSql: String;
    flTop, flVisible, flName, fUpd, fOrdNum, flTD, flMarks: Boolean;
begin
  Result:= '';
  if not Assigned(self) then Exit;
//  ORD_IBD:= nil;
  ORD_IBS:= nil;
  flName:= Name<>pName;
  flVisible:= IsVisible<>pVisible;
  flTop:= IsTop<>pIsTop;
  fOrdNum:= (pOrdNum>-1) and (ModelOrderNum<>pOrdNum);
  flTD:= (pTDcode>-1) and (SubCode<>pTDcode);
  flMarks:= Assigned(marks) and CheckDiffModelMarks(marks);
  sSql:= '';
  if flName    then sSql:= sSql+'DMOSNAME=:NAME, ';
  if flVisible then sSql:= sSql+'DMOSISVISIBLE="'+fnIfStr(pVisible, 'T', 'F')+'", ';
  if flTop     then sSql:= sSql+'DMOSISTOP="'+fnIfStr(pIsTop, 'T', 'F')+'", ';
  if fOrdNum   then sSql:= sSql+'DMOSORDERNUM='+IntToStr(pOrdNum)+', ';
  if flTD      then sSql:= sSql+'DMOSTDCODE='+IntToStr(pTDcode)+', ';

  sSql:= sSql+GetModelParamsUpdSql(mps);
  fUpd:= (sSql<>'') and (flName or fOrdNum or
    (FParams.pYStart<>mps.pYStart) or (FParams.pMStart<>mps.pMStart));
  try
    if (sSql='') and not flMarks then raise EBOBError.Create(MessText(mtkNotParams));
    if (sSql<>'') then begin
      ORD_IBD:= cntsOrd.GetFreeCnt;
      if not Assigned(ORD_IBD) then raise EBOBError.Create(MessText(mtkErrConnectToDB));
      try
        ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite, true);
        ORD_IBS.SQL.Text:= 'update DIRMODELS set '+sSql+
          ' DMOSUSERID='+IntToStr(pUserID)+' where DMOSCODE='+IntToStr(ID);
        if flName then ORD_IBS.ParamByName('NAME').AsString:= pName;
        ORD_IBS.ExecQuery;
        ORD_IBS.Transaction.Commit;
      finally
        prFreeIBSQL(ORD_IBS);
        cntsOrd.SetFreeCnt(ORD_IBD);
      end;

      if flName    then Name      := pName;
      if flVisible then IsVisible:= pVisible;
      if flTop     then IsTop    := pIsTop;
      if fOrdNum   then ModelOrderNum:= pOrdNum;
      if flTD      then FSubCode  := pTDcode;
      SetModelParams(mps);
      with Cache.FDCA.ModelLines[ModelLineID] do
        if fUpd then FMLModelsSort.Delimiter:= lCharUpdate
        else if flTop then FMLModelsTopUp.Delimiter:= lCharUpdate;
    end; // if (sSql<>'')

    if flMarks then SetModelMarks(Marks, pUserID); // маркировки двигателей
  except
    on E: EBOBError do Result:= E.Message;
    on E: Exception do begin
      Result:= MessText(mtkErrEditRecord);
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
    end;
  end;
end;
//================================== записать / обновить параметры модели в кеше
procedure TModelAuto.SetModelParams(mps: TModelParams; flFill: Boolean=False);
begin
  if not Assigned(self) then Exit else with FParams do if flFill then begin
    pYStart    := mps.pYStart;
    pMStart    := mps.pMStart;
    pYEnd      := mps.pYEnd;
    pMEnd      := mps.pMEnd;
    pKW        := mps.pKW;
    pHP        := mps.pHP;
    pCCM       := mps.pCCM;
    pCylinders := mps.pCylinders;
    pValves    := mps.pValves;
    pBodyID    := mps.pBodyID;
    pDriveID   := mps.pDriveID;
    pEngTypeID := mps.pEngTypeID;
    pFuelID    := mps.pFuelID;
    pFuelSupID := mps.pFuelSupID;
    pBrakeID   := mps.pBrakeID;
    pBrakeSysID:= mps.pBrakeSysID;
    pCatalID   := mps.pCatalID;
    pTransID   := mps.pTransID;
  end else begin
    if pYStart     <>mps.pYStart      then pYStart    := mps.pYStart;
    if pMStart     <>mps.pMStart      then pMStart    := mps.pMStart;
    if pYEnd       <>mps.pYEnd        then pYEnd      := mps.pYEnd;
    if pMEnd       <>mps.pMEnd        then pMEnd      := mps.pMEnd;
    if (pKW        <>mps.pKW)         then pKW        := mps.pKW;
    if (pHP        <>mps.pHP)         then pHP        := mps.pHP;
    if (pCCM       <>mps.pCCM)        then pCCM       := mps.pCCM;
    if (pCylinders <>mps.pCylinders)  then pCylinders := mps.pCylinders;
    if (pValves    <>mps.pValves)     then pValves    := mps.pValves;
    if (pBodyID    <>mps.pBodyID)     then pBodyID    := mps.pBodyID;
    if (pDriveID   <>mps.pDriveID)    then pDriveID   := mps.pDriveID;
    if (pEngTypeID <>mps.pEngTypeID)  then pEngTypeID := mps.pEngTypeID;
    if (pFuelID    <>mps.pFuelID)     then pFuelID    := mps.pFuelID;
    if (pFuelSupID <>mps.pFuelSupID)  then pFuelSupID := mps.pFuelSupID;
    if (pBrakeID   <>mps.pBrakeID)    then pBrakeID   := mps.pBrakeID;
    if (pBrakeSysID<>mps.pBrakeSysID) then pBrakeSysID:= mps.pBrakeSysID;
    if (pCatalID   <>mps.pCatalID)    then pCatalID   := mps.pCatalID;
    if (pTransID   <>mps.pTransID)    then pTransID   := mps.pTransID;
  end;
end;
//================================== строка редактирования параметров модели
function TModelAuto.GetModelParamsUpdSql(mps: TModelParams): string;
begin
  if not Assigned(self) then Result:= '' else with FParams do begin // update
    if (pYStart    <>mps.pYStart)     then Result:= Result+'DMOSYEARSTART='     +IntToStr(mps.pYStart)+', ';
    if (pMStart    <>mps.pMStart)     then Result:= Result+'DMOSMONTHSTART='    +IntToStr(mps.pMStart)+', ';
    if (pYEnd      <>mps.pYEnd)       then Result:= Result+'DMOSYEAREND='       +IntToStr(mps.pYEnd)+', ';
    if (pMEnd      <>mps.pMEnd)       then Result:= Result+'DMOSMONTHEND='      +IntToStr(mps.pMEnd)+', ';
    if (pKW        <>mps.pKW)         then Result:= Result+'DMOSKW='            +IntToStr(mps.pKW)+', ';
    if (pHP        <>mps.pHP)         then Result:= Result+'DMOSHP='            +IntToStr(mps.pHP)+', ';
    if (pCCM       <>mps.pCCM)        then Result:= Result+'DMOSCCM='           +IntToStr(mps.pCCM)+', ';
    if (pCylinders <>mps.pCylinders)  then Result:= Result+'DMOSCylinders='     +IntToStr(mps.pCylinders)+', ';
    if (pValves    <>mps.pValves)     then Result:= Result+'DMOSValves='        +IntToStr(mps.pValves)+', ';
    if (pBodyID    <>mps.pBodyID)     then Result:= Result+'DMOSBodyCode='      +IntToStr(mps.pBodyID)+', ';
    if (pDriveID   <>mps.pDriveID)    then Result:= Result+'DMOSDriveCode='     +IntToStr(mps.pDriveID)+', ';
    if (pEngTypeID <>mps.pEngTypeID)  then Result:= Result+'DMOSEngineTypeCode='+IntToStr(mps.pEngTypeID)+', ';
    if (pFuelID    <>mps.pFuelID)     then Result:= Result+'DMOSFuelCode='      +IntToStr(mps.pFuelID)+', ';
    if (pFuelSupID <>mps.pFuelSupID)  then Result:= Result+'DMOSFuelSupCode='   +IntToStr(mps.pFuelSupID)+', ';
    if (pBrakeID   <>mps.pBrakeID)    then Result:= Result+'DMOSBrakeCode='     +IntToStr(mps.pBrakeID)+', ';
    if (pBrakeSysID<>mps.pBrakeSysID) then Result:= Result+'DMOSBRAKESYSCODE='  +IntToStr(mps.pBrakeSysID)+', ';
    if (pCatalID   <>mps.pCatalID)    then Result:= Result+'DMOSCATALCODE='     +IntToStr(mps.pCatalID)+', ';
    if (pTransID   <>mps.pTransID)    then Result:= Result+'DMOSTRANSCODE='     +IntToStr(mps.pTransID)+', ';
  end;
end;
//============================================== список связок 2 с узлами модели
function TModelAuto.GetModelNodesList(OnlyVisible: boolean=False; flFromBase: boolean=False): TList;// must Free
// OnlyVisible=True - только видимые ноды
var i: Integer;
    links2: TLinks;
begin
  Result:= TList.Create;
  if not Assigned(self) then Exit;
  if flFromBase then links2:= GetModelNodesLinks else links2:= NodeLinks;
  with links2 do for i:= 0 to LinkCount-1 do begin
    if not OnlyVisible or TAutoTreeNode(GetLinkPtr(ListLinks[i])).Visible then
      Result.Add(ListLinks[i])
    else if flFromBase then TObject(ListLinks[i]).Free;
  end;
  if flFromBase then links2.ListLinks.Free;
end;
//=============================================== список связок 2 модели из базы
function TModelAuto.GetModelNodesLinks: TLinks; // must Free Result
const nmProc = 'GetNodesLinks';
var ORD_IBD: TIBDatabase;
    ORD_IBS: TIBSQL;
    lstLinks: TList;       // Список связок для пакетного добавления
    tnID, index, j, iCount, err2: Integer;
    pNode: TAutoTreeNode;
    haswares, hasFilters: Boolean;
    link2: TSecondLink;
    Nodes: TAutoTreeNodes;
  //---------------------- добавляем родителей / проставляем признак у родителей
  procedure AddParentLink(idp: Integer; hasw: Boolean);
  begin
    if (idp<1) then Exit; // если ноды нет в дереве - выходим
    index:= -1;
    if Nodes.NodeGet(idp, pNode) then index:= pNode.OrderNum-1;
    if (index<0) then Exit;
    if not assigned(lstLinks[index]) then  // если линка на ноду нет в списке - добавляем
      lstLinks[index]:= TSecondLink.Create(0, 0, pNode, False, hasw)
    else begin
      link2:= lstLinks[index];
      if not hasw or link2.NodeHasWares then Exit; // если признака нет или у ноды он уже есть - выходим
      link2.NodeHasWares:= hasw;
    end;
    AddParentLink(pNode.ParentID, hasw); // добавляем родителей / проставляем признак у родителей
  end;
  //----------------------------------
begin
  Result:= TLinks.Create();
  if not Assigned(self) then Exit;
  ORD_IBD:= nil;
  ORD_IBS:= nil;
  pNode:= nil;
  lstLinks:= TList.Create; // заготовка линков по кол-ву узлов системы
  err2:= 0;
  try
    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise Exception.Create(MessText(mtkErrConnectToDB));

    Nodes:= Cache.FDCA.AutoTreeNodesSys[TypeSys];
    with Nodes.NodesList do iCount:= Count; // количество нод в списке отсортированных узлов системы
    with lstLinks do begin
      Capacity:= iCount;  // заполняем заготовку линков
      for j:= 0 to iCount-1 do Add(nil);
    end;
    ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpRead, true);
    ORD_IBS.SQL.Text:= 'select * from GetModelNodes('+IntToStr(ID)+')';
    ORD_IBS.ExecQuery;
    while not ORD_IBS.Eof do begin
      tnID := ORD_IBS.FieldByName('RNode').AsInteger;  // Код узла
      index:= -1;
      if Nodes.NodeGet(tnID, pNode) and pNode.IsEnding // если нода есть и конечная
        and ((pNode.ID=pNode.MainCode) or pNode.Visible) then // нода главная или видимая дублирующая
        index:= pNode.OrderNum-1;
      if index>-1 then begin                  // признак наличия товаров
        haswares:= ORD_IBS.FieldByName('RHasWar').AsInteger=1;
        if ORD_IBS.FieldIndex['RHasFilt']>-1 then
          hasFilters:= ORD_IBS.FieldByName('RHasFilt').AsInteger=1 // признак наличия фильтров в узле модели
        else hasFilters:= False;
        lstLinks[index]:= TSecondLink.Create(ORD_IBS.FieldByName('Rsrc').AsInteger,
          ORD_IBS.FieldByName('RCount').AsFloat, pNode, True, haswares, hasFilters); // заносим связку узла
        AddParentLink(pNode.ParentID, haswares); // добавляем родителей
      end else begin
        prMessageLOGS(nmProc+': not node '+IntToStr(tnID), fLogCache+'_test', false);
        Inc(err2);
      end; // if index>-1
      cntsORD.TestSuspendException;
      ORD_IBS.Next;
    end;
    ORD_IBS.Close;

    Result.AddLinkItems(lstLinks); // заносим пакет отсортированных линков
  except
    on E: Exception do prMessageLOGS(nmProc+': '+E.Message, fLogCache);
  end;
  prFreeIBSQL(ORD_IBS);
  cntsOrd.SetFreeCnt(ORD_IBD);
  prFree(lstLinks);
  if (err2>0) then prMessageLOGS(nmProc+': no Nodes='+IntToStr(err2), fLogCache, false);
end;
//=============================================== признак 2-й связки узла модели
function TModelAuto.GetModelNodeIsSecondLink(nodeID: Integer): Boolean;
begin
  Result:= False;
  if not Assigned(self) then Exit;
  if not NodeLinks.LinkExists(nodeID) then Exit;
  Result:= TSecondLink(NodeLinks[nodeID]).IsLinkNode;
end;
//===================================== must Free, список товаров по узлу модели
function TModelAuto.GetModelNodeWaresList(nodeID: Integer; withChildNodes: boolean=True; flFromBase: boolean=False): TStringList;
const nmProc = 'GetModelNodeWaresList';
// Возвращает список товаров по узлу, при withChildNodes=True с товарами дочерних узлов
var ORD_IBD: TIBDatabase;
    ORD_IBS: TIBSQL;
    iw: Integer;
  //---------------------------------- добавляем связки с товарами по ветке дерева
  procedure AddLinkWares(idn: Integer);
  var i, idMain, iw: integer;
      lnks: TLinkList;
  begin
    if not NodeLinks.LinkExists(idn) then Exit;
    with TAutoTreeNode(GetLinkPtr(NodeLinks[idn])) do begin
      if not Visible then Exit; // товары выбираем только по видимым нодам
      idMain:= MainCode;        // связки с товарами выбираем по главной ноде
    end;
    lnks:= NodeLinks.GetDoubleLinks(idMain);
    if Assigned(lnks) then with lnks do for i:= 0 to Count-1 do begin
      iw:= GetLinkID(Items[i]);
      if Cache.WareExist(iw) then with Cache.GetWare(iw) do
        if not IsArchive then Result.AddObject(Name, Pointer(iw));
    end;
    if withChildNodes then with TAutoTreeNode(GetLinkPtr(NodeLinks[idn])) do // дочерние узлы входящей ноды
      if Assigned(Children) then with Children do
        for i:= 0 to Count-1 do AddLinkWares(TAutoTreeNode(Objects[i]).ID);
  end;
  //----------------------------------
begin
  Result:= fnCreateStringList(True, DupIgnore);
  if not Assigned(self) then Exit;

  if not flFromBase then begin
    AddLinkWares(nodeID);
    Exit;
  end;

  ORD_IBD:= nil;
  ORD_IBS:= nil;
  try  //---------------------------------------- из базы
    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise Exception.Create(MessText(mtkErrConnectToDB));
    ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpRead, true);
    ORD_IBS.SQL.Text:= 'select RWare from GetModelNodeWares('+IntToStr(ID)+', '+
      IntToStr(nodeID)+', '+fnIfStr(withChildNodes, '1', '0')+') group by RWare';
    ORD_IBS.ExecQuery;
    while not ORD_IBS.Eof do begin
      iw:= ORD_IBS.FieldByName('RWare').AsInteger;  // Код товара
      if Cache.WareExist(iw) then with Cache.GetWare(iw) do
        if not IsArchive then Result.AddObject(Name, Pointer(iw));
      cntsORD.TestSuspendException;
      ORD_IBS.Next;
    end;
  except
    on E: Exception do prMessageLOGS(nmProc+': '+E.Message, fLogCache);
  end;
  prFreeIBSQL(ORD_IBS);
  cntsOrd.SetFreeCnt(ORD_IBD);
end;
//=============================== must Free, массив кодов товаров по узлу модели
function TModelAuto.GetModelNodeWares(nodeID: Integer; withChildNodes: boolean=True; flFromBase: boolean=False): Tai;
// Возвращает массив кодов товаров по узлу, при withChildNodes=True с кодами дочерних узлов
var i: integer;
begin
  setLength(Result, 0);
  if not Assigned(self) then Exit;
  with GetModelNodeWaresList(nodeID, withChildNodes, flFromBase) do try
    if Count>0 then begin
      setLength(Result, Count);
      for i:= 0 to Count-1 do Result[i]:= Integer(Objects[i]);
    end;
  finally Free; end;  
end;
//================================== сверить links с marks, True - есть различия
function TModelAuto.CheckDiffModelMarks(marks: TStringList): Boolean;
var i, j: Integer;
begin
  Result:= True;
  if EngLinks.LinkCount>marks.Count then Exit;
  for i:= 0 to marks.Count-1 do begin
    j:= TTwoCodes(marks.Objects[i]).ID2;
    if not EngLinks.LinkExists(j) then Exit;
  end;
  Result:= False;
end;
//================================================= записать маркировки по marks
procedure TModelAuto.SetModelMarks(marks: TStringList; pUserID: integer);
var i, iORD: Integer;
    s: string;
    tdCodes: Tai;
begin
  if not Assigned(marks) then exit;
  SetLength(tdCodes, 0);
  with EngLinks do begin  // маркировки двигателей
    SetLinkStates(False);
    for i:= 0 to marks.Count-1 do try
      with TTwoCodes(marks.Objects[i]) do begin // ID1 - код TDT, ID2 - код ORD
        if (ID1<1) then raise Exception.Create('empty eng TD code');
        if (ID2<1) then ID2:= Cache.FDCA.Engines.GetIDBySubCode(ID1);
        if (ID2>0) and not Cache.FDCA.Engines.ItemExists(ID2) then ID2:= 0;
        if (ID2<1) then prAddItemToIntArray(ID1, tdCodes); // добавить код TD в массив, если его там нет
      end;
    except
      on E: Exception do prMessageLOGS('SetModelMarks: '+E.Message, fLogCache);
    end; // for

    if Length(tdCodes)>0 then CheckEnginesFromTDT(tdCodes, pUserID); // добавляем двигатели из TDT

    for i:= 0 to marks.Count-1 do try
      with TTwoCodes(marks.Objects[i]) do begin
        if (ID1<1) then Continue;          // ищем двигатель
        if (ID2<1) then ID2:= Cache.FDCA.Engines.GetIDBySubCode(ID1);
                           // если не нашли - пишем в базу короткий вариант ???
        if (ID2<1) or not Cache.FDCA.Engines.ItemExists(ID2) then begin
          if marks[i]='' then raise Exception.Create('empty eng.Mark');
          iORD:= 0; // для функции (ID2 не подходит)
          s:= Cache.FDCA.Models.SaveMarkAndGetID(iORD, ID1, ID, pUserID, marks[i]);
          if s<>'' then raise Exception.Create(s);
          ID2:= iORD;
        end;
        if (ID2<1) then Continue;
        if LinkExists(ID2) then begin
          TLink(Items[ID2]).State:= True;
          Continue;
        end;
        s:= AddModelEngLink(ID2, pUserID);
        if s<>'' then raise Exception.Create(s);
      end;
    except
      on E: Exception do prMessageLOGS('SetModelMarks: '+E.Message, fLogCache);
    end; // for
    DelNotTestedLinks;
    SortByLinkName;
  end;
  SetLength(tdCodes, 0);
end;
//=============================== Добавить связку модели с маркировкой двигателя
function TModelAuto.AddModelEngLink(pEngID, pUserID: Integer): String;
const nmProc = 'AddModelEngLink';
var ORD_IBS: TIBSQL;
    ORD_IBD: TIBDatabase;
begin
  Result:= '';
//  ORD_IBD:= nil;
  ORD_IBS:= nil;
  try
    if not Assigned(self) then raise EBOBError.Create(MessText(mtkErrAddRecord));
    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise EBOBError.Create(MessText(mtkErrConnectToDB));
    try                    // пишем в базу связку модели с маркировкой двигателя
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite, true);
      ORD_IBS.SQL.Text:= 'insert into LINKMODELSENGINES'+
        ' (LMENDMOSCODE, LMENDENGCODE, LMENSRCLECODE, LMENUSERID) values ('+
        ''+IntToStr(ID)+', '+IntToStr(pEngID)+', '+IntToStr(soTecDocBatch)+', '+
        IntToStr(pUserID)+') returning LMENCODE';
      ORD_IBS.ExecQuery;
      if (ORD_IBS.Bof and ORD_IBS.Eof) then raise Exception.Create('empty link code');
      ORD_IBS.Transaction.Commit;
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD);
    end;

    EngLinks.CheckLink(pEngID, 0, Cache.FDCA.Engines[pEngID]); // пишем в кеш
  except
    on E: EBOBError do Result:= nmProc+': '+E.Message;
    on E: Exception do begin
      Result:= MessText(mtkErrAddRecord);
      prMessageLOGS(nmProc+': error add - '+E.Message, 'import', False);
    end;
  end;
end;

//******************************************************************************
//                       TModelsAuto - Список моделей
//******************************************************************************
constructor TModelsAuto.Create;
begin
  inherited Create;
  SetLength(FarModels, 1);
  FarModels[0]:= TModelAuto.Create(0, 0, 0, 0, '');
  CS_Models:= TCriticalSection.Create;
end;
//==============================================================================
destructor TModelsAuto.Destroy;
var i: Integer;
begin
  if not Assigned(self) then Exit;
  for i:= Low(FarModels) to High(FarModels) do
    if Assigned(FarModels[i]) then try prFree(FarModels[i]); except end;
  SetLength(FarModels, 0);
  prFree(CS_Models);
  inherited Destroy;
end;
//========================================== Добавить модель с занесением в базу
function TModelsAuto.ModelAdd(var pModelID: Integer; pName: String;
         pVis, pTops: Boolean; UserID, MLineID: Integer; mps: TModelParams;
         pOrdNum: Integer=-1; pTDcode: Integer=0; marks: TStringList=nil): String;
const nmProc = 'ModelAdd';
var ORD_IBS: TIBSQL;
    ORD_IBD: TIBDatabase;
    ModelLine: TModelLine;
    Model: TModelAuto;
    ii, jj: Integer;
begin
  Result:= '';
  pModelID:= 0;
//  ORD_IBD:= nil;
  ORD_IBS:= nil;
  try
    if not Assigned(self) then raise EBOBError.Create(MessText(mtkErrAddRecord));
    if not Cache.FDCA.ModelLines.ModelLineExists(MLineID) then
      raise EBOBError.Create(MessText(mtkNotFoundModLine, IntToStr(MLineID)));

    ModelLine:= Cache.FDCA.ModelLines[MLineID];
    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise EBOBError.Create(MessText(mtkErrConnectToDB));
    try
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite, true); // пишем в базу
      ORD_IBS.SQL.Text:= 'insert into DIRMODELS ('+
        'DMOSNAME, DMOSISVISIBLE, DMOSISTOP, DMOSDRMLCODE, '+
        fnIfStr(mps.pMStart    <1, '', 'DMOSMONTHSTART, ')    +
        fnIfStr(mps.pYStart    <1, '', 'DMOSYEARSTART, ')     +
        fnIfStr(mps.pMEnd      <1, '', 'DMOSMONTHEND, ')      +
        fnIfStr(mps.pYEnd      <1, '', 'DMOSYEAREND, ')       +
        fnIfStr(mps.pKW        <1, '', 'DMOSKW, ')            +
        fnIfStr(mps.pHP        <1, '', 'DMOSHP, ')            +
        fnIfStr(mps.pCCM       <1, '', 'DMOSCCM, ')           +
        fnIfStr(mps.pCylinders <1, '', 'DMOSCYLINDERS, ')     +
        fnIfStr(mps.pValves    <1, '', 'DMOSVALVES, ')        +
        fnIfStr(mps.pBodyID    <1, '', 'DMOSBODYCODE, ')      +
        fnIfStr(mps.pDriveID   <1, '', 'DMOSDRIVECODE, ')     +
        fnIfStr(mps.pEngTypeID <1, '', 'DMOSENGINETYPECODE, ')+
        fnIfStr(mps.pFuelID    <1, '', 'DMOSFUELCODE, ')      +
        fnIfStr(mps.pFuelSupID <1, '', 'DMOSFUELSUPCODE, ')   +
        fnIfStr(mps.pBrakeID   <1, '', 'DMOSBRAKECODE, ')     +
        fnIfStr(mps.pBrakeSysID<1, '', 'DMOSBRAKESYSCODE, ')  +  // Тормозная система
        fnIfStr(mps.pCatalID   <1, '', 'DMOSCATALCODE, ')     +  // Тип катализатора
        fnIfStr(mps.pTransID   <1, '', 'DMOSTRANSCODE, ')     +  // Вид коробки передач
        fnIfStr(pTDcode        <1, '', 'DMOSTDCODE, ')        +  // код TecDoc
        'DMOSUSERID'+fnIfStr(pOrdNum<0, '', ', DMOSORDERNUM')+
        ') values (:DMOSNAME, :DMOSISVISIBLE, :DMOSISTOP, '+IntToStr(MLineID)+', '+
        fnIfStr(mps.pMStart    <1, '', IntToStr(mps.pMStart)+', ')    +
        fnIfStr(mps.pYStart    <1, '', IntToStr(mps.pYStart)+', ')    +
        fnIfStr(mps.pMEnd      <1, '', IntToStr(mps.pMEnd)+', ')      +
        fnIfStr(mps.pYEnd      <1, '', IntToStr(mps.pYEnd)+', ')      +
        fnIfStr(mps.pKW        <1, '', IntToStr(mps.pKW)+', ')        +
        fnIfStr(mps.pHP        <1, '', IntToStr(mps.pHP)+', ')        +
        fnIfStr(mps.pCCM       <1, '', IntToStr(mps.pCCM)+', ')       +
        fnIfStr(mps.pCylinders <1, '', IntToStr(mps.pCylinders)+', ') +
        fnIfStr(mps.pValves    <1, '', IntToStr(mps.pValves)+', ')    +
        fnIfStr(mps.pBodyID    <1, '', IntToStr(mps.pBodyID)+', ')    +
        fnIfStr(mps.pDriveID   <1, '', IntToStr(mps.pDriveID)+', ')   +
        fnIfStr(mps.pEngTypeID <1, '', IntToStr(mps.pEngTypeID)+', ') +
        fnIfStr(mps.pFuelID    <1, '', IntToStr(mps.pFuelID)+', ')    +
        fnIfStr(mps.pFuelSupID <1, '', IntToStr(mps.pFuelSupID)+', ') +
        fnIfStr(mps.pBrakeID   <1, '', IntToStr(mps.pBrakeID)+', ')   +
        fnIfStr(mps.pBrakeSysID<1, '', IntToStr(mps.pBrakeSysID)+', ')+
        fnIfStr(mps.pCatalID   <1, '', IntToStr(mps.pCatalID)+', ')   +
        fnIfStr(mps.pTransID   <1, '', IntToStr(mps.pTransID)+', ')   +
        fnIfStr(pTDcode        <1, '', IntToStr(pTDcode)+', ')        +
        IntToStr(UserID)+fnIfStr(pOrdNum<0, '', ', '+IntToStr(pOrdNum))+') returning DMOSCODE';
      ORD_IBS.ParamByName('DMOSNAME').AsString:= pName;
      ORD_IBS.ParamByName('DMOSISTOP').AsString:= fnIfStr(pTops, 'T', 'F');
      ORD_IBS.ParamByName('DMOSISVISIBLE').AsString:= fnIfStr(pVis, 'T', 'F');
      ORD_IBS.ExecQuery;
      if not (ORD_IBS.Bof and ORD_IBS.Eof) then
        pModelID:= ORD_IBS.fieldByName('DMOSCODE').AsInteger;
      if (pModelID<1) then raise EBOBError.Create(MessText(mtkErrAddRecord));
      ORD_IBS.Transaction.Commit;
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD);
    end;

    CS_Models.Enter;                            // пишем в кеш
    try
      Model:= TModelAuto.Create(pModelID, pTDcode, pOrdNum, MLineID, pName);
      if High(FarModels)<pModelID then begin
        jj:= Length(FarModels);            // добавляем длину массива
        SetLength(FarModels, pModelID+100);   // и инициируем элементы
        for ii:= jj to High(FarModels) do
         if ii<>pModelID then FarModels[ii]:= nil;
      end;
      FarModels[pModelID]:= Model;
      with Model do begin
        FNodeLinks.dirNodes:= Cache.FDCA.AutoTreeNodesSys[TypeSys];
        IsTop:= pTops;
        IsVisible:= pVis;
        SetModelParams(mps, True);
      end;
    finally
      CS_Models.Leave;
    end;
    with ModelLine.FMLModelsSort do Delimiter:= LCharUpdate;

    if Assigned(marks) then Model.SetModelMarks(Marks, UserID); // маркировки двигателей

  except
    on E: EBOBError do Result:= E.Message;
    on E: Exception do begin
      Result:= MessText(mtkErrAddRecord);
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
    end;
  end;
end;
//========================================== Удалить модель, с удалением из базы
function TModelsAuto.ModelDel(pModelID: Integer): String;
const nmProc = 'ModelDel';
var ORD_IBS: TIBSQL;
    ORD_IBD: TIBDatabase;
    Model: TModelAuto;
begin
  Result:= '';
//  ORD_IBD:= nil;
  ORD_IBS:= nil;
  try
    if not Assigned(self) then raise EBOBError.Create(MessText(mtkErrDelRecord));
    if not ModelExists(pModelID) then
      raise EBOBError.Create(MessText(mtkNotFoundModel, IntToStr(pModelID)));
    Model:= FarModels[pModelID];
    if Model.NodesExists then raise EBOBError.Create('Модель имеет связи с узлами.');

    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise EBOBError.Create(MessText(mtkErrConnectToDB));
    try
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite, true);
      ORD_IBS.SQL.Text:= 'delete from DIRMODELS where DMOSCODE='+IntToStr(pModelID);
      ORD_IBS.ExecQuery;
      ORD_IBS.Transaction.Commit;
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD);
    end;

    Cache.FDCA.ModelLines[Model.ModelLineID].ModelDelFromLine(pModelID);
    CS_Models.Enter;
    try
      prFree(Model);
    finally
      CS_Models.Leave;
    end;
  except
    on E: Exception do begin
      Result:= E.Message;
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
    end;
  end;
end;
//============================================== Получить ссылку на обьет модели
function TModelsAuto.GetModel(pModelID: Integer): TModelAuto;
begin
  Result:= nil;
  if not Assigned(self) then Exit;
  if ModelExists(pModelID) then Result:= FarModels[pModelID] else Result:= FarModels[0];
end;
//======================================== Проверка существования модели по коду
function TModelsAuto.ModelExists(pModelID: Integer): Boolean;
begin
  Result:= Assigned(self) and (pModelID>0) and (pModelID<length(FarModels)) and Assigned(FarModels[pModelID]);
end;
//==================================== список моделей указанного модельного ряда
function TModelsAuto.GetMLModelsList(pModelLineID: Integer): Tai; // must Free
const nmProc = 'GetListModels';
var i, j: Integer;
begin
  SetLength(Result, 0);
  if not Assigned(self) then Exit;
  SetLength(Result, Length(FarModels));
  j:= 0;
  try
    for i:= 1 to High(FarModels) do
      if (FarModels[i]<> nil) and (FarModels[i].ModelLineID=pModelLineID) then begin
        Result[j]:= i;
        inc(j);
      end;
    if Length(Result)>j then SetLength(Result, j);
  except
    on E: Exception do begin
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
      SetLength(Result, 0);
    end;
  end;
end;
//===================== устанавливает флаг проверки всем моделям модельного ряда
procedure TModelsAuto.SetStates(pState: Boolean; pModelLineID: Integer=0);
var i: Integer;
begin
  if not Assigned(self) or (length(FarModels)<2) then Exit;
  CS_Models.Enter;
  try
    for i:= 1 to High(FarModels) do
      if Assigned(FarModels[i]) then with FarModels[i] do
        if (pModelLineID<1) or (ModelLineID=pModelLineID) then State:= pState;
  finally
    CS_Models.Leave;
  end;
end;
//=================================================== Добавить маркировку в базу
function TModelsAuto.SaveMarkAndGetID(var pMarkID: Integer; pTDcode, pModelID, UserID: Integer; pName: String): String;
const nmProc = 'MarkAdd';
var ORD_IBS: TIBSQL;
    ORD_IBD: TIBDatabase;
    p: Pointer;
begin
  Result:= '';
//  ORD_IBD:= nil;
  ORD_IBS:= nil;
  try
    if not Assigned(self) then raise EBOBError.Create(MessText(mtkErrAddRecord));
    if pName='' then raise Exception.Create('empty eng.Mark');
    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise EBOBError.Create(MessText(mtkErrConnectToDB));
    pName:= copy(pName, 1, 30);
    try                                             // пишем в базу
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite, true);
      ORD_IBS.SQL.Text:= 'select EngID from GetModelEngMarkByTD('+
        IntToStr(UserID)+', '+IntToStr(pModelID)+', '+IntToStr(pTDcode)+', :TDmark)';
      ORD_IBS.ParamByName('TDmark').AsString:= pName; // маркировка двиг.
      ORD_IBS.ExecQuery;
      if (ORD_IBS.Bof and ORD_IBS.Eof) then pMarkID:= 0
      else pMarkID:= ORD_IBS.fieldByName('EngID').AsInteger; // код двиг.
      if pMarkID<1 then raise Exception.Create('empty eng.ORDcode');
      ORD_IBS.Transaction.Commit;
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD);
    end;

    p:= TEngine.Create(pMarkID, pTDcode, 0, pName);
    Cache.FDCA.Engines.CheckItem(p);
  except
    on E: EBOBError do Result:= E.Message;
    on E: Exception do begin
      Result:= MessText(mtkErrAddRecord);
      prMessageLOGS(nmProc+': error add eng.mark '+pName+' - '+E.Message, fLogCache);
    end;
  end;
end;

//******************************************************************************
//                      TEngParams - параметры двигателя
//******************************************************************************
//=========================================================== очистить параметры
procedure TEngParams.Clear;
begin
  if not Assigned(self) then Exit;
  pCompFrom   := 0;  // Компрессор * 100 от
  pCompTo     := 0;  // Компрессор * 100 до
  pRPMtorqFrom:= 0;  // Момент вращения (Nm) при [об/мин] от
  pRPMtorqTo  := 0;  // Момент вращения (Nm) при [об/мин] до
  pBore       := 0;  // Расточка * 1000
  pStroke     := 0;  // Ход поршня * 1000
  pYearFrom   := 0;  // Год выпуска от
  pYearTo     := 0;  // Год выпуска до
  pKWfrom     := 0;  // Мощность кВт от
  pRPMKWfrom  := 0;  // при [об/мин] от
  pKWto       := 0;  // Мощность кВт до
  pRPMKWto    := 0;  // при [об/мин] до
  pHPfrom     := 0;  // Мощность ЛС от
  pHPto       := 0;  // Мощность ЛС до
  pCCtecFrom  := 0;  // Тех.обьем в куб.см. от
  pCCtecTo    := 0;  // Тех.обьем в куб.см. до
  pMonFrom    := 0;  // Месяц выпуска от
  pMonTo      := 0;  // Месяц выпуска до
  pVal        := 0;  // Количество клапанов
  pCyl        := 0;  // Количество цилиндров
  pCrank      := 0;  // Кол-во подшипников коленвала
  pDesign     := 0;  // Код, Исполнение двигателя   (KT 96) (TYPEDIR=13)
  pFuelType   := 0;  // Код, Вид топлива            (KT 88) (TYPEDIR=12)
  pFuelMixt   := 0;  // Код, Система впрыска        (KT 97) (TYPEDIR=5)
  pAspir      := 0;  // Код, Подача воздуха         (KT 99) (TYPEDIR=14)
  pType       := 0;  // Код, Вид двигателя          (KT 80) (TYPEDIR=3)
  pNorm       := 0;  // Код, Норма выхлопных газов  (KT 63) (TYPEDIR=15)
  pCylDesign  := 0;  // Код, Тип привода ГРМ        (KT 79) (TYPEDIR=16)
  pManag      := 0;  // Код, Привод распредвала     (KT 77) (TYPEDIR=17)
  pValCnt     := 0;  // Код, Привод клапана         (KT 78) (TYPEDIR=18)
  pCoolType   := 0;  // Код, Тип системы охлаждения (KT 76) (TYPEDIR=19)
  pSalesDesc  := ''; // Обозначение продажи
end;

//******************************************************************************
//                      TEngine - двигатель
//******************************************************************************
constructor TEngine.Create(pID, pSubCode, pEngMFau: Integer; pName: String; WithLinks: Boolean=False);
begin
  inherited Create(pID, pSubCode, 0, pName, 0, WithLinks);
  FEngMFau:= pEngMFau;
  FParams := TEngParams.Create; // параметры двигателя
end;
//===========================================
destructor TEngine.Destroy;
begin
  prFree(FParams);
  inherited;
end;
//=============================== записать / обновить параметры двигателя в кеше
procedure TEngine.SetParams(eps: TEngParams; flFill: Boolean);
begin
  if not Assigned(self) then Exit else with FParams do if flFill then begin
    pCompFrom   := eps.pCompFrom;    // Компрессор * 100 от
    pCompTo     := eps.pCompTo;      // Компрессор * 100 до
    pRPMtorqFrom:= eps.pRPMtorqFrom; // Момент вращения (Nm) при [об/мин] от
    pRPMtorqTo  := eps.pRPMtorqTo;   // Момент вращения (Nm) при [об/мин] до
    pBore       := eps.pBore;        // Расточка * 1000
    pStroke     := eps.pStroke;      // Ход поршня * 1000
    pYearFrom   := eps.pYearFrom;    // Год выпуска от
    pYearTo     := eps.pYearTo;      // Год выпуска до
    pKWfrom     := eps.pKWfrom;      // Мощность кВт от
    pRPMKWfrom  := eps.pRPMKWfrom;   // при [об/мин] от
    pKWto       := eps.pKWto;        // Мощность кВт до
    pRPMKWto    := eps.pRPMKWto;     // при [об/мин] до
    pHPfrom     := eps.pHPfrom;      // Мощность ЛС от
    pHPto       := eps.pHPto;        // Мощность ЛС до
    pCCtecFrom  := eps.pCCtecFrom;   // Тех.обьем в куб.см. от
    pCCtecTo    := eps.pCCtecTo;     // Тех.обьем в куб.см. до
    pMonFrom    := eps.pMonFrom;     // Месяц выпуска от
    pMonTo      := eps.pMonTo;       // Месяц выпуска до
    pVal        := eps.pVal;         // Количество клапанов
    pCyl        := eps.pCyl;         // Количество цилиндров
    pCrank      := eps.pCrank;       // Кол-во подшипников коленвала
    pDesign     := eps.pDesign;      // Код, Исполнение двигателя   (KT 96) (TYPEDIR=13)
    pFuelType   := eps.pFuelType;    // Код, Вид топлива            (KT 88) (TYPEDIR=12)
    pFuelMixt   := eps.pFuelMixt;    // Код, Система впрыска        (KT 97) (TYPEDIR=5)
    pAspir      := eps.pAspir;       // Код, Подача воздуха         (KT 99) (TYPEDIR=14)
    pType       := eps.pType;        // Код, Вид двигателя          (KT 80) (TYPEDIR=3)
    pNorm       := eps.pNorm;        // Код, Норма выхлопных газов  (KT 63) (TYPEDIR=15)
    pCylDesign  := eps.pCylDesign;   // Код, Тип привода ГРМ        (KT 79) (TYPEDIR=16)
    pManag      := eps.pManag;       // Код, Привод распредвала     (KT 77) (TYPEDIR=17)
    pValCnt     := eps.pValCnt;      // Код, Привод клапана         (KT 78) (TYPEDIR=18)
    pCoolType   := eps.pCoolType;    // Код, Тип системы охлаждения (KT 76) (TYPEDIR=19)
    pSalesDesc  := eps.pSalesDesc;   // Обозначение продажи
  end else begin
    if pCompFrom    <>eps.pCompFrom    then pCompFrom   := eps.pCompFrom;    // Компрессор * 100 от
    if pCompTo      <>eps.pCompTo      then pCompTo     := eps.pCompTo;      // Компрессор * 100 до
    if pRPMtorqFrom <>eps.pRPMtorqFrom then pRPMtorqFrom:= eps.pRPMtorqFrom; // Момент вращения (Nm) при [об/мин] от
    if pRPMtorqTo   <>eps.pRPMtorqTo   then pRPMtorqTo  := eps.pRPMtorqTo;   // Момент вращения (Nm) при [об/мин] до
    if pBore        <>eps.pBore        then pBore       := eps.pBore;        // Расточка * 1000
    if pStroke      <>eps.pStroke      then pStroke     := eps.pStroke;      // Ход поршня * 1000
    if pYearFrom    <>eps.pYearFrom    then pYearFrom   := eps.pYearFrom;    // Год выпуска от
    if pYearTo      <>eps.pYearTo      then pYearTo     := eps.pYearTo;      // Год выпуска до
    if pKWfrom      <>eps.pKWfrom      then pKWfrom     := eps.pKWfrom;      // Мощность кВт от
    if pRPMKWfrom   <>eps.pRPMKWfrom   then pRPMKWfrom  := eps.pRPMKWfrom;   // при [об/мин] от
    if pKWto        <>eps.pKWto        then pKWto       := eps.pKWto;        // Мощность кВт до
    if pRPMKWto     <>eps.pRPMKWto     then pRPMKWto    := eps.pRPMKWto;     // при [об/мин] до
    if pHPfrom      <>eps.pHPfrom      then pHPfrom     := eps.pHPfrom;      // Мощность ЛС от
    if pHPto        <>eps.pHPto        then pHPto       := eps.pHPto;        // Мощность ЛС до
    if pCCtecFrom   <>eps.pCCtecFrom   then pCCtecFrom  := eps.pCCtecFrom;   // Тех.обьем в куб.см. от
    if pCCtecTo     <>eps.pCCtecTo     then pCCtecTo    := eps.pCCtecTo;     // Тех.обьем в куб.см. до
    if pMonFrom     <>eps.pMonFrom     then pMonFrom    := eps.pMonFrom;     // Месяц выпуска от
    if pMonTo       <>eps.pMonTo       then pMonTo      := eps.pMonTo;       // Месяц выпуска до
    if pVal         <>eps.pVal         then pVal        := eps.pVal;         // Количество клапанов
    if pCyl         <>eps.pCyl         then pCyl        := eps.pCyl;         // Количество цилиндров
    if pCrank       <>eps.pCrank       then pCrank      := eps.pCrank;       // Кол-во подшипников коленвала
    if pDesign      <>eps.pDesign      then pDesign     := eps.pDesign;      // Код, Исполнение двигателя   (KT 96) (TYPEDIR=13)
    if pFuelType    <>eps.pFuelType    then pFuelType   := eps.pFuelType;    // Код, Вид топлива            (KT 88) (TYPEDIR=12)
    if pFuelMixt    <>eps.pFuelMixt    then pFuelMixt   := eps.pFuelMixt;    // Код, Система впрыска        (KT 97) (TYPEDIR=5)
    if pAspir       <>eps.pAspir       then pAspir      := eps.pAspir;       // Код, Подача воздуха         (KT 99) (TYPEDIR=14)
    if pType        <>eps.pType        then pType       := eps.pType;        // Код, Вид двигателя          (KT 80) (TYPEDIR=3)
    if pNorm        <>eps.pNorm        then pNorm       := eps.pNorm;        // Код, Норма выхлопных газов  (KT 63) (TYPEDIR=15)
    if pCylDesign   <>eps.pCylDesign   then pCylDesign  := eps.pCylDesign;   // Код, Тип привода ГРМ        (KT 79) (TYPEDIR=16)
    if pManag       <>eps.pManag       then pManag      := eps.pManag;       // Код, Привод распредвала     (KT 77) (TYPEDIR=17)
    if pValCnt      <>eps.pValCnt      then pValCnt     := eps.pValCnt;      // Код, Привод клапана         (KT 78) (TYPEDIR=18)
    if pCoolType    <>eps.pCoolType    then pCoolType   := eps.pCoolType;    // Код, Тип системы охлаждения (KT 76) (TYPEDIR=19)
    if pSalesDesc   <>eps.pSalesDesc   then pSalesDesc  := eps.pSalesDesc;   // Обозначение продажи
  end;
end;
//============================================================== получить строку
function TEngine.GetStrEng(const ik: T8InfoKinds): String;
begin
  Result:= '';
  if not Assigned(self) or not Assigned(EngParams) then exit;
  with EngParams do case ik of
    ik8_1: Result:= FName;          // маркировка
    ik8_2: begin                    // строка Мощность кВт
        if (pKWfrom>0) then Result:= Result+IntToStr(pKWfrom);
        if (pKWto>0) then
          Result:= Result+fnIfStr(Result='', '', ' - ')+IntToStr(pKWto);
      end;
    ik8_3: begin                    // строка Мощность ЛС
        if (pHPfrom>0) then Result:= Result+IntToStr(pHPfrom);
        if (pHPto>0) then
          Result:= Result+fnIfStr(Result='', '', ' - ')+IntToStr(pHPto);
      end;
    ik8_4: begin                    // строка Тех.обьем в куб.см.
        if (pCCtecFrom>0) then Result:= Result+IntToStr(pCCtecFrom);
        if (pCCtecTo>0) then
          Result:= Result+fnIfStr(Result='', '', ' - ')+IntToStr(pCCtecTo);
      end;
    ik8_5: Result:= IntToStr(pCyl); // строка Количество цилиндров
    ik8_6: Result:= MfauName+' | '+Mark+' | '+EngCCstr+'куб.см. | '+EngKWstr+'квт | '+EngHPstr+'лс | '+EngCYLstr+'цил'; // WebName
    ik8_7: if Cache.FDCA.ManufAutoExist(EngMFau) then // MfauName
             Result:= Cache.FDCA.Manufacturers[EngMFau].Name;
  end;
end;
//==================================== список параметров двигателя для просмотра
function TEngine.GetViewList(Delim: String=cSpecDelim): TStringList; // must Free Result
const nmProc = 'GetViewList';
var s1, s2, sName, sValue: string;
    TS: TTypesInfoModel;
begin
  Result:= TStringList.Create;
  if not Assigned(self) or not Assigned(FParams) then exit;
  TS:= Cache.FDCA.TypesInfoModel;
  try
    if (EngMFau<1) or not Cache.FDCA.ManufAutoExist(EngMFau) then sValue:= ''
    else sValue:= Cache.FDCA.Manufacturers[EngMFau].Name;
    Result.Add('Код двигателя'+Delim+sValue+fnIfStr(sValue='', '', ' ')+Mark);
    with EngParams do begin
      if pSalesDesc<>'' then Result.Add('Обозначение продажи'+Delim+pSalesDesc);
      if (pType>0) and TS.ItemExists(pType) then                            // Код, Вид двигателя (KT 80) (TYPEDIR=3)
        Result.Add(TS.GetTypeName(pType)+Delim+TS.InfoItems[pType].Name);
      if (pYearFrom>0) or (pYearTo>0) then begin
        sName:= 'Год выпуска';
        if (pYearFrom>0) and (pYearTo>0) then sName:= sName+' от-до';
        sValue:= '';
        if (pYearFrom>0) then begin
          if pMonFrom>0 then s1:= fnMakeAddCharStr(IntToStr(pMonFrom), 2, '0')+'.' else s1:= '';
          sValue:= s1+copy(IntToStr(pYearFrom), 3, 2);
        end;
        if (pYearTo>0) then begin
          if pMonTo>0 then s1:= fnMakeAddCharStr(IntToStr(pMonTo), 2, '0')+'.' else s1:= '';
          sValue:= sValue+fnIfStr(sValue='', '', ' - ')+s1+copy(IntToStr(pYearTo), 3, 2);
        end;
        Result.Add(sName+Delim+sValue);
      end;
      if pRPMKWfrom>0 then s1:= IntToStr(pRPMKWfrom) else s1:= ''; // при [об/мин] от
      if pRPMKWto  >0 then s2:= IntToStr(pRPMKWto)   else s2:= ''; // при [об/мин] до
      if (s1='') and (s2<>'') then s1:= s2;
      if (s2='') and (s1<>'') then s2:= s1;
      if (pKWfrom>0) or (pKWto>0) then begin
        sName:= 'Мощность кВт';
        if (pRPMKWfrom>0) or (pRPMKWto>0) then sName:= sName+' при [об/мин]';
        if (pKWfrom>0) and (pKWto>0) then sName:= sName+' от-до';
        sValue:= '';
        if (pKWfrom>0) then sValue:= sValue+IntToStr(pKWfrom)+fnIfStr(s1='', '', ' / ')+s1;
        if (pKWto>0) then
          sValue:= sValue+fnIfStr(sValue='', '', ' - ')+IntToStr(pKWto)+fnIfStr(s2='', '', ' / ')+s2;
        Result.Add(sName+Delim+sValue);
      end;
      if (pHPfrom>0) or (pHPto>0) then begin
        sName:= 'Мощность ЛС';
        if (pRPMKWfrom>0) or (pRPMKWto>0) then sName:= sName+' при [об/мин]';
        if (pHPfrom>0) and (pHPto>0) then sName:= sName+' от-до';
        sValue:= '';
        if (pHPfrom>0) then sValue:= sValue+IntToStr(pHPfrom)+fnIfStr(s1='', '', ' / ')+s1;
        if (pHPto>0) then
          sValue:= sValue+fnIfStr(sValue='', '', ' - ')+IntToStr(pHPto)+fnIfStr(s2='', '', ' / ')+s2;
        Result.Add(sName+Delim+sValue);
      end;
      if (pCCtecFrom>0) or (pCCtecTo>0) then begin
        sName:= 'Тех.обьем в куб.см.';
        if (pCCtecFrom>0) and (pCCtecTo>0) then sName:= sName+' от-до';
        Result.Add(sName+Delim+EngCCstr);
      end;
      if (pCyl>0) then Result.Add('Кол-во цилиндров'+Delim+EngCYLstr);
      if (pVal>0) then Result.Add('Кол-во клапанов'+Delim+IntToStr(pVal));
      if (pRPMtorqFrom>0) or (pRPMtorqTo>0) then begin
        sName:= 'Момент вращения (Nm) при (об/мин)';
        if (pRPMtorqFrom>0) and (pRPMtorqTo>0) then sName:= sName+' от-до';
        sValue:= '';
        if (pRPMtorqFrom>0) then sValue:= sValue+IntToStr(pRPMtorqFrom);
        if (pRPMtorqTo>0) then
          sValue:= sValue+fnIfStr(sValue='', '', ' - ')+IntToStr(pRPMtorqTo);
        Result.Add(sName+Delim+sValue);
      end;
      if (pCompFrom>0) or (pCompTo>0) then begin // компрессор
        sName:= 'Степень сжатия';
        if (pCompFrom>0) and (pCompTo>0) then sName:= sName+' от-до';
        sValue:= '';
        if (pCompFrom>0) then sValue:= sValue+FormatFloat('# ##0.000', pCompFrom/100)+':1';
        if (pCompTo>0) then
          sValue:= sValue+fnIfStr(sValue='', '', ' - ')+FormatFloat('# ##0.000', pCompTo/100)+':1';
        Result.Add(sName+Delim+sValue);
      end;
      if (pBore>0)   then Result.Add('Диаметр цилиндра'+Delim+FormatFloat('# ##0.000', pBore/1000)); // Расточка
      if (pStroke>0) then Result.Add('Ход поршня'+Delim+FormatFloat('# ##0.000', pStroke/1000));
      if (pCrank>0)  then Result.Add('Кол-во подшипников коленвала'+Delim+IntToStr(pCrank));
      if (pDesign>0) and TS.ItemExists(pDesign) then                        // Код, Исполнение двигателя   (KT 96) (TYPEDIR=13)
        Result.Add(TS.GetTypeName(pDesign)+Delim+TS.InfoItems[pDesign].Name);
      if (pFuelType>0) and TS.ItemExists(pFuelType) then                    // Код, Вид топлива            (KT 88) (TYPEDIR=12)
        Result.Add(TS.GetTypeName(pFuelType)+Delim+TS.InfoItems[pFuelType].Name);
      if (pFuelMixt>0) and TS.ItemExists(pFuelMixt) then                    // Код, Система впрыска        (KT 97) (TYPEDIR=5)
        Result.Add(TS.GetTypeName(pFuelMixt)+Delim+TS.InfoItems[pFuelMixt].Name);
      if (pAspir>0) and TS.ItemExists(pAspir) then                          // Код, Подача воздуха         (KT 99) (TYPEDIR=14)
        Result.Add(TS.GetTypeName(pAspir)+Delim+TS.InfoItems[pAspir].Name);
      if (pNorm>0) and TS.ItemExists(pNorm) then                            // Код, Норма выхлопных газов  (KT 63) (TYPEDIR=15)
        Result.Add(TS.GetTypeName(pNorm)+Delim+TS.InfoItems[pNorm].Name);
      if (pCylDesign>0) and TS.ItemExists(pCylDesign) then                  // Код, Тип привода ГРМ        (KT 79) (TYPEDIR=16)
        Result.Add(TS.GetTypeName(pCylDesign)+Delim+TS.InfoItems[pCylDesign].Name);
      if (pManag>0) and TS.ItemExists(pManag) then                          // Код, Привод распредвала     (KT 77) (TYPEDIR=17)
        Result.Add(TS.GetTypeName(pManag)+Delim+TS.InfoItems[pManag].Name);
      if (pValCnt>0) and TS.ItemExists(pValCnt) then                        // Код, Привод клапана         (KT 78) (TYPEDIR=18)
        Result.Add(TS.GetTypeName(pValCnt)+Delim+TS.InfoItems[pValCnt].Name);
      if (pCoolType>0) and TS.ItemExists(pCoolType) then                    // Код, Тип системы охлаждения (KT 76) (TYPEDIR=19)
        Result.Add(TS.GetTypeName(pCoolType)+Delim+TS.InfoItems[pCoolType].Name);
    end; // with Params
  except
    on E: Exception do begin
      Result.Clear;
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
    end;
  end;
end;
//==================================================== список связок 2 двигателя
function TEngine.GetNodesLinks: TLinks; // must Free Result
const nmProc = 'GetNodesLinks';
var ORD_IBD: TIBDatabase;
    ORD_IBS: TIBSQL;
    lstLinks: TList;       // Список связок для пакетного добавления
    tnID, index, j, iCount, err2: Integer;
    pNode: TAutoTreeNode;
    haswares, hasFilters: Boolean;
    link2: TSecondLink;
    Nodes: TAutoTreeNodes;
  //---------------------- добавляем родителей / проставляем признак у родителей
  procedure AddParentLink(idp: Integer; hasw: Boolean);
  begin
    if (idp<1) then Exit; // если ноды нет в дереве - выходим
    index:= -1;
    if Nodes.NodeGet(idp, pNode) then index:= pNode.OrderNum-1;
    if (index<0) then Exit;
    if not assigned(lstLinks[index]) then  // если линка на ноду нет в списке - добавляем
      lstLinks[index]:= TSecondLink.Create(0, 0, pNode, False, hasw)
    else begin
      link2:= lstLinks[index];
      if not hasw or link2.NodeHasWares then Exit; // если признака нет или у ноды он уже есть - выходим
      link2.NodeHasWares:= hasw;
    end;
    AddParentLink(pNode.ParentID, hasw); // добавляем родителей / проставляем признак у родителей
  end;
  //----------------------------------
begin
  Result:= TLinks.Create();
  if not Assigned(self) or not EngHasNodes then Exit;
  ORD_IBD:= nil;
  ORD_IBS:= nil;
  pNode:= nil;
  lstLinks:= TList.Create; // заготовка линков по кол-ву узлов системы
  err2:= 0;
  try
    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise Exception.Create(MessText(mtkErrConnectToDB));

    Nodes:= Cache.FDCA.AutoTreeNodesSys[constIsAuto];
    with Nodes.NodesList do iCount:= Count; // количество нод в списке отсортированных узлов системы
    with lstLinks do begin
      Capacity:= iCount;  // заполняем заготовку линков
      for j:= 0 to iCount-1 do Add(nil);
    end;
    ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpRead, true);
    ORD_IBS.SQL.Text:= 'select * from GetEngNodes('+IntToStr(ID)+')';
    ORD_IBS.ExecQuery;
    while not ORD_IBS.Eof do begin
      tnID := ORD_IBS.FieldByName('RNode').AsInteger;  // Код узла
      index:= -1;
      if Nodes.NodeGet(tnID, pNode) and pNode.IsEnding // если нода есть и конечная
        and ((pNode.ID=pNode.MainCode) or pNode.Visible) then // нода главная или видимая дублирующая
        index:= pNode.OrderNum-1;
      if index>-1 then begin                  // признак наличия товаров
        haswares:= ORD_IBS.FieldByName('RHasWar').AsInteger=1;
        if ORD_IBS.FieldIndex['RHasFilt']>-1 then
          hasFilters:= ORD_IBS.FieldByName('RHasFilt').AsInteger=1 // признак наличия фильтров в узле двигателя
        else hasFilters:= False;
        lstLinks[index]:= TSecondLink.Create(ORD_IBS.FieldByName('Rsrc').AsInteger,
          ORD_IBS.FieldByName('RCount').AsFloat, pNode, True, haswares, hasFilters); // заносим связку узла
        AddParentLink(pNode.ParentID, haswares); // добавляем родителей
      end else begin
        prMessageLOGS(nmProc+': not node '+IntToStr(tnID), fLogCache+'_test', false);
        Inc(err2);
      end; // if index>-1
      cntsORD.TestSuspendException;
      ORD_IBS.Next;
    end;
    ORD_IBS.Close;

    Result.AddLinkItems(lstLinks); // заносим пакет отсортированных линков
  except
    on E: Exception do prMessageLOGS(nmProc+': '+E.Message, fLogCache);
  end;
  prFreeIBSQL(ORD_IBS);
  cntsOrd.SetFreeCnt(ORD_IBD);
  prFree(lstLinks);
  if (err2>0) then prMessageLOGS(nmProc+': no Nodes='+IntToStr(err2), fLogCache, false);
end;
{//============================================= список товаров по узлу двигателя
function TEngine.GetEngNodeWaresList(nodeID: Integer; withChildNodes: boolean=True): TStringList; // must Free Result
const nmProc = 'GetEngNodeWaresList';
// Возвращает список товаров по узлу, при withChildNodes=True с товарами дочерних узлов
var ORD_IBD: TIBDatabase;
    ORD_IBS: TIBSQL;
    i: Integer;
begin
  Result:= TStringList.Create;
  if not Assigned(self) or not EngHasNodes then Exit;
  ORD_IBD:= nil;
  ORD_IBS:= nil;
  try
    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise Exception.Create(MessText(mtkErrConnectToDB));
    ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpRead, true);
    ORD_IBS.SQL.Text:= 'select distinct RWare from GetEngNodeWares('+
      IntToStr(ID)+', '+IntToStr(nodeID)+', '+fnIfStr(withChildNodes, '1', '0')+')';
    ORD_IBS.ExecQuery;
    while not ORD_IBS.Eof do begin
      i:= ORD_IBS.FieldByName('RWare').AsInteger;  // Код товара
      if Cache.WareExist(i) then with Cache.GetWare(i) do
        if not IsArchive then Result.AddObject(Name, Pointer(i));
      cntsORD.TestSuspendException;
      ORD_IBS.Next;
    end;
    ORD_IBS.Close;
    if Result.Count>1 then Result.Sort;
  except
    on E: Exception do begin
      Result.Clear;
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
    end;
  end;
  prFreeIBSQL(ORD_IBS);
  cntsOrd.SetFreeCnt(ORD_IBD);
end;
//======================================= массив кодов товаров по узлу двигателя
function TEngine.GetEngNodeWares(nodeID: Integer; withChildNodes: boolean=True): Tai; // must Free Result
// Возвращает массив кодов товаров по узлу, при withChildNodes=True с кодами по дочерним узлам
var i: integer;
begin
  setLength(Result, 0);
  if not Assigned(self) then Exit;
  with GetEngNodeWaresList(nodeID, withChildNodes) do try
    if Count>0 then begin
      setLength(Result, Count);
      for i:= 0 to Count-1 do Result[i]:= Integer(Objects[i]);
    end;
  finally Free; end;
end;      }
//=== фильтр.список товаров с текстами и условиями к связкам 3, Objects - WareID
function TEngine.GetEngNodeWaresWithUsesByFilters(NodeID: Integer; // must Free Result
         withChildNodes: boolean=True; sFilters: String=''): TStringList;
// sFilters - коды значений критериев через запятую
const nmProc = 'GetEngNodeWaresWithUsesByFilters';
var ORD_IBD: TIBDatabase;
    ORD_IBS: TIBSQL;
    iType, i, iNode, iWare, index, iPart, pCount: integer;
    s, TypeName, str, nodeDelim, partDelim: String;
    nodes: TAutoTreeNodes;
    node: TAutoTreeNode;
    arLst: TASL;
    WareCodes: Tai;
    lst: TStringList;
    flPart0: Boolean;
begin
  Result:= TStringList.Create;
  if not Assigned(self) or not EngHasNodes then Exit;
  lst:= TStringList.Create;
  ORD_IBD:= nil;
  ORD_IBS:= nil;
  setLength(WareCodes, 0);
  try
    nodes:= Cache.FDCA.AutoTreeNodesSys[constIsAuto];
    if not nodes.NodeExists(NodeID) then
      raise Exception.Create(MessText(mtkNotFoundNode, IntToStr(NodeID)));
    node:= nodes[NodeID];

    if not node.IsEnding then str:= cWebSpace+cWebSpace+cWebSpace else str:= ''; // отступ, если несколько нод
    nodeDelim:= brcWebDelim;  // разделитель узлов / 0-й порции
    partDelim:= '---------- или ----------';      // разделитель порций
    partDelim:= brcWebBoldBlackBegin+partDelim+brcWebBoldEnd;  // жирный черный шрифт
//    partDelim:= cWebItalBegin+cWebBoldBlackBegin+partDelim+cWebBoldEnd+cWebItalEnd; // жирный черный курсив

    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise Exception.Create(MessText(mtkErrConnectToDB));

    sFilters:= StringReplace(sFilters, ' ', '', [rfReplaceAll]); // убираем все пробелы
//---------------------------------------------------------- если заданы фильтры
    if node.IsEnding and (sFilters<>'') then begin
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite, true);
      ORD_IBS.SQL.Text:= 'select RWare, Rpart, Rtype, RtypeName, Rtext'+
        ' from GetModEngFiltWaresWithUseParts('+
        IntToStr(ID)+', '+IntToStr(NodeID)+', :sFilters)';
      ORD_IBS.ParamByName('sFilters').AsString:= sFilters;
      ORD_IBS.ExecQuery;
      while not ORD_IBS.Eof do begin
        if (ORD_IBS.FieldByName('Rpart').AsInteger<0)
          and (ORD_IBS.FieldByName('Rtext').AsString<>'') then begin
          sFilters:= ORD_IBS.FieldByName('Rtext').AsString; // строка значений фильтров
          ORD_IBS.Next;
          Continue;
        end;

        iWare:= ORD_IBS.FieldByName('RWare').AsInteger; // код товара
        if not Cache.WareExist(iWare) or Cache.GetWare(iWare).IsArchive then begin
          while not ORD_IBS.Eof and (iWare=ORD_IBS.FieldByName('RWare').AsInteger) do ORD_IBS.Next;
          Continue;
        end;

        if (ORD_IBS.FieldByName('RtypeName').AsString='') then begin // если только код товара
          Result.AddObject('', Pointer(iWare));
          ORD_IBS.Next;
          Continue;
        end;

        pCount:= 0; // счетчик порций
        if lst.Count>0 then lst.Clear;
        flPart0:= False;                    // собираем тексты условий по 1-му товару
        while not ORD_IBS.Eof and (iWare=ORD_IBS.FieldByName('RWare').AsInteger) do begin
          iPart:= ORD_IBS.FieldByName('Rpart').AsInteger; // порция

          if (iPart<0) and (ORD_IBS.FieldByName('Rtext').AsString<>'') then begin
            sFilters:= ORD_IBS.FieldByName('Rtext').AsString; // строка значений фильтров
            ORD_IBS.Next;
            Continue;
          end;

          if (iPart=0) then flPart0:= True; // флаг 0-й порции (значение критерия фильтра товара)
          if pCount>0 then
            if flPart0 then begin
              lst.Add(brcWebDelim); // разделитель 0-й порции
              flPart0:= False;
            end else lst.Add(partDelim); // разделитель порций

          while not ORD_IBS.Eof and (iWare=ORD_IBS.FieldByName('RWare').AsInteger)
            and (iPart=ORD_IBS.FieldByName('Rpart').AsInteger) do begin // тексты условий 1 порции
            iType:= ORD_IBS.FieldByName('Rtype').AsInteger;
            TypeName:= ORD_IBS.FieldByName('RtypeName').AsString;
            s:= '';
            while not ORD_IBS.Eof and (iWare=ORD_IBS.FieldByName('RWare').AsInteger)
              and (iPart=ORD_IBS.FieldByName('Rpart').AsInteger)
              and (iType=ORD_IBS.FieldByName('Rtype').AsInteger) do begin // тексты 1 типа (критерия)
              s:= s+fnIfStr(s='', '', ', ')+ORD_IBS.FieldByName('Rtext').AsString;
              cntsORD.TestSuspendException;
              ORD_IBS.Next;
            end;
            s:= str+TypeName+fnIfStr(s='', '', ': '+s);  // строка по 1-му типу текста
            if (iPart=0) then  // выделяем строки 0-й порции
              s:= brcWebColorRedBegin+s+brcWebColorEnd; // красный шрифт
            lst.Add(s);
          end; // while not ORD_IBS.Eof and (wareID=... and (iPart=

          inc(pCount); // счетчик порций
        end; // while not ORD_IBS.Eof and (wareID=

        Result.AddObject(lst.Text, Pointer(iWare));
      end; // while not ORD_IBS.Eof
      ORD_IBS.Close;

      if Result.Count>1 then Result.CustomSort(ObjWareNameSortCompare); // сортировка по наименованию товара

//------------------------------------------------------- если не заданы фильтры
    end else begin
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpRead, true);
      ORD_IBS.SQL.Text:= 'select distinct RWare from GetEngNodeWares('+
        IntToStr(ID)+', '+IntToStr(nodeID)+', '+fnIfStr(withChildNodes, '1', '0')+')';
      ORD_IBS.ExecQuery;
      while not ORD_IBS.Eof do begin
        i:= ORD_IBS.FieldByName('RWare').AsInteger;  // Код товара
        if Cache.WareExist(i) then with Cache.GetWare(i) do
          if not IsArchive then lst.AddObject(Name, Pointer(i));
        cntsORD.TestSuspendException;
        ORD_IBS.Next;
      end;
      ORD_IBS.Close;
      if lst.Count>1 then lst.Sort;
      setLength(WareCodes, lst.Count);
      for i:= 0 to lst.Count-1 do WareCodes[i]:= Integer(lst.Objects[i]);

      SetLength(arLst, Length(WareCodes));

      ORD_IBS.SQL.Text:= 'select distinct RNode, RWare, Rpart, Rtype, RtypeName, Rtext'+
        ' from GetEngNodesWaresUseParts('+IntToStr(ID)+', '+IntToStr(nodeID)+', '+
        fnIfStr(node.IsEnding, '0', '1')+ // 1 - с товарами дочерних узлов (нужные потом отберем)
        ') order by RWare, RNode, Rpart, Rtype';
      ORD_IBS.ExecQuery;
      while not ORD_IBS.Eof do begin
        iWare:= ORD_IBS.FieldByName('RWare').AsInteger;  // Код товара
        index:= fnInIntArray(iWare, WareCodes);          // ищем индекс товара в заданных кодах
        if (index<0) or not Cache.WareExist(iWare) or Cache.GetWare(iWare).IsArchive then begin
          while not ORD_IBS.Eof and (iWare=ORD_IBS.FieldByName('RWare').AsInteger) do ORD_IBS.Next;
          Continue;
        end;

        arLst[index]:= TStringList.Create; // соответствующий товару список условий
        while not ORD_IBS.Eof and (iWare=ORD_IBS.FieldByName('RWare').AsInteger) do begin
          iNode:= ORD_IBS.FieldByName('RNode').AsInteger;  // код главной конечной ноды
          if not nodes.NodeExists(iNode) then begin
            while not ORD_IBS.Eof and (iWare=ORD_IBS.FieldByName('RWare').AsInteger)
              and (iNode=ORD_IBS.FieldByName('RNode').AsInteger) do ORD_IBS.Next;
            Continue;
          end;

          if not node.IsEnding then begin                // собираем тексты условий по 1-му узлу
            if arLst[index].Count>0 then arLst[index].Add(nodeDelim);  // разделитель узлов
            s:= 'Узел - '+nodes[iNode].Name+': ';
            s:= brcWebColorBlueBegin+s+brcWebColorEnd; // синий шрифт
            arLst[index].Add(s);
          end;
          pCount:= 0; // счетчик порций
          while not ORD_IBS.Eof and (iWare=ORD_IBS.FieldByName('RWare').AsInteger)
            and (iNode=ORD_IBS.FieldByName('RNode').AsInteger) do begin
            iPart:= ORD_IBS.FieldByName('Rpart').AsInteger; // порция
            if pCount>0 then arLst[index].Add(partDelim);   // разделитель порций

            while not ORD_IBS.Eof and (iWare=ORD_IBS.FieldByName('RWare').AsInteger)
              and (iNode=ORD_IBS.FieldByName('RNode').AsInteger)
              and (iPart=ORD_IBS.FieldByName('Rpart').AsInteger) do begin

              iType:= ORD_IBS.FieldByName('Rtype').AsInteger;       // номер типа текста
              TypeName:= ORD_IBS.FieldByName('RtypeName').AsString; // название типа текста
              s:= '';
              while not ORD_IBS.Eof and (iWare=ORD_IBS.FieldByName('RWare').AsInteger)
                and (iNode=ORD_IBS.FieldByName('RNode').AsInteger)
                and (iType=ORD_IBS.FieldByName('Rtype').AsInteger) do begin
                s:= s+fnIfStr(s='', '', ', ')+ORD_IBS.FieldByName('Rtext').AsString; // текст
                cntsORD.TestSuspendException;
                ORD_IBS.Next;
              end;

              arLst[index].Add(str+TypeName+fnIfStr(s='', '', ': '+s)); // строка по 1-му типу
            end; // while ... and (iWare= ... and (iNode= ... and (iPart=

            inc(pCount); // счетчик порций
          end; // while ... and (WareID= ... and (iNode=
        end; // while ... and (WareID=
      end;
      ORD_IBS.Close;

      for i:= 0 to High(WareCodes) do // выдаем условия по товарам в заданном порядке
        if Assigned(arLst[i]) and (arLst[i].Count>0) then
          Result.AddObject(arLst[i].Text, Pointer(WareCodes[i]))
        else Result.AddObject('', Pointer(WareCodes[i]));
    end;

  except
    on E: Exception do begin
      Result.Clear;
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
    end;
  end;
  prFreeIBSQL(ORD_IBS);
  cntsOrd.SetFreeCnt(ORD_IBD);
  for i:= 0 to High(arLst) do if Assigned(arLst[i]) then prFree(arLst[i]);
  SetLength(arLst, 0);
  setLength(WareCodes, 0);
  prFree(lst);
end;

//============== список условий по заданным товарам узла двигателя для просмотра
function TEngine.GetEngNodeWareUsesView(nodeID: Integer; WareCodes: Tai; sFilters: String=''): TStringList; // must Free Result
const nmProc = 'GetEngNodeWareUsesView';
var ORD_IBD: TIBDatabase;
    ORD_IBS: TIBSQL;
    iType, i, iNode, iWare, index, iPart, pCount: integer;
    s, TypeName, str, nodeDelim, partDelim: String;
    nodes: TAutoTreeNodes;
    node: TAutoTreeNode;
    arLst: TASL;
    flPart0: Boolean;
begin
  Result:= TStringList.Create;
  if not Assigned(self) or not EngHasNodes or (Length(WareCodes)<1) then Exit;
  ORD_IBD:= nil;
  ORD_IBS:= nil;
  SetLength(arLst, Length(WareCodes));
  try
    nodes:= Cache.FDCA.AutoTreeNodesSys[constIsAuto];
    if not nodes.NodeExists(NodeID) then
      raise Exception.Create(MessText(mtkNotFoundNode, IntToStr(NodeID)));
    node:= nodes[NodeID];

    if not node.IsEnding then str:= cWebSpace+cWebSpace+cWebSpace else str:= ''; // отступ, если несколько нод
    nodeDelim:= brcWebDelim;       // разделитель узлов / 0-й порции
    partDelim:= '---------- или ----------';           // разделитель порций
    partDelim:= brcWebBoldBlackBegin+partDelim+brcWebBoldEnd;  // жирный черный шрифт
//    partDelim:= cWebItalBegin+cWebBoldBlackBegin+partDelim+cWebBoldEnd+cWebItalEnd; // жирный черный курсив

    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise Exception.Create(MessText(mtkErrConnectToDB));
    ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpRead, true);
    ORD_IBS.SQL.Text:= 'select distinct RNode, RWare, Rpart, Rtype, RtypeName, Rtext'+
      ' from GetEngNodWareFiltUses('+IntToStr(ID)+', '+IntToStr(nodeID)+', '+
      fnIfStr(node.IsEnding, '0', '1')+ // 1 - с товарами дочерних узлов (нужные потом отберем)
      ', :sFilters) order by RWare, RNode, Rpart, Rtype';
    ORD_IBS.ParamByName('sFilters').AsString:= sFilters;
    ORD_IBS.ExecQuery;
    while not ORD_IBS.Eof do begin
      iWare:= ORD_IBS.FieldByName('RWare').AsInteger;  // Код товара

      index:= fnInIntArray(iWare, WareCodes);          // ищем индекс товара в заданных кодах
      if (index<0) or not Cache.WareExist(iWare) or Cache.GetWare(iWare).IsArchive then begin
        while not ORD_IBS.Eof and (iWare=ORD_IBS.FieldByName('RWare').AsInteger) do ORD_IBS.Next;
        Continue;
      end;

      arLst[index]:= TStringList.Create; // соответствующий товару список условий
      while not ORD_IBS.Eof and (iWare=ORD_IBS.FieldByName('RWare').AsInteger) do begin

        if (ORD_IBS.FieldByName('Rpart').AsInteger<0)
          and (ORD_IBS.FieldByName('Rtext').AsString<>'') then begin
          sFilters:= ORD_IBS.FieldByName('Rtext').AsString; // строка значений фильтров
          ORD_IBS.Next;
          Continue;
        end;

        iNode:= ORD_IBS.FieldByName('RNode').AsInteger;  // код главной конечной ноды
        if not nodes.NodeExists(iNode) then begin
          while not ORD_IBS.Eof and (iWare=ORD_IBS.FieldByName('RWare').AsInteger)
            and (iNode=ORD_IBS.FieldByName('RNode').AsInteger) do ORD_IBS.Next;
          Continue;
        end;

        if not node.IsEnding then begin                // собираем тексты по 1-му узлу
          if arLst[index].Count>0 then arLst[index].Add(nodeDelim);  // разделитель узлов
          s:= 'Узел - '+nodes[iNode].Name+': ';
          s:= brcWebColorBlueBegin+s+brcWebColorEnd; // синий шрифт
          arLst[index].Add(s);
        end;
        pCount:= 0; // счетчик порций
        flPart0:= False;
        while not ORD_IBS.Eof and (iWare=ORD_IBS.FieldByName('RWare').AsInteger)
          and (iNode=ORD_IBS.FieldByName('RNode').AsInteger) do begin
          iPart:= ORD_IBS.FieldByName('Rpart').AsInteger; // порция

          if (iPart<0) and (ORD_IBS.FieldByName('Rtext').AsString<>'') then begin
            sFilters:= ORD_IBS.FieldByName('Rtext').AsString; // строка значений фильтров
            ORD_IBS.Next;
            Continue;
          end;

          if (iPart=0) then flPart0:= True; // флаг 0-й порции (значение критерия фильтра товара)
          if pCount>0 then
            if flPart0 then begin
              arLst[index].Add(brcWebDelim); // разделитель 0-й порции
              flPart0:= False;
            end else arLst[index].Add(partDelim);   // разделитель порций

          while not ORD_IBS.Eof and (iWare=ORD_IBS.FieldByName('RWare').AsInteger)
            and (iNode=ORD_IBS.FieldByName('RNode').AsInteger)
            and (iPart=ORD_IBS.FieldByName('Rpart').AsInteger) do begin

            iType:= ORD_IBS.FieldByName('Rtype').AsInteger;       // номер типа текста
            TypeName:= ORD_IBS.FieldByName('RtypeName').AsString; // название типа текста
            s:= '';
            while not ORD_IBS.Eof and (iWare=ORD_IBS.FieldByName('RWare').AsInteger)
              and (iNode=ORD_IBS.FieldByName('RNode').AsInteger)
              and (iType=ORD_IBS.FieldByName('Rtype').AsInteger) do begin
              s:= s+fnIfStr(s='', '', ', ')+ORD_IBS.FieldByName('Rtext').AsString; // текст
              cntsORD.TestSuspendException;
              ORD_IBS.Next;
            end;

            if (iPart=0) then  // выделяем строки 0-й порции
              s:= brcWebColorRedBegin+s+brcWebColorEnd; // красный шрифт
            arLst[index].Add(str+TypeName+fnIfStr(s='', '', ': '+s)); // строка по 1-му виду
          end; // while ... and (iWare= ... and (iNode= ... and (iPart=

          inc(pCount); // счетчик порций
        end; // while ... and (WareID= ... and (iNode=
      end; // while ... and (WareID=
    end;
    ORD_IBS.Close;

    for i:= 0 to High(WareCodes) do // выдаем условия по товарам в заданном порядке
      if Assigned(arLst[i]) and (arLst[i].Count>0) then
        Result.AddObject(arLst[i].Text, Pointer(WareCodes[i]));
  except
    on E: Exception do begin
      Result.Clear;
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
    end;
  end;
  prFreeIBSQL(ORD_IBS);
  cntsOrd.SetFreeCnt(ORD_IBD);
  for i:= 0 to High(arLst) do if Assigned(arLst[i]) then prFree(arLst[i]);
  SetLength(arLst, 0);
end;

//******************************************************************************
//                      TEngines - двигатели
//******************************************************************************
//=================================================== получить двигатель по коду
function TEngines.GetEngine(engID: integer): TEngine;
begin
  if not Assigned(self) then Result:= nil else Result:= DirItems[engID];
end;
//====================================================== найти двигатель по коду
function TEngines.FindEngineByTDcode(engTD: integer; var eng: TEngine): Boolean;
var i: integer;
begin
  eng:= nil;
  i:= GetIDBySubCode(engTD);
  if i>-1 then eng:= DirItems[i];
  Result:= Assigned(eng);
end;
//=========================================================== добавить двигатель
function TEngines.AddEngine(var pID: Integer; pSys, pTDcode, pMFau, pUserID: Integer;
                             pMark: String; eps: TEngParams): String;
const nmProc = 'AddEngine';
var ORD_IBD: TIBDatabase;
    ORD_IBS: TIBSQL;
    p: Pointer;
    s: string;
begin
  Result:= '';
  pID:= 0;
  if not Assigned(self) then Exit;
//  ORD_IBD:= nil;
  ORD_IBS:= nil;
  try
    if (pMark='') or (pUserID<1) then raise EBOBError.Create(MessText(mtkNotEnoughParams));
    ORD_IBD:= cntsOrd.GetFreeCnt;
    if not Assigned(ORD_IBD) then raise EBOBError.Create(MessText(mtkErrConnectToDB));
    try
      ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite, true);
      ORD_IBS.SQL.Add('insert into DIRENGINES (DENGTDCODE, DENGENGINEMARK, DENGDTSYCODE,');
      if (pMFau>0) then ORD_IBS.SQL.Add('DENGMFAU,');
      if Assigned(eps) then with eps do begin
        if (pYearFrom>0)    then ORD_IBS.SQL.Add('DENGMODFR,');
        if (pYearTo>0)      then ORD_IBS.SQL.Add('DENGMODTO,');
        if (pCCtecFrom>0)   then ORD_IBS.SQL.Add('DENGCCTECFR,');
        if (pCCtecTo>0)     then ORD_IBS.SQL.Add('DENGCCTECTO,');
        if (pHPfrom>0)      then ORD_IBS.SQL.Add('DENGHPFR,');
        if (pHPto>0)        then ORD_IBS.SQL.Add('DENGHPTO,');
        if (pRPMKWFrom>0)   then ORD_IBS.SQL.Add('DENGRPMKWFR,');
        if (pRPMKWTo>0)     then ORD_IBS.SQL.Add('DENGRPMKWTO,');
        if (pKWFrom>0)      then ORD_IBS.SQL.Add('DENGKWFR,');
        if (pKWTo>0)        then ORD_IBS.SQL.Add('DENGKWTO,');
        if (pCompFrom>0)    then ORD_IBS.SQL.Add('DENGCOMPFR,');
        if (pCompTo>0)      then ORD_IBS.SQL.Add('DENGCOMPTO,');
        if (pRPMtorqFrom>0) then ORD_IBS.SQL.Add('DENGRPMTORQFR,');
        if (pRPMtorqTo>0)   then ORD_IBS.SQL.Add('DENGRPMTORQTO,');
        if (pStroke>0)      then ORD_IBS.SQL.Add('DENGSTROKE,');
        if (pBore>0)        then ORD_IBS.SQL.Add('DENGBORE,');
        if (pVal>0)         then ORD_IBS.SQL.Add('DENGVAL,');
        if (pCyl>0)         then ORD_IBS.SQL.Add('DENGCYL,');
        if (pCrank>0)       then ORD_IBS.SQL.Add('DENGCRANK,');
        if (pDesign>0)      then ORD_IBS.SQL.Add('DENGDESIGN,');
        if (pFuelType>0)    then ORD_IBS.SQL.Add('DENGFUELTYPE,');
        if (pFuelMixt>0)    then ORD_IBS.SQL.Add('DENGFUELMIXT,');
        if (pAspir>0)       then ORD_IBS.SQL.Add('DENGASPIR,');
        if (pType>0)        then ORD_IBS.SQL.Add('DENGTYPE,');
        if (pNorm>0)        then ORD_IBS.SQL.Add('DENGNORM,');
        if (pCylDesign>0)   then ORD_IBS.SQL.Add('DENGCYLDESIGN,');
        if (pManag>0)       then ORD_IBS.SQL.Add('DENGMANAG,');
        if (pValCnt>0)      then ORD_IBS.SQL.Add('DENGVALCNT,');
        if (pCoolType>0)    then ORD_IBS.SQL.Add('DENGCOOLTYPE,');
        if (pSalesDesc<>'') then ORD_IBS.SQL.Add('DENGSALESDESC,');
      end;
      ORD_IBS.SQL.Add('DENGUSERID) values ('+IntToStr(pTDcode)+', :pMark, '+IntToStr(pSys)+',');
      if (pMFau>0) then ORD_IBS.SQL.Add(IntToStr(pMFau)+',');
      if Assigned(eps) then with eps do begin
        if (pYearFrom>0)    then ORD_IBS.SQL.Add(IntToStr(pYearFrom*100+pMonFrom)+','); // Год выпуска от
        if (pYearTo>0)      then ORD_IBS.SQL.Add(IntToStr(pYearTo*100+pMonTo)+',');     // Год выпуска до
        if (pCCtecFrom>0)   then ORD_IBS.SQL.Add(IntToStr(pCCtecFrom)+',');   // Тех.обьем в куб.см. от
        if (pCCtecTo>0)     then ORD_IBS.SQL.Add(IntToStr(pCCtecTo)+',');     // Тех.обьем в куб.см. до
        if (pHPfrom>0)      then ORD_IBS.SQL.Add(IntToStr(pHPfrom)+',');      // Мощность ЛС от
        if (pHPto>0)        then ORD_IBS.SQL.Add(IntToStr(pHPto)+',');        // Мощность ЛС до
        if (pRPMKWFrom>0)   then ORD_IBS.SQL.Add(IntToStr(pRPMKWFrom)+',');   // при [об/мин] от
        if (pRPMKWTo>0)     then ORD_IBS.SQL.Add(IntToStr(pRPMKWTo)+',');     // при [об/мин] до
        if (pKWFrom>0)      then ORD_IBS.SQL.Add(IntToStr(pKWFrom)+',');      // Мощность кВт от
        if (pKWTo>0)        then ORD_IBS.SQL.Add(IntToStr(pKWTo)+',');        // Мощность кВт до
        if (pCompFrom>0)    then ORD_IBS.SQL.Add(IntToStr(pCompFrom)+',');    // Компрессор * 100 от
        if (pCompTo>0)      then ORD_IBS.SQL.Add(IntToStr(pCompTo)+',');      // Компрессор * 100 до
        if (pRPMtorqFrom>0) then ORD_IBS.SQL.Add(IntToStr(pRPMtorqFrom)+','); // Момент вращения (Nm) при [об/мин] от
        if (pRPMtorqTo>0)   then ORD_IBS.SQL.Add(IntToStr(pRPMtorqTo)+',');   // Момент вращения (Nm) при [об/мин] до
        if (pStroke>0)      then ORD_IBS.SQL.Add(IntToStr(pStroke)+',');      // Ход поршня * 1000
        if (pBore>0)        then ORD_IBS.SQL.Add(IntToStr(pBore)+',');        // Расточка * 1000
        if (pVal>0)         then ORD_IBS.SQL.Add(IntToStr(pVal)+',');         // Количество клапанов
        if (pCyl>0)         then ORD_IBS.SQL.Add(IntToStr(pCyl)+',');         // Количество цилиндров
        if (pCrank>0)       then ORD_IBS.SQL.Add(IntToStr(pCrank)+',');       // Кол-во подшипников коленвала
        if (pDesign>0)      then ORD_IBS.SQL.Add(IntToStr(pDesign)+',');      // Код, Исполнение двигателя   (KT 96) (TYPEDIR=13)
        if (pFuelType>0)    then ORD_IBS.SQL.Add(IntToStr(pFuelType)+',');    // Код, Вид топлива            (KT 88) (TYPEDIR=12)
        if (pFuelMixt>0)    then ORD_IBS.SQL.Add(IntToStr(pFuelMixt)+',');    // Код, Система впрыска        (KT 97) (TYPEDIR=5)
        if (pAspir>0)       then ORD_IBS.SQL.Add(IntToStr(pAspir)+',');       // Код, Подача воздуха         (KT 99) (TYPEDIR=14)
        if (pType>0)        then ORD_IBS.SQL.Add(IntToStr(pType)+',');        // Код, Вид двигателя          (KT 80) (TYPEDIR=3)
        if (pNorm>0)        then ORD_IBS.SQL.Add(IntToStr(pNorm)+',');        // Код, Норма выхлопных газов  (KT 63) (TYPEDIR=15)
        if (pCylDesign>0)   then ORD_IBS.SQL.Add(IntToStr(pCylDesign)+',');   // Код, Тип привода ГРМ        (KT 79) (TYPEDIR=16)
        if (pManag>0)       then ORD_IBS.SQL.Add(IntToStr(pManag)+',');       // Код, Привод распредвала     (KT 77) (TYPEDIR=17)
        if (pValCnt>0)      then ORD_IBS.SQL.Add(IntToStr(pValCnt)+',');      // Код, Привод клапана         (KT 78) (TYPEDIR=18)
        if (pCoolType>0)    then ORD_IBS.SQL.Add(IntToStr(pCoolType)+',');    // Код, Тип системы охлаждения (KT 76) (TYPEDIR=19)
        if (pSalesDesc<>'') then ORD_IBS.SQL.Add(':SALESDESC,');              // Обозначение продажи
      end;
      ORD_IBS.SQL.Add(IntToStr(pUserID)+') returning DENGCODE');
      if Length(pMark)>30 then begin
        s:= pMark;
        pMark:= copy(pMark, 1, 30);
        prMessageLOGS(nmProc+': маркировка '+s+' обрезана до '+pMark, 'import', False);
      end;
      ORD_IBS.ParamByName('pMark').AsString:= pMark;
      if Assigned(eps) then with eps do if (pSalesDesc<>'') then
        ORD_IBS.ParamByName('SALESDESC').AsString:= pSalesDesc;
      ORD_IBS.ExecQuery;
      if not (ORD_IBS.Eof and ORD_IBS.Bof) then pID:= ORD_IBS.Fields[0].AsInteger;
      if pID<1 then raise Exception.Create(MessText(mtkErrAddRecord));
      ORD_IBS.Transaction.Commit;
    finally
      prFreeIBSQL(ORD_IBS);
      cntsOrd.SetFreeCnt(ORD_IBD);
    end;

    p:= TEngine.Create(pID, pTDcode, pMFau, pMark);
    CheckItem(p);
    TEngine(p).SetParams(eps, True);

    // закачка связок с товарами ???
  except
    on E: EBOBError do Result:= E.Message;
    on E: Exception do begin
      Result:= MessText(mtkErrAddRecord);
      prMessageLOGS(nmProc+': '+E.Message, 'import', False);
    end;
  end;
end;
//=========================================================== изменить двигатель
function TEngines.EditEngine(pID, pTDcode, pMFau, pUserID: Integer;
                             pMark: String; eps: TEngParams): String;
const nmProc = 'EditEngine';
var ORD_IBD: TIBDatabase;
    ORD_IBS: TIBSQL;
    lst: TStringList;
    eng: TEngine;
    s: String;
begin
//  Result:= '';
  if not Assigned(self) then Exit;
  s:= '';
  ORD_IBS:= nil;
  try
    if (pID<1) or not ItemExists(pID) then
      raise EBOBError.Create(MessText(mtkNotFoundEngine, IntToStr(pID)));
    lst:= TStringList.Create;
    try
      eng:= DirItems[pID]; //------------------ проверяем, надо ли что-то менять
      if (eng.TDCode<>pTDcode) then lst.Add('DENGTDCODE='+IntToStr(pTDcode)+',');
      if (eng.EngMFau<>pMFau) then lst.Add('DENGMFAU='+IntToStr(pMFau)+',');

      if Length(pMark)>30 then begin
        s:= pMark;
        pMark:= copy(pMark, 1, 30);
      end;
      if (eng.Mark<>pMark) then begin
        if s<>'' then prMessageLOGS(nmProc+': маркировка '+s+' обрезана до '+pMark, 'import', False);
        lst.Add('DENGENGINEMARK=:mark,');
      end;

      if Assigned(eps) then with eng.EngParams do begin
        if (eps.pYearFrom<>pYearFrom) or (eps.pMonFrom<>pMonFrom) then
          lst.Add('DENGMODFR='+IntToStr(eps.pYearFrom*100+eps.pMonFrom)+',');
        if (eps.pYearTo<>pYearTo) or (eps.pMonTo<>pMonTo) then
          lst.Add('DENGMODTO='+IntToStr(eps.pYearTo*100+eps.pMonTo)+',');
        if (eps.pRPMtorqFrom<>pRPMtorqFrom) then lst.Add('DENGRPMTORQFR='+IntToStr(eps.pRPMtorqFrom)+',');
        if (eps.pRPMtorqTo<>pRPMtorqTo) then lst.Add('DENGRPMTORQTO='+IntToStr(eps.pRPMtorqTo)+',');
        if (eps.pCCtecFrom<>pCCtecFrom) then lst.Add('DENGCCTECFR='+IntToStr(eps.pCCtecFrom)+',');
        if (eps.pCCtecTo<>pCCtecTo)     then lst.Add('DENGCCTECTO='+IntToStr(eps.pCCtecTo)+',');
        if (eps.pHPfrom<>pHPfrom)       then lst.Add('DENGHPFR='+IntToStr(eps.pHPfrom)+',');
        if (eps.pHPto<>pHPto)           then lst.Add('DENGHPTO='+IntToStr(eps.pHPto)+',');
        if (eps.pRPMKWFrom<>pRPMKWFrom) then lst.Add('DENGRPMKWFR='+IntToStr(eps.pRPMKWFrom)+',');
        if (eps.pRPMKWTo<>pRPMKWTo)     then lst.Add('DENGRPMKWTO='+IntToStr(eps.pRPMKWTo)+',');
        if (eps.pKWFrom<>pKWFrom)       then lst.Add('DENGKWFR='+IntToStr(eps.pKWFrom)+',');
        if (eps.pKWTo<>pKWTo)           then lst.Add('DENGKWTO='+IntToStr(eps.pKWTo)+',');
        if (eps.pCompFrom<>pCompFrom)   then lst.Add('DENGCOMPFR='+IntToStr(eps.pCompFrom)+',');
        if (eps.pCompTo<>pCompTo)       then lst.Add('DENGCOMPTO='+IntToStr(eps.pCompTo)+',');
        if (eps.pStroke<>pStroke)       then lst.Add('DENGSTROKE='+IntToStr(eps.pStroke)+',');
        if (eps.pBore<>pBore)           then lst.Add('DENGBORE='+IntToStr(eps.pBore)+',');
        if (eps.pVal<>pVal)             then lst.Add('DENGVAL='+IntToStr(eps.pVal)+',');
        if (eps.pCyl<>pCyl)             then lst.Add('DENGCYL='+IntToStr(eps.pCyl)+',');
        if (eps.pCrank<>pCrank)         then lst.Add('DENGCRANK='+IntToStr(eps.pCrank)+',');
        if (eps.pDesign<>pDesign)       then lst.Add('DENGDESIGN='+IntToStr(eps.pDesign)+',');
        if (eps.pFuelType<>pFuelType)   then lst.Add('DENGFUELTYPE='+IntToStr(eps.pFuelType)+',');
        if (eps.pFuelMixt<>pFuelMixt)   then lst.Add('DENGFUELMIXT='+IntToStr(eps.pFuelMixt)+',');
        if (eps.pAspir<>pAspir)         then lst.Add('DENGASPIR='+IntToStr(eps.pAspir)+',');
        if (eps.pType<>pType)           then lst.Add('DENGTYPE='+IntToStr(eps.pType)+',');
        if (eps.pNorm<>pNorm)           then lst.Add('DENGNORM='+IntToStr(eps.pNorm)+',');
        if (eps.pCylDesign<>pCylDesign) then lst.Add('DENGCYLDESIGN='+IntToStr(eps.pCylDesign)+',');
        if (eps.pManag<>pManag)         then lst.Add('DENGMANAG='+IntToStr(eps.pManag)+',');
        if (eps.pValCnt<>pValCnt)       then lst.Add('DENGVALCNT='+IntToStr(eps.pValCnt)+',');
        if (eps.pCoolType<>pCoolType)   then lst.Add('DENGCOOLTYPE='+IntToStr(eps.pCoolType)+',');
        if (eps.pSalesDesc<>pSalesDesc) then lst.Add('DENGSALESDESC=:saledesc,');
      end;

      if lst.Count<1 then Exit; // если нечего менять - выходим

      if (pUserID<1) then raise EBOBError.Create(MessText(mtkNotEnoughParams));
      lst.Insert(0, 'update DIRENGINES set ');
      lst.Add('DENGUSERID='+IntToStr(pUserID)+' where DENGCODE='+IntToStr(pID));

      ORD_IBD:= cntsOrd.GetFreeCnt; //---------------------------- запись в базу
      if not Assigned(ORD_IBD) then raise EBOBError.Create(MessText(mtkErrConnectToDB));
      try
        ORD_IBS:= fnCreateNewIBSQL(ORD_IBD, 'ORD_IBS_'+nmProc, -1, tpWrite, true);
        ORD_IBS.SQL.AddStrings(lst);
        if (eng.Mark<>pMark) then ORD_IBS.ParamByName('mark').AsString:= pMark;
        if Assigned(eps) then if (eps.pSalesDesc<>eng.EngParams.pSalesDesc) then
          ORD_IBS.ParamByName('saledesc').AsString:= eps.pSalesDesc;
        ORD_IBS.ExecQuery;
        ORD_IBS.Transaction.Commit;
      finally
        prFreeIBSQL(ORD_IBS);
        cntsOrd.SetFreeCnt(ORD_IBD);
      end;
    finally
      prFree(lst);
    end;

    if (eng.TDCode<>pTDcode) then eng.FSubCode:= pTDcode; //-------------- кеш
    if (eng.EngMFau<>pMFau) then eng.FEngMFau:= pMFau;
    if (eng.Mark<>pMark) then eng.FName:= pMark;
    eng.SetParams(eps, False);
  except
    on E: EBOBError do Result:= E.Message;
    on E: Exception do begin
      Result:= MessText(mtkErrAddRecord);
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
    end;
  end;
end;
//======== список двигателей производителя (Object-TEngine)(сорт.по маркировкам)
function TEngines.GetMfauEngList(mfau: integer): TStringList; // must clear Result
const nmProc = 'GetMfauEngList';
var i: Integer;
    eng: TEngine;
begin
  Result:= TStringList.Create;
  if not Assigned(self) then Exit;
  try
    if (mfau<1) or not Cache.FDCA.ManufAutoExist(mfau) then
      raise Exception.Create(MessText(mtkNotValidParam)+' производителя, код='+IntToStr(mfau));
    Result.Capacity:= FItemsList.Count;
    for i:= 0 to FItemsList.Count-1 do begin
      eng:= FItemsList[i];
      if eng.EngMFau<>mfau then Continue;
      Result.AddObject(eng.Mark, eng);
    end;
  except
    on E: Exception do begin
      Result.Clear;
      prMessageLOGS(nmProc+': '+E.Message, fLogCache);
    end;
  end;
  if Result.Count>1 then Result.Sort;
end;

//=================================== Формирование строки для сортировки списков
function GetNameForSort(pName: String; pY, pM: Integer): String;
const nmProc = 'GetNameForSort';
var s1, s2: String;
begin
  Result:= '';
  try
    if pY>0 then s1:= IntToStr(pY) else s1:= '    ';
    if pM>0 then s2:= IntToStr(pM) else s2:= '  ';
    Result:= fnMakeAddCharStr(pName, 60, True)+
             fnMakeAddCharStr(s1, 4, True)+fnMakeAddCharStr(s2, 2, True);
  except
    on E: Exception do prMessageLOGS(nmProc+': '+E.Message, fLogCache);
  end;
end;
//=== Получение наименование товара с отбошеным префиксом Бренда и окончания с P
function GetCutNameForSearchTD(pName: String): String;
begin
  Result:= fnGetStrPart(' ', fnGetStrPart('   ', Trim(pName)), 1);
end;

//******************************************************************************
{
CREATE TABLE CURRENCYALTER (
    CRNCALTERDATA       DATE,
    CRNCALTERTIME       DATE, 
    CRNCALTERCODE       DATAID /* DATAID = INTEGER default 0 NOT NULL check (value>=0) */, 
    CRNCALTERATTRIBUTE  INTEGER

CREATE TABLE DELAYALTER (
    DELAYALTERDATA       DATE,
    DELAYALTERTIME       DATE,
    DELAYALTERFIRM       DATAID /* DATAID = INTEGER default 0 NOT NULL check (value>=0) */,
    DELAYALTERDATE       TDOCMDATE /* TDOCMDATE = DATE NOT NULL */,
    DELAYALTERATTRIBUTE  INTEGER

CREATE TABLE DEPARTMENTALTER (
    DPRTALTERDATA       DATE, 
    DPRTALTERTIME       DATE, 
    DPRTALTERCODE       DATAID /* DATAID = INTEGER default 0 NOT NULL check (value>=0) */, 
    DPRTALTERATTRIBUTE  INTEGER

CREATE TABLE FIRMALTER (
    FIRMALTERDATA       DATE, 
    FIRMALTERTIME       DATE, 
    FIRMALTERCODE       DATAID /* DATAID = INTEGER default 0 NOT NULL check (value>=0) */, 
    FIRMALTERATTRIBUTE  INTEGER

CREATE TABLE MEASUREALTER (
    MEASALTERDATA       DATE, 
    MEASALTERTIME       DATE, 
    MEASALTERCODE       DATAID /* DATAID = INTEGER default 0 NOT NULL check (value>=0) */, 
    MEASALTERATTRIBUTE  INTEGER

CREATE TABLE PERSONSALTER (
    PRSNALTERDATA       DATE, 
    PRSNALTERTIME       DATE, 
    PRSNALTERCODE       DATAID /* DATAID = INTEGER default 0 NOT NULL check (value>=0) */, 
    PRSNALTERATTRIBUTE  INTEGER

CREATE TABLE VLADFIRMALTER (
    VLADFIRMALTERDATA       DATE, 
    VLADFIRMALTERTIME       DATE, 
    VLADFIRMALTERCODE       DATAID /* DATAID = INTEGER default 0 NOT NULL check (value>=0) */, 
    VLADFIRMALTERATTRIBUTE  INTEGER

CREATE TABLE VLADFIRMDISCALTER (
    FRDCALTERDATA       DATE,
    FRDCALTERTIME       DATE,
    FRDCALTERFIRMCODE   DATAID /* DATAID = INTEGER default 0 NOT NULL check (value>=0) */,
    FRDCALTERWARECODE   DATAID /* DATAID = INTEGER default 0 NOT NULL check (value>=0) */,
    FRDCALTERDATE       TDOCMDATE /* TDOCMDATE = DATE NOT NULL */, 
    FRDCALTERATTRIBUTE  INTEGER

CREATE TABLE VLADGRALTER (
    VGALTERDATE    DATE, 
    VGALTERKODGR   INTEGER, 
    VGALTERKODPGR  INTEGER, 
    VGALTERATTR    INTEGER, 
    USERNAME       VARCHAR(20), 
    ID             DATAID /* DATAID = INTEGER default 0 NOT NULL check (value>=0) */

методы отгрузки
CREATE TABLE SHIPMENTMETHODS (
    SHMHCODE       DATAID NOT NULL /* DATAID = INTEGER default 0 NOT NULL check (value>=0) */, 
    SHMHARCHIVE    BOOLEANGB /* BOOLEANGB = CHAR(1) default 'F' NOT NULL check(value in ('T', 'F')) */, 
    SHMHNAME       VARCHAR(30) NOT NULL, 
    SHMHUPPERNAME  VARCHAR(30) NOT NULL, 
    SHMHTIMEKEY    BOOLEANGB /* BOOLEANGB = CHAR(1) default 'F' NOT NULL check(value in ('T', 'F')) */, 
    SHMHLABELKEY   BOOLEANGB /* BOOLEANGB = CHAR(1) default 'F' NOT NULL check(value in ('T', 'F')) */

методы отгрузки по складам
CREATE TABLE DPRTSHIPMENTMETHODS (
    DPSHMHCODE        DATAID NOT NULL /* DATAID = INTEGER default 0 NOT NULL check (value>=0) */, 
    DPSHMHDPRTCODE    LINK NOT NULL /* LINK = INTEGER check (value is null or value>=0) */, 
    DPSHMHMETHODCODE  LINK NOT NULL /* LINK = INTEGER check (value is null or value>=0) */

времена отгрузки
CREATE TABLE SHIPMENTTIMES (
    SHTICODE       DATAID NOT NULL /* DATAID = INTEGER default 0 NOT NULL check (value>=0) */,
    SHTIARCHIVE    BOOLEANGB /* BOOLEANGB = CHAR(1) default 'F' NOT NULL check(value in ('T', 'F')) */, 
    SHTIHOUR       INTEGER NOT NULL,
    SHTIMINUTE     INTEGER NOT NULL, 
    SHTINAME       VARCHAR(30) NOT NULL, 
    SHTIUPPERNAME  VARCHAR(30) NOT NULL

alter TABLE ORDERSREESTR add ORDRSHIPMETHODCODE LINK, 
  add ORDRSHIPTIMECODE LINK, add ORDRSHIPDATE DATE;

CREATE TABLE FIRMLABELREESTR (
    FRLBCODE          DATAID /* DATAID = INTEGER default 0 NOT NULL check (value>=0) */,
    FRLBARCHIVE       BOOLEANGB /* BOOLEANGB = CHAR(1) default 'F' NOT NULL check(value in ('T', 'F')) */, 
    FRLBNAME          VARCHAR(40) NOT NULL, 
    FRLBUPPERNAME     VARCHAR(40) NOT NULL, 
    FRLBFIRMCODE      LINK /* LINK = INTEGER check (value is null or value>=0) */, 
    FRLBFIRMNAME      VARCHAR(100), 
    FRLBFACENAME      VARCHAR(100) NOT NULL, 
    FRLBPHONE         VARCHAR(100), 
    FRLBCARRIER       VARCHAR(100), 
    FRLBDELIVERYTIME  VARCHAR(100), 
    FRLBFEEDBACK      VARCHAR(100), 
    FRLBCOMMENT       VARCHAR(255), 
    FRLBLOCKCHECKKEY  BOOLEANGB /* BOOLEANGB = CHAR(1) default 'F' NOT NULL check(value in ('T', 'F')) */, 
    FRLBDPRTCODE      LINK /* LINK = INTEGER check (value is null or value>=0) */, 
    FRLBSUBJTYPE      INTEGER NOT NULL,
    FRLBSUBJCODE      INTEGER NOT NULL
}
end.

